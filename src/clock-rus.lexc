! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ================= !
! The Russian clock !
! ================= !


! 5:00 пять часов
! 6:20 двадцать минут седьмого
! 9:30 половина десятого
! 9:30 полдесятого
! 4:45 без четверти пять
! 4:45 без пятна́дцати пять

! 6:20 двадцать минут седьмого



Multichar_Symbols

@U.TIME.1@
@U.TIME.2@
@U.TIME.3@
@U.TIME.4@
@U.TIME.5@
@U.TIME.6@
@U.TIME.7@
@U.TIME.8@
@U.TIME.9@
@U.TIME.10@
@U.TIME.11@
@U.TIME.12@

+Use/NG


LEXICON Root

BEFORE ; ! 31-59
!HALF ;
REST ; ! 1-30

LEXICON BEFORE
:без%    BEFOREHOURS ;

LEXICON BEFOREHOURS
%01@U.TIME.2@:@U.TIME.2@     BEF ;    !01:31 - 01:59
%02@U.TIME.3@:@U.TIME.3@     BEF ;    !02:31 - 02:59
%03@U.TIME.4@:@U.TIME.4@     BEF ;    !03:31 - 03:59
%04@U.TIME.5@:@U.TIME.5@     BEF ;    !04:31 - 04:59
%05@U.TIME.6@:@U.TIME.6@     BEF ;    !05:31 - 05:59
%06@U.TIME.7@:@U.TIME.7@     BEF ;    !06:31 - 06:59
%07@U.TIME.8@:@U.TIME.8@     BEF ;    !07:31 - 07:59
%08@U.TIME.9@:@U.TIME.9@     BEF ;    !08:31 - 08:59
%09@U.TIME.10@:@U.TIME.10@   BEF ;  !09:31 - 09:59
1%0@U.TIME.11@:@U.TIME.11@   BEF ;  !10:31 - 10:59
11@U.TIME.12@:@U.TIME.12@    BEF ;   !11:29 - 11:59
12@U.TIME.1@:@U.TIME.1@      BEF ;     !12:29 - 12:59
13@U.TIME.2@:@U.TIME.2@      BEF ;     !13:29 - 13:59
14@U.TIME.3@:@U.TIME.3@      BEF ;     !14:29 - 14:59
15@U.TIME.4@:@U.TIME.4@      BEF ;     !15:29 - 15:59
16@U.TIME.5@:@U.TIME.5@      BEF ;     !16:29 - 16:59
17@U.TIME.6@:@U.TIME.6@      BEF ;     !17:29 - 17:59
18@U.TIME.7@:@U.TIME.7@      BEF ;     !18:29 - 18:59
19@U.TIME.8@:@U.TIME.8@      BEF ;     !19:31 - 19:59
2%0@U.TIME.9@:@U.TIME.9@     BEF ;    !20:31 - 20:59
21@U.TIME.10@:@U.TIME.10@    BEF ;   !21:31 - 21:59
22@U.TIME.11@:@U.TIME.11@    BEF ;   !22:29 - 22:59
23@U.TIME.12@:@U.TIME.12@    BEF ;   !23:29 - 23:59
%0%0@U.TIME.1@:@U.TIME.1@  BEF ; !00:29 - 00:59

LEXICON REST
%01@U.TIME.1@:@U.TIME.1@     AFT ;    !01:00 - 01:29
%02@U.TIME.2@:@U.TIME.2@     AFT ;    !02:00 - 02:29
%03@U.TIME.3@:@U.TIME.3@     AFT ;    !03:00 - 03:29
%04@U.TIME.4@:@U.TIME.4@     AFT ;    !04:00 - 04:29
%05@U.TIME.5@:@U.TIME.5@     AFT ;    !05:00 - 05:29
%06@U.TIME.6@:@U.TIME.6@     AFT ;    !06:00 - 06:29
%07@U.TIME.7@:@U.TIME.7@     AFT ;    !07:00 - 07:29
%08@U.TIME.8@:@U.TIME.8@     AFT ;    !08:00 - 08:29
%09@U.TIME.9@:@U.TIME.9@     AFT ;    !09:00 - 09:29
1%0@U.TIME.10@:@U.TIME.10@   AFT ;  !10:00 - 10:29
11@U.TIME.11@:@U.TIME.11@    AFT ;   !11:00 - 11:59
12@U.TIME.12@:@U.TIME.12@    AFT ;   !12:00 - 12:29
13@U.TIME.1@:@U.TIME.1@      AFT ;     !13:00 - 13:29
14@U.TIME.2@:@U.TIME.2@      AFT ;     !14:00 - 114:29
15@U.TIME.3@:@U.TIME.3@      AFT ;     !15:00 - 15:29
16@U.TIME.4@:@U.TIME.4@      AFT ;     !16:00 - 16:29
17@U.TIME.5@:@U.TIME.5@      AFT ;     !17:00 - 17:29
18@U.TIME.6@:@U.TIME.6@      AFT ;     !18:00 - 18:29
19@U.TIME.7@:@U.TIME.7@      AFT ;     !19:00 - 19:29
2%0@U.TIME.8@:@U.TIME.8@     AFT ;    !20:00 - 20:29
21@U.TIME.9@:@U.TIME.9@      AFT ;     !21:00 - 21:29
22@U.TIME.10@:@U.TIME.10@    AFT ;   !22:00 - 22:29
23@U.TIME.11@:@U.TIME.11@    AFT ;   !23:00 - 23:29
%0%0@U.TIME.12@:@U.TIME.12@  AFT ; !00:00 - 00:29
!24%:%0%0:двенадцать            # ;      !24:00


LEXICON BEF
 %:31:два́дцати% девя́ти%               TO ;
 %:32:два́дцати% восьми́%              TO ;
 %:33:два́дцати% семи́%              TO ;
 %:34:два́дцати% шести́%             TO ;
 %:35:два́дцати% пяти́%             TO ;
 %:36:два́дцати% четырёх%              TO ;
 %:37:два́дцати% трёх%              TO ;
 %:38:два́дцати% двух%             TO ;
 %:39:два́дцати% одно́й%              TO ;
 %:4%0:двадцати%              TO ;
 %:41:девятна́дцати%     TO ;
 %:42:восемна́дцати%    TO ;
 %:43:семна́дцати%     TO ;
 %:44:шестна́дцати%     TO ;
 %:45:четверти%            TO ;
 %:46:четырна́дцати%  TO ;
 %:47:трина́дцати%    TO ;
 %:48:двена́дцати%    TO ;
 %:49:одинна́дцати%      TO ;
 %:5%0:деся́ти%              TO ;
 %:51:девя́ти%              TO ;
 %:52:восьми́%             TO ;
 %:53:семи́%              TO ;
 %:54:шести́%              TO ;
 %:55:пя́ти%             TO ;
 %:56:четырёх%           TO ;
 %:57:трёх%             TO ;
 %:58:двух%             TO ;
 %:59:одно́й%               TO ;
 %:59+Use/NG:мину́ты%     TO ;

LEXICON AFT
%:%0%0:                    SHARP ;
 %:%01:одна%               1OVER ;
 %:%01+Use/NG:минут%     OVER ;
 %:%02:две%             2to4OVER ;
 %:%03:три%             2to4OVER ;
 %:%04:четыре%           2to4OVER ;
 %:%05:пять%             OVER ;
 %:%06:шесть%              OVER ;
 %:%07:семь%              OVER ;
 %:%08:во́семь%             OVER ;
 %:%09:де́вять%              OVER ;
 %:1%0:де́сять%               OVER ;
 %:11:одинна́дцать%       OVER ;
 %:12:двена́дцать%     OVER ;
 %:13:трина́дцать%     OVER ;
 %:14:четырна́дцать%   OVER ;
 %:15:четве́рть%                15OVER ;
 %:16:шестна́дцать%      OVER ;
 %:17:семна́дцать%      OVER ;
 %:18:восемна́дцать%     OVER ;
 %:19:девятна́дцать%      OVER ;
%:2%0:два́дцать%               OVER ;
 %:21:два́дцать% %              1OVER ;
 %:22:два́дцать% две%             2to4OVER ;
 %:23:два́дцать% три%              2to4OVER ;
 %:24:два́дцать% четы́ре%              2to4OVER ;
 %:25:два́дцать% пять%             OVER ;
 %:26:два́дцать% шесть%           OVER ;
 %:27:два́дцать% семь%             OVER ;
 %:28:два́дцать% во́семь%             OVER ;
 %:29:два́дцать% де́вять%               OVER ;
 %:3%0:полови́на%               HOUR ;

LEXICON TO ! BEFORE
@U.TIME.1@:час@U.TIME.1@ # ;                !^N^
@U.TIME.2@:два́@U.TIME.2@ # ;              !^N^
@U.TIME.3@:три@U.TIME.3@ # ;              !^N^
@U.TIME.4@:четы́ре@U.TIME.4@ # ;            !^N^
@U.TIME.5@:пять@U.TIME.5@ # ;              !^N^
@U.TIME.6@:шесть@U.TIME.6@ # ;              !^N^
@U.TIME.7@:семь@U.TIME.7@ # ;               !^N^
@U.TIME.8@:во́семь@U.TIME.8@ # ;              !^N^
@U.TIME.9@:де́вять@U.TIME.9@ # ;               !^N^
@U.TIME.10@:де́сять@U.TIME.10@ # ;              !^N^
@U.TIME.11@:оди́ннадцать@U.TIME.11@ # ;   !^N^
@U.TIME.12@:двена́дцать@U.TIME.12@  # ; !^N^


LEXICON 1OVER
 :мину́та%   HOUR ;

LEXICON 2to4OVER
 :мину́ты%   HOUR ;

LEXICON OVER
 :мину́т%   HOUR ;

LEXICON 15OVER
  HOUR ;

LEXICON HOUR
NOMHOUR ; !^N^
!LOCHOUR ; !^L^

LEXICON NOMHOUR                              !^N^
@U.TIME.1@:вто́рого@U.TIME.1@ # ;                !^N^
@U.TIME.2@:тре́тьего@U.TIME.2@ # ;              !^N^
@U.TIME.3@:четвёртого@U.TIME.3@ # ;              !^N^
@U.TIME.4@:пя́того@U.TIME.4@ # ;            !^N^
@U.TIME.5@:ше́стого@U.TIME.5@ # ;              !^N^
@U.TIME.6@:седьмо́го@U.TIME.6@ # ;              !^N^
@U.TIME.7@:во́сьмого@U.TIME.7@ # ;               !^N^
@U.TIME.8@:девя́того@U.TIME.8@ # ;              !^N^
@U.TIME.9@:деся́того@U.TIME.9@ # ;               !^N^
@U.TIME.10@:одинна́дцатого@U.TIME.10@ # ;              !^N^
@U.TIME.11@:двена́дцатого@U.TIME.11@ # ;   !^N^
@U.TIME.12@:пе́рвого@U.TIME.12@ # ; !^N^

LEXICON SHARP
@U.TIME.1@:оди́н@U.TIME.1@%  1HOUR ;
@U.TIME.2@:два́@U.TIME.2@%  2TO4HOUR ;
@U.TIME.3@:три@U.TIME.3@%  2TO4HOUR;
@U.TIME.4@:четы́ре@U.TIME.4@%  2TO4HOUR;
@U.TIME.5@:пять@U.TIME.5@%  5HOUR ;
@U.TIME.6@:шесть@U.TIME.6@%  5HOUR ;
@U.TIME.7@:семь@U.TIME.7@%  5HOUR ;
@U.TIME.8@:во́семь@U.TIME.8@%  5HOUR ;
@U.TIME.9@:де́вять@U.TIME.9@%  5HOUR ;
@U.TIME.10@:де́сять@U.TIME.10@%  5HOUR ;
@U.TIME.11@:оди́ннадцать@U.TIME.11@%  5HOUR ;
@U.TIME.12@:двена́дцать@U.TIME.12@%  5HOUR ;

LEXICON 1HOUR
:час # ;

LEXICON 2TO4HOUR
:часа́ # ;

LEXICON 5HOUR
:часо́в # ;


! Outcommented due to Oahpa. Insert again ad lib.
! LEXICON LOCHOUR                             !^L^
! @U.TIME.1@:ovttas@U.TIME.1@ # ;             !^L^
! @U.TIME.2@:guovttis@U.TIME.2@ # ;           !^L^
! @U.TIME.3@:golmmas@U.TIME.3@ # ;            !^L^
! @U.TIME.4@:njealjis@U.TIME.4@ # ;           !^L^
! @U.TIME.5@:viđas@U.TIME.5@ # ;              !^L^
! @U.TIME.6@:guđas@U.TIME.6@ # ;              !^L^
! @U.TIME.7@:čiežas@U.TIME.7@ # ;             !^L^
! @U.TIME.8@:gávccis@U.TIME.8@ # ;            !^L^
! @U.TIME.9@:ovccis@U.TIME.9@ # ;             !^L^
! @U.TIME.10@:logis@U.TIME.10@ # ;            !^L^
! @U.TIME.11@:oktanuppelogis@U.TIME.11@ # ;   !^L^
! @U.TIME.12@:guoktenuppelogis@U.TIME.12@ # ; !^L^
