! ======
! Conversion from lat to cyr
! =======

echo <<defining Cns and Vow...>>

define Vow       [ a | á | e | i | o | u | y | æ | ø | å | ä | ö | 
                   A | Á | E | I | O | U | Y | Æ | Ø | Å | Ä | Ö | 
é | ó | ú | í | à | è | ò | ù | ì | ë | ü | ï | â | ê | ô | û | î | ã | ý 
É | Ó | Ú | Í | À | È | Ò | Ù | Ì | Ë | Ü | Ï | Â | Ê | Ô | Û | Î | Ã | Ý ] ;


define Cns [ b | c | č | d | đ | f | g | h | j | k | l | m | n | ŋ | ñ | ń | 
             B | C | Č | D | Đ | F | G | H | J | K | L | M | N | Ŋ | Ñ | Ń |
             p | q | r | s | š | t | ŧ | v | w | x | z | ž |   
             P | Q | R | S | Š | T | Ŧ | V | W | X | Z | Ž ] ; 


define CyrVow    [ а | э | ы | о | у | я | е | и | ё | ю
                   А | Э | Ы | О | У | Я | Е | И | Ё | Ю ] ;

define NonPalVow [ а | э | ы | о | у | я | е | и | ё | ю ] ;

define PalVow    [ А | Э | Ы | О | У | Я | Е | И | Ё | Ю ] ;

define CyrCns   [ б | в | г | д | ж | з | й | к | л | м | н | 
                  п | р | с | т | ф | х | ц | ч | ш | щ |
                  Б | В | Г | Д | Ж | З | Й | К | Л | М | Н |
                  П | Р | С | Т | Ф | Х | Ц | Ч | Ш | Щ ] ;

define Syll Cns* Vow+ Cns* ;

echo << Rules >>

define lexicalexceptions [

{Hamsun} -> {Гамсун} ,
{Sigurd} -> {Сигурд} , 
{Harald} -> {Харальд} ,
{Helsingfors} -> {Гелсингфорс}

] ;

define frontC c -> s , C -> S || _ [ i | e | æ | y ] ;
define backC  c -> k , C -> K || _ [ a | o | u ] ;
define backO  o -> u , O -> U || _ n [ g | d ] ;

define diphth ø y -> о й , Ø y -> О й , 
              e y -> е й , E y -> Є й , 
              a u -> э у , A u -> Э у ;

define shortUandO u -> у , U -> У , o -> о , O -> О || _ Cns Cns [.#.|Cns] ;


define consassim d -> 0 || n _ [.#.|Cns] ,,
                 d -> 0 || l _ [.#.|Cns] ;
#                 d -> 0 || r _ [.#.|Cns] ; # they like the -rd


define specialvow a a -> о , A a -> О ;

define specialL l -> л , L -> Л || [ o | å | a | O | Å | A ] (e|h|r) _ ;
define generalL l -> л ь , L -> Л Ь ;


define initialconsgroups [ 
   C h r -> К р ,
   G j ø -> Ё ,
   G j o -> Ю ,
   G j a -> Я ,
   S k j -> Ш ,
   S j   -> Ш , 
   S k i -> Ш и ,
   S k y -> Ш ю ,
   H j a -> Я ,
   H j e -> Е ,
   H j ø -> Ё ,
   H j o -> Ю ,
   K j   -> Х ,
   k j   -> x
   ] ;

define specialO o -> у , О -> У , u -> у , U -> У || _ [ m | n g ] ;


define silentH h -> 0 || Vow _ ;

! the big conversion rule

define Convert a -> а , A -> А , 
      b -> б ,     B -> Б , 
      c -> к ,     C -> К , 
      d -> д ,     D -> Д , 
      e -> е ,     E -> Е , 
      f -> ф ,     F -> Ф , 
      g -> г ,     G -> Г , 
      h -> х ,     H -> Х , 
      i -> и ,     I -> И , 
      j -> й ,     J -> Й , 
      k -> к ,     K -> К , 
      m -> м ,     M -> М , 
      n -> н ,     N -> Н , 
      o -> у ,     O -> У ,
      p -> п ,     P -> П , 
      q -> к ,     Q -> К , 
      r -> р ,     R -> Р , 
      s -> с ,     S -> С , 
      t -> т ,     T -> Т , 
      u -> у ,     U -> У , 
      v -> в ,     V -> В , 
      w -> в ,     W -> В , 
      x -> к с ,   X -> К С , 
      y -> ю ,     Y -> Ю , 
      z -> з ,     Z -> З , 
      ä -> я ,     Ä -> Я , 
      ö -> ё ,     Ö -> Ё , 
      æ -> я ,     Æ -> Я , 
      ø -> ё ,     Ø -> Ё ,
      å -> о ,     Å -> О , 
      č -> ч ,     Č -> Ч ,
      đ -> д ,     Đ -> Д ,
      ŋ -> ӊ ,     Ŋ -> Н ,
      š -> ш ,     Š -> Ш ,
      Ž -> ж ,     Ž -> Ж ; 
      

define CVinContext й  -> ь || CyrCns _ CyrVow ;


define iDiphthong и -> й || CyrVow _ ;

define CV й а -> я ,
          Й а -> Я ,
          й е -> е ,
          Й е -> Е ,
          й о -> ю ,
          Й о -> Ю ,
          й у -> ю ,
          Й у -> Ю ;

echo << Compile... >>

read regex [
     lexicalexceptions
 .o. frontC 
 .o. diphth     
 .o. shortUandO
 .o. backC  
 .o. backO  
 .o. consassim
 .o. specialvow
 .o. specialL
 .o. generalL
 .o. initialconsgroups
 .o. specialO
 .o. silentH
 .o. Convert
 .o. CVinContext
 .o. iDiphthong
 .o. CV
 ] ;


