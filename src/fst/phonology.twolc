!! ==> sourced from phonology-rules/twolc_header <==
!! This file is produced automatically from the script make_twolc.sh
!! Instead, edit the component file(s) in the 
!! phonology-rules/ directory and then run `sh make_twolc.sh`.


!! ==> sourced from phonology-rules/000 <==
!! ============================================================================
Alphabet
      %>     ! End of nominal stem
      %<     ! End of verbal stem
      %^F:0  ! placed immediately before fleeting vowels Fа Fе Fо Fи Fя Fа́ Fе́ Fё Fо́ Fи́ Fя́
      %^o:о %^O:о́ ! verbal prefix fleeting vowels
      %^G:0  ! Facilitates GenPl ов/ев when it "should" be zero or ей (its presence blocks the transformation rule)
      %^Z:0  ! "Zero ending" resolves to 0/ь/й (used for short-form adj's)
      %^M:0  ! Verb stem mutation
      %^D:д  ! Archiphoneme for realizing the д~жд verbal alternation (as opposed to д~ж)
      %^T:т  ! Archiphoneme for realizing the т~щ verbal alternation (as opposed to т~ч)
      %^d:д  ! Archiphoneme for verb stems with -дший past active participles (-сти 7 (-д-) )
      %^t:т  ! Archiphoneme for verb stems with -тший past active participles (-сти 7 (-т-) )
      %^R:р  ! Archiphoneme for бороть and пороть
      %^U:ь %^U:й %^U:и   ! Imperative ending (unstressed)
      %^S:й %^S:и́ %^S:0   ! Imperative ending (stressed)
      %^P:0 ! п in prefix по for comparatives
      %^A:0 ! о in prefix по for comparatives
      %^Y:ы́ ! ы́ in verbal prefix вы́- (Triggers rules for forming the imperative with %^U and %^S)

      »7     ! »
      «7     ! «
      %[%>%]
      %[%<%] ! angle brackets (this is to avoid the internal use of < and >)

      %%
      %  ! space
      %-

      ¹ ² ³ ⁴ ⁵ ⁶ ⁷ ⁸ ⁹ ⁰ ! Used to enumerate homonymous lemmas

      № 1 2 3 4 5 6 7 8 9 %0 ! numerals in compounds like 10-летие

      P S ! as in, PS

      А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
      А́         Е́ Ё́     И́           О́         У́               Ы́   Э́ Ю́ Я́
      А̀         Ѐ Ё̀     Ѝ           О̀         У̀               Ы̀   Э̀ Ю̀ Я̀
      а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я
      а́         е́ ё́     и́           о́         у́               ы́   э́ ю́ я́   ! ё́ is not currently used, since = ё
      а̀         ѐ ё̀     ѝ           о̀         у̀               ы̀   э̀ ю̀ я̀ ;

Sets

Letter = А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
         А́         Е́ Ё́     И́           О́         У́               Ы́   Э́ Ю́ Я́
         А̀         Ѐ Ё̀     Ѝ           О̀         У̀               Ы̀   Э̀ Ю̀ Я̀
         а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я
         а́         е́ ё́     и́           о́         у́               ы́   э́ ю́ я́
         а̀         ѐ ё̀     ѝ           о̀         у̀               ы̀   э̀ ю̀ я̀
         %- %  № 1 2 3 4 5 6 7 8 9 %0 ;
! Consonants
C = Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц Ч Ш Щ
    б в г д ж з й к л м н п р с т ф х ц ч ш щ ;
! Consonants, excluding { Д д Т т }
CNoDT = Б В Г Ж З Й К Л М Н П Р С Ф Х Ц Ч Ш Щ
        б в г ж з й к л м н п р с ф х ц ч ш щ ;
! Unvoiced consonants
UnvoicedC = К П С Т Ф Х Ц Ч Ш Щ
            к п с т ф х ц ч ш щ ;
! Hushers
HUSH = Ж Ш Щ Ч
       ж ш щ ч ;
! Velars
VEL = Г К Х
      г к х ;
! Paired consonants (consonants that have both a hard and soft variant, excluding Velars)
PC = Б В Д З Л М Н П Р С Т Ф
     б в д з л м н п р с т ф ;
! Paired consonants, excluding { Н н }
PCNoN = Б В Д З Л М П Р С Т Ф
        б в д з л м п р с т ф ;
! Vowels
V = А Е Ё И О У Ы Э Ю Я
    А́ Е́ Ё́ И́ О́ У́ Ы́ Э́ Ю́ Я́
    А̀ Ѐ Ё̀ Ѝ О̀ У̀ Ы̀ Э̀ Ю̀ Я̀
    а е ё и о у ы э ю я
    а́ е́ ё́ и́ о́ у́ ы́ э́ ю́ я́
    а̀ ѐ ё̀ ѝ о̀ у̀ ы̀ э̀ ю̀ я̀ ;
! Stressed vowels
StressedV = А́ Е́ Ё́ Ё И́ О́ У́ Ы́ Э́ Ю́ Я́
            а́ е́ ё́ ё и́ о́ у́ ы́ э́ ю́ я́ ;
! Unstressed vowels
UnstressedV = А Е И О У Ы Э Ю Я
              а е и о у ы э ю я ;

Definitions

End = [ а́:0 | о́:0 | а:0 | о:0 | я́:0 | ё:0 | е́:0 | я:0 | е:0 | ь:0 | й:0 ] ;
VELEndV = :VEL [ а́:0 | о́:0 | а:0 | о:0 ] %>: ;     ! тоска́, молоко́, кни́га, я́блоко
VELEndC = :VEL %>: ;                               ! уче́бник
VELEnd = [ VELEndV | VELEndC ] ;
HUSHEndV = :HUSH [ а́:0 | о́:0 | а:0 | е:0 ] %>: ;   ! моча́, плечо́, пи́ща, чудо́вище
HUSHEndC = :HUSH ( ь:0 ) %>: ;                     ! мяч, вещь
HUSHEnd = [ HUSHEndV | HUSHEndC ] ;
цEndV = :ц [ а́:0 | о́:0 | а:0 | е:0 ] %>: ;         ! рысца́, мясцо́, учи́тельница, се́рдце
цEndC = :ц %>: ;                                   ! молоде́ц
цEnd = [ цEndV | цEndC ] ;
SCEndV = :PC [ е́:0 | е:0 | ё:0 | я́:0 | я:0 ] %>: ; ! мо́ре, земля́, неде́ля
SCEndVNoN = :PCNoN [ е́:0 | е:0 | ё:0 | я́:0 | я:0 ] %>: ;
! SCEndVNoNcleanup = \%^F:0 ? :н [ е́:0 | е:0 | ё:0 | я́:0 | я:0 ] %>: ;
! SCEndVNoN = [ SCEndVNoNmostly | SCEndVNoNcleanup ] ;
FVn0 = %^F:0 V: :н [ ь:0 | я:0 ] %>: ; ! GenPl of e.g. башня and MscShortForm of e.g. столетний
SCEndC = :PC ь:0 %>: ;                                 ! конь
SCEnd = [ SCEndV | SCEndC ] ;
HCEndV = :PC [ а́:0 | о́:0 | а:0 | о:0 ] %>: ;           ! красота́, окно́, ве́ра, сло́во
HCEndC = :PC %>: ;                                     ! пол
HCEnd = [ HCEndV | HCEndC ] ;
VEndV = V: [ я́:0 | ё:0 | е́:0 | я:0 | е:0 ] %>: ;       ! судия́, остриё, житие́, Мари́я, поня́тие
VEndC = \%^F:0 V: й:0 %>: ;                            ! музе́й
VEnd = [ VEndV | VEndC ] ;
JEndV = C: %^F:0 V: й: [ я́:0 | ё:0 | я:0 | е:0 ] %>: ; ! семья́, бельё, колдунья, счастье
JEndC = C: %^F:0 V: й: %>: ;                           ! мураве́й
JEnd = [ JEndV | JEndC ] ;
jEndV = \%^F:0 V: (%^F:0 V:) й:й [ я́:0 | ё:0 | я:0 | е:0 ] %>: ; ! вайя
jEndC = \%^F:0 V: (%^F:0 V:) й:й й:0 %>: ;                       ! Гавайи (lexical form = Гавайй)
jEnd = [ jEndV | jEndC ] ;
1ConjV = V %<: ;                      ! читать, умолять, белеть, воевать, etc.?
1ConjJ = (%^F:0 V:) :ь %<: ;          ! бить, бей, бью
VerbHUSH = :HUSH (%^M:0) %<: ;        ! слышу (ю)
VyPrefix = ( [ п о | п е р е | с а м о ] ) в %^Y:ы́  ;

Rules
!! 01_base_rules.twolc
!! 02_stem_palatalization.twolc
!! 03_spellingrule-husher.twolc
!! 04_spellingrule-velar.twolc
!! 05_spellingrule-c.twolc
!! 06_fleeting-vowel-deletion.twolc
!! 07_fleeting-vowel-insertion.twolc
!! 08_nouns_ie-ii.twolc
!! 09_stress.twolc
!! Genitive plural of nouns
!! Comparative adjectives
!! Masculine short-form adjectives
!! Stem alternations
!! Vowels in nonpast endings
!! Imperatives
!! Verbal prefix fleeting vowels and voicing assimilation

! If one wants to test specific two-level rules one can add test pairs to the
! twolc files. Support for this type of testing has only recently been added, and
! works only for the Xerox tools (because the pair-testing facilities in Hfst use
! a different format, and converting from one to the other is non-trivial without
! knowledge of the alphabet).

! The test data looks like the following: (€ for positive, $ for negative)

!!€ рабо́та>у
!!€ рабо́т0>у
!!$ рабо́та>у
!!$ рабо́та>0

!! ==> sourced from phonology-rules/001 <==
!! ============================================================================
"Remove extra stress marks on stem: the stress mark closest to the end remains"
Vx:Vy <=> [ .#. | \[ %^F:0 | %>: | %<: ] ] _ [ :StressedV | [ \[ %>: ] $[ :StressedV ] ] ] ;
where Vx in ( а́ э́ о́ у́ ы́ я́ е́ ё ю́ и́ )
      Vy in ( а э о у ы я е е ю и )
    matched ;
!!€ сло́во>а́
!!€ слов0>а́
!!€ у́мн>а́
!!€ умн>а́
!!€ сёст^Fёра́>ы
!!€ сёст00р0>ы
!!€ се́м^Fе́йя́>у́
!!€ сем00ь0>ю́
!!€ прочи́та́<ем
!!€ прочита́<ем

!! ==> sourced from phonology-rules/002 <==
!! ============================================================================
"V>0: Delete lexical vowel endings (o/о́ and е are covered in other rules), and delete most fleeting vowels with syllabic endings"
Vx:0 <=> .#. ( %^P: %^A: ) [ Letter: | %^F:0 | %>: ]+ _ %>: ;
!!€ Тама́ра>а
!!€ Тама́р0>а
!!€ мо́ре>о
!!€ мо́р0>е
!!€ Петр>овна>а
!!€ Петр>овн0>а
         %^F:0 _ [ Letter ]+ (End) %>: :V ; ! This does not delete люб(о)вью, since the ь is 1st
!!€ о́к^Fоно>о́
!!€ ок00н0>о́
!!€ зе́м^Fе́ля́>е́
!!€ зем00л0>е́
         %^F:0 _ й: %<: :V ;
!!€ б^Fе́й<у́
!!€ б00ь<ю́
!!€ б^Fе́й<^Sте
!!€ б0е́й<0те
         %^F:0 _ ( :C ) %<: :л :V ( :с :ь ) .#. ;
!!€ ж^Fёг<ла́
!!€ ж00г<ла́
         %^F:0 _ ( :C ) %<: :V ;
! TODO examples to test for this rule
where Vx in ( а е́ ё и я а́ и́ я́ ) ;

!! ==> sourced from phonology-rules/003 <==
!! ============================================================================
"е>0: 1) Delete lexical vowel endings; 2) Delete fleeting vowels where ending begins with a vowel; 3) Delete е after comparative adjective stem mutation."
е:0 <=> .#. ( %^P: %^A: ) [ Letter: | %^F:0 ]+ _ %>: ;
            ! see examples in parallel rule of "V>0" above
         %^F:0 _ [ Letter ]+ (End) %>: :V ;
            ! see examples in parallel rule of "V>0" above
         [ г: | к: | х: ] %>: [ е | е́: ] _ ( %^P: ).#. ; ! After comparative adjective mutation
!!€ ли́х>е́е
!!€ ли́ш>е0
         %^F:0 _ ( :C ) %<: :л :V ( :с :ь ) .#. ;
         %^F:0 _ ( :C ) %<: :V ;

!! ==> sourced from phonology-rules/004 <==
!! ============================================================================
"[оо́]>0 in lexical endings, fleeting vowels (including verbal prefixes), and zero GenPl"
[ о:0 | о́:0 ] <=> .#. ( %^P: %^A: ) [ Letter: | %^F:0 ]+ _  %>: ; ! lexical ending
                    ! see examples in parallel rule of "V>0" above
                  %^F:0 _ [ Letter ]+ (End) %>: :V ; ! fleeting vowel
                    ! see examples in parallel rule of "V>0" above
!!€ люб^Fо́вь>ы́
!!€ люб00в0>и́
!!€ люб^Fо́вь>ью
!!€ люб0о́в0>ью
                  [ VELEndV | HUSHEndV | цEndV | HCEndV | FVn0 | JEndV | jEndV ] _ в:0 .#. ; ! zero GenPl
!!€ кни́га>ов
!!€ кни́г0>00
!!€ чудо́вище>ов
!!€ чудо́вищ0>00
!!€ пти́ца>ов
!!€ пти́ц0>00
!!€ пи́ла́>о́в
!!€ пи́л0>00
!!€ ба́ш^Fеня>ов
!!€ ба́ш0ен0>00
!!€ се́м^Fе́йя́>ов
!!€ сем0е́й0>00
!!€ ва́йя>ов
!!€ ва́й0>00
                  %^F:0 _ ( :C ) %<: :л :V ( :с :ь ) .#. ;
                  %^F:0 _ ( :C ) %<: :V ;

!! ==> sourced from phonology-rules/005 <==
!! ============================================================================
"[oO]>0 Delete verbal prefix fleeting vowels"
[ %^o:0 | %^O:0 ] <=> [ в | н а д | н а́: д | о б | о́: б | о т | о́: т | п о д | п о́: д | п р е д | п р е́: д | с | в з: | в о з: | в о́: з: | и з: | и́: з: | н и з: | н и́: з: | р а з: | р а́: з: ] _ :C (%^F:0) :V ;
!!€ с^Oго́н^M<ю́
!!€ с0гон0<ю́

!! ==> sourced from phonology-rules/006 <==
!! ============================================================================
"O>о Destress verbal prefix fleeting vowel, if there is stress to the right"
%^O:о <=> [ в | н а д | н а́: д | о б | о́: б | о т | о́: т | п о д | п о́: д | п р е д | п р е́: д | с | в з: | в о з: | в о́: з: | и з: | и́: з: | н и з: | н и́: з: | р а з: | р а́: з: ] _ [ [ ?* :StressedV ?* ] - [ :C (%^F:0) :V ?* ] ] .#. ;

!! ==> sourced from phonology-rules/007 <==
!! ============================================================================
"з>с in some verbal prefixes with dropped fleeting vowels, when the following consonant is unvoiced"
з:с <=> [ в | в о | в о́: | и | и́: | н и | н и́: | р а | р а́: ] _ [ %^o:0 | %^O:0 ] :UnvoicedC (%^F:0) :V ;
!!€ раз^oш^Fе́й<у́
!!€ разош00ь<ю́
!!€ раз^oши́л
!!€ рас0ши́л

!! ==> sourced from phonology-rules/008 <==
!! ============================================================================
"ь>0 in lexical endings, and before fleeting vowels"
ь:0 <=> .#. ( %^P: %^A: ) [ Letter: | %^F:0 ]+ _  %>: ;
!!€ ко́сть>ь
!!€ ко́ст0>ь
!!€ ми́лость>ы
!!€ ми́лост0>и
        .#. ( %^P: %^A: ) Letter+ _ %^F:0 V ;
!!€ во́сь^Fемь>ь
!!€ во́с00ем0>ь

!! ==> sourced from phonology-rules/009 <==
!! ============================================================================
"й>0 in lexical endings, and before fleeting vowels, and in comparative adjectives with velar mutation"
й:0 <=> .#. ( %^P: %^A: ) Letter+ V _  %>: ; ! in lexical ending
!!€ музе́й>а
!!€ музе́0>я
        .#. ( %^P: %^A: ) Letter+ V й:й _  %>: ; ! in lexical ending
!!€ Гава́йй>ы
!!€ Гава́й0>и
        .#. ( %^P: %^A: ) Letter+ _ %^F:0 V ; ! before fleeting vowel
!!€ копе́й^Fека>ов
!!€ копе́00ек0>00
        [ г: | к: | х: ] %>: [ е | е́: ] _ ( %^P: ) .#. ; ! comparative adjectives with velar mutation
!!€ ли́х>е́й
!!€ ли́ш>е0

!! ==> sourced from phonology-rules/010 <==
!! ============================================================================
"й>ь (1) when after a fleeting vowel and before a vowel ( муравей > муравья; семей > семья ) (2) in verbs with й-stems, such as бить (3) in imperatives of code 2 on consonant stems"
й:ь <=> :C %^F:0 V: _ ([ я́: | ё: | е́: | я: | е: ]) %>: :V ;
!!€ мурав^Fе́й>а́
!!€ мурав00ь>я́
!!€ бел^Fе́йё>о́
!!€ бел00ь0>ё
        C: %^F:0 V: _ %<: :V ;
!!€ б^Fе́й<у́
!!€ б00ь<ю́
        :C %<: _ ( т е ) ( с [ я: | ь ] ) .#. ;
!!€ вы́бурав<й
!!€ вы́бурав<ь

!! ==> sourced from phonology-rules/011 <==
!! ============================================================================
"Z>0"
%^Z:0 <=> [ VELEnd | HUSHEnd | цEnd | HCEnd | JEnd | FVn0 ] _ .#. ; ! Fem 2*a in н, e.g. столетний~столетен
!!€ но́в>^Z
!!€ но́в>0
!!€ столе́т^Fень>^Z
!!€ столе́т0ен0>0

!! ==> sourced from phonology-rules/012 <==
!! ============================================================================
"Z>ь"
%^Z:ь <=>         :PCNoN ь:0 %>: _ .#. ;
          \[%^F:0] V: н: ь:0 %>: _ .#. ;
!!€ си́нь>^Z
!!€ си́н0>ь
                   C: н: ь:0 %>: _ .#. ;

!! ==> sourced from phonology-rules/013 <==
!! ============================================================================
"Z>й"
%^Z:й <=> \%^F:0 V: й: %>: _ .#. ;
!!€ длинноше́й>^Z
!!€ длинноше́0>й

!! ==> sourced from phonology-rules/014 <==
!! ============================================================================
"о>е"
о:е <=> [ HUSHEnd | цEnd | VEnd | SCEnd | JEnd | jEnd ] _ [ ?* - [ в:0 ?* ] ] .#. ; ! в:0 occurs in zero GenPl and PastAct participles(see rule below)
!!€ хоро́ш>ому
!!€ хоро́ш>ему
!!€ па́ль^Fец>ом
!!€ па́ль00ц>ем
!!€ музе́й>ом
!!€ музе́0>ем
!!€ си́нь>ой
!!€ си́н0>ей
!!€ сча́ст^Fий>ом
!!€ сча́ст00ь>ем
!!€ ва́йя>ой
!!€ ва́й0>ей
!!€ Гава́йй>ов
!!€ Гава́й0>ев

!! ==> sourced from phonology-rules/015 <==
!! ============================================================================
"ы>и (stressed or unstressed)"
Vx:Vy <=> [ VELEnd | HUSHEnd | VEnd | SCEnd | JEnd | jEnd ] _ ;
!!€ кни́га>ы
!!€ кни́г0>и
!!€ хоро́ш>ый
!!€ хоро́ш>ий
!!€ музе́й>ы
!!€ музе́0>и
where Vx in ( ы ы́ )
      Vy in ( и и́ )
    matched;

!! ==> sourced from phonology-rules/016 <==
!! ============================================================================
"ю>у and я>а (stressed or unstressed) after a velar, husher, or ц"  ! This rule does not seem to ever apply for nominals
Vx:Vy <=> [ VELEnd | HUSHEnd | цEnd | VerbHUSH ] _ [ ?* - [ %>: ?* ] ] .#. ;
!!€ слы́ш<ю
!!€ слы́ш<у
!!€ и́ск^M<я́
!!€ и0щ0<а́
where Vx in ( ю ю́ я я́ )
      Vy in ( у у́ а а́ )
    matched;

!! ==> sourced from phonology-rules/017 <==
!! ============================================================================
"[а а́ о́] > [я я́ ё] after soft-consonant stems (including й: муравей, семья, бельё, etc) and vowel stems (музей, критерий, понятие, Мария, etc) including 1st conj verbs like читать, болеть, воевать, etc."
Vx:Vy <=> [ SCEnd | VEnd | JEnd | jEnd ] _ [ ?* - [ [ %>: | в:й | в:0 ] ?* ] ] .#. ;
          1ConjV _ ; ! This does not seem to match anything TODO
where Vx in ( а а́ о́ )
      Vy in ( я я́ ё )
    matched;

!! ==> sourced from phonology-rules/018 <==
!! ============================================================================
"у>ю (1) after soft-consonant stems (including й: муравей, семья, бельё, etc) and vowel stems (музей, критерий, понятие, Мария, etc) including 1st conj verbs like читать, болеть, воевать, etc. (2) in 1st conjugation after л or ь"
Vx:Vy <=> [ SCEnd | VEnd | JEnd | jEnd ] _ [ ?* - [ [ в:й | в:0 ] ?* ] ] .#. ;
          [ 1ConjV | 1ConjJ ] _ ;
          :л %<: _ ;
          %^R:р %<: _ ; ! Verbs in ~роть
where Vx in ( у у́ )
      Vy in ( ю ю́ )
    matched;

!! ==> sourced from phonology-rules/019 <==
!! ============================================================================
"е>и after и-stems (e.g. о Марии)"
е:и <=> [ и | и́ ] [ я́:0 | ё:0 | е́:0 | я:0 | е:0 | й:0 ]  %>: _ .#. ;

!! ==> sourced from phonology-rules/020 <==
!! ============================================================================
"в>й: GenPl ей"
в:й <=> [ HUSHEndC | SCEndC ] [ о: | о́: ] _ .#. ;
        [ HUSHEndV | SCEndV ] о́: _ .#. ;

!! ==> sourced from phonology-rules/021 <==
!! ============================================================================
"о́>е́: GenPl stressed е́й"
о́:е́ <=> [ HUSHEndC | HUSHEndV | SCEndC | SCEndV ] _ в:й .#. ;

!! ==> sourced from phonology-rules/022 <==
!! ============================================================================
"в>0: (1) GenPl zero and (2) Masc past tense endings or past deverbals, after consonant stems"
в:0 <=> [ VELEndV | цEndV | HCEndV | VEndV | JEndV | jEndV ] [ о: | о́: ] _ .#. ;
        HUSHEndV о: _ .#. ;
        SCEndV о: _ .#. ;
        :C %<: _ ( ш и ( с :ь ) ) .#. ; ! Past verbal adverb (handled by lexc)
        :C %<: _ ш %>: ; ! Past active ppl (see Zaliznjak intro pg 85) л>вший, no л +ший EXCEPTION: 7-сти (-д-) or (-т-) > дший/тший uses %^d/%^t

!! ==> sourced from phonology-rules/023 <==
!! ============================================================================
"о>ь: GenPl zero after soft C"
о:ь <=> SCEndVNoN _ в:0 .#. ; ! However, Fem 2*a in ~ня go to 0, not ь (characterized by F V н я)
        \[%^F:0] V: н: я:0 %>: _ в:0 .#. ; ! However, Fem 2*a in н, e.g. башня~башен
        C: н: [ е́:0 | е:0 | ё:0 | я́:0 ] %>: _ в:0 .#. ;

!! ==> sourced from phonology-rules/024 <==
!! ============================================================================
"[оо́]>й: GenPl zero after V"
[ о:й | о́:й ] <=> VEndV _ в:0 .#. ;

!! ==> sourced from phonology-rules/025 <==
!! ============================================================================
"P>п in comparatives"
%^P:п <=> .#. _ %^A: ?* %>: [ е | е́: ] [ е: | й: ] %^P:0 .#. ;

!! ==> sourced from phonology-rules/026 <==
!! ============================================================================
"A>о in comparatives"
%^A:о <=> .#. %^P: _ ?* %>: [ е | е́: ] [ е: | й: ] %^P:0 .#. ;

!! ==> sourced from phonology-rules/027 <==
!! ============================================================================
"е́>е in comparative adjective ending after velar mutation"
! ли́х>е́е
! ли́ш0е0
е́:е <=> [ г: | к: | х: ] %>: _ [ е: | й: ] (%^P:0) .#. ;

!! ==> sourced from phonology-rules/028 <==
!! ============================================================================
"г>ж (1) with comparative(-ее) adjectives (2) with ^M symbol (verbs) (3) in pres/fut before е/ё"
г:ж <=> _ %>: [ е | е́: ] [ е: | й: ] ( %^P: ) .#. ;
        _ %^M: ;
        _ %<: [ е: | ё: ] ;
        ! TODO make ~ейший productive in lexc (ш: V: Letter: (V:))

!! ==> sourced from phonology-rules/029 <==
!! ============================================================================
"х>ш (1) with comparative(-ее) adjectives (2) with ^M symbol (verbs)"
х:ш <=> _ %>: [ е | е́: ] [ е: | й: ] ( %^P: ) .#. ;
        _ %^M: ;
        ! TODO make ~ейший productive in lexc (ш: V: Letter: (V:))

!! ==> sourced from phonology-rules/030 <==
!! ============================================================================
"к>ч Velar mutation (1) with comparative(-ее) adjectives (2) with ^M symbol (verbs) (as opposed to ск>щ) (3) in pres/fut before е/ё"
к:ч <=> _ %>: [ е | е́: ] [ е: | й: ] ( %^P: ) .#. ;
   \с:0 _ %^M: ;
     \т _ %<: [ е: | ё: ] ; ! печёшь, but ткёшь
        ! TODO make ~ейший productive in lexc (ш: V: Letter: (V:))

!! ==> sourced from phonology-rules/031 <==
!! ============================================================================
"т>ч (1) Pres/Fut mutation of т (as opposed to ст>щ); (2) гть/кть > 0чь"
т:ч <=>              \с: _ %^M: ;
        [ г: | к: ] %<: _ ь ( с я ) .#. ;

!! ==> sourced from phonology-rules/032 <==
!! ============================================================================
"[гк]>0 before infinitive ending: гть/кть > чь"
Cx:0 <=> _ %<: т: ь ( с я ) .#. ;
where Cx in ( г к ) ;

!! ==> sourced from phonology-rules/033 <==
!! ============================================================================
"ё>е́ in some infinitives (чь and [бдт...]сть)"
ё:е́ <=> _ [ г: | к: ] %<: т: ь ( с я ) .#. ;
        _ [ б: | д: | с | (с) т:с ] %<: т ь ( с я ) .#. ;

!! ==> sourced from phonology-rules/034 <==
!! ============================================================================
"[дтdt]>0 before past tense"
Cx:0 <=> _ %<: л ( [ V ( с :ь ) | с [ :я | :я́ ] ] ) .#.  ;
where Cx in ( д т %^d %^t ) ;

!! ==> sourced from phonology-rules/035 <==
!! ============================================================================
"[дт]>в before past active deverbals" ! NB! Compare меtший etc.
Cx:в <=> _ %<: ( в:0 ) ш и ( с ь ) .#. ;
         _ %<: в:0 .#. ;
         _ %<: в:0 ш %>: ;
where Cx in ( д т ) ;

!! ==> sourced from phonology-rules/036 <==
!! ============================================================================
"[бдтdt]>с in before infinitive endings"
Cx:с <=> _ %<: т ( [ и́ ( с :ь ) | ь ( с я ) ] ) .#.  ;
         $[ :в :ы́ ] _ %<: т ( и ( с :ь ) ) .#.  ;
where Cx in ( б д т %^d %^t ) ;

!! ==> sourced from phonology-rules/037 <==
!! ============================================================================
"с>0 before т>щ and к>щ stem mutations"
с:0 <=> _ [ к: | т: ] %^M: ;

!! ==> sourced from phonology-rules/038 <==
!! ============================================================================
"с[ к т ]>щ Mutation of ск and ст"
Cx:щ <=> с:0 _ %^M: ;
where Cx in ( к т ) ;

!! ==> sourced from phonology-rules/039 <==
!! ============================================================================
"[ з с д T D ] > [ ж ш ж щ ж ] Verb stem mutations with ^M symbol (г к х covered in another rule)"
Cx:Cy <=> _ %^M: ;
where Cx in ( д з с %^T %^D )
      Cy in ( ж ж ш щ   ж )
      matched ;

!! ==> sourced from phonology-rules/040 <==
!! ============================================================================
"M>л Addition of л after labials in 'mutation' with ^M symbol (verbs)"
%^M:л <=> [ б: | п: | в: | ф: | м: ] _ ;

!! ==> sourced from phonology-rules/041 <==
!! ============================================================================
"M>д in DM>жд mutation"
%^M:д <=> %^D: _ [ е: | ё: ] н ( н ) %>: ;

!! ==> sourced from phonology-rules/042 <==
!! ============================================================================
"л>0 in Masc past tense after consonant stems"
л:0 <=> CNoDT: %<: _ ( с я ) .#. ;

!! ==> sourced from phonology-rules/043 <==
!! ============================================================================
"S>0 in imperatives with й stems (e.g. бить, вить, лить, пить, шить)"
%^S:0 <=>                                                                й %<: _ ( т е ) ( с [ я: | ь ] ) .#. ;

!! ==> sourced from phonology-rules/044 <==
!! ============================================================================
"S>и́ in imperatives with consonant stems"
%^S:и́ <=>                                               [ :C - :й ] (%^M:) %<: _ ( т е ) ( с [ я: | ь ] ) .#. ;

!! ==> sourced from phonology-rules/045 <==
!! ============================================================================
"[SU]>й in Imperatives with vowel stems"
Ex:й <=>                                                                :V %<: _ ( т е ) ( с [ я: | ь ] ) .#. ;
where Ex in ( %^U %^S ) ;

!! ==> sourced from phonology-rules/046 <==
!! ============================================================================
"U>и in Imperatives with double-consonant stems, щ stems, and single-consonant stems with prefix вы́-"
%^U:и <=>                                      :C (:ь) [ :C - :й ] (%^M:0) %<: _ ( т е ) ( с [ я: | ь ] ) .#. ;
                                                                :щ (%^M:0) %<: _ ( т е ) ( с [ я: | ь ] ) .#. ; !TODO native S this true?
          .#. VyPrefix [ Letter | %^F:0 ]* :V [ :C - [ :г | :й ] ] (%^M:0) %<: _ ( т е ) ( с [ я: | ь ] ) .#. ; ! TODO fix ~вы́сить, etc. perhaps by explicitly marking the prefix вы́- with an archiphoneme %^Y? This could also be used in stress rules and remove -a continuation classes that are only for вы́-prefixed verbs.
          .#. VyPrefix [ Letter | %^F:0 ]* [ :V - [ :я́ | :я ] ] :г (%^M:0) %<: _ ( т е ) ( с [ я: | ь ] ) .#. ;
                ! по́мн<U   мо́рщ<U  вы́бег<U
                ! по́мн_и   мо́рщ_и  бы́бег_и

!! ==> sourced from phonology-rules/047 <==
!! ============================================================================
"U>ь in Imperatives with consonant stem (exceptions in previous rule)"
%^U:ь <=> .#. [ ?* - [ VyPrefix ?* ]] :V [ :C - [ :щ | :г | :й ] ] (%^M:0) %<: _ ( т е ) ( с [ я: | ь ] ) .#. ;
                ! отве́т<U
                ! отве́т_ь

!! ==> sourced from phonology-rules/048 <==
!! ============================================================================
"U>0 in Imperatives with velar stem, i.e. лечь"
%^U:0 <=>                                             [:я́ | :я] :г (%^M:0) %<: _ ( т е ) ( с [ я: | ь ] ) .#. ; ! only applies to ~лечь
                                                                         й %<: _ ( т е ) ( с [ я: | ь ] ) .#. ;
	! повы́бFей<U    ля́г<U
	! повы́б_ей__	ля́г__

!! ==> sourced from phonology-rules/049 <==
!! ============================================================================
"я>ь (i.e. ся>сь) in singular reflexive imperatives"
я:ь <=> %<: [ :и | :и́ ] с: _ .#. ;
