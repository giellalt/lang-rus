! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no

! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.

! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

!!!!!!!!!!!!!!!!!!!!!!!!!MASCULINE PARADIGMS!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON м_0
+N+Msc: Indecl_SgInan ;
+N+Msc: Indecl_PlInan ;

LEXICON соб-м_0
+N+Prop+Alt+Msc: Indecl_SgInan ;
+N+Prop+Alt+Msc: Indecl_PlInan ;

LEXICON м_a
+N+Msc: MSgStem ;
+N+Msc: ыPlStem ;

LEXICON соб-м_a
+N+Prop+Alt+Msc: MSgStem ;
+N+Prop+Alt+Msc: ыPlStem ;

LEXICON мн._[м_a]
+N+Msc: ыPlStem ;

LEXICON м_a_П2
м_a ;
MscLoc2 ;

LEXICON м_a_[П2]
м_a ;
MscLoc2_Fac ;

LEXICON м_a_[П2]_Р2
м_a ;
MscLoc2_Fac ;
MscGen2 ;

LEXICON м_a_Р2
м_a ;
MscGen2 ;

LEXICON м_a_ёнок
+N+Msc:ён%^Fок MSgStem ;
+N+Msc:я́т аPlStem_zero ;

LEXICON м_a1_JPl
+N+Msc: MSgStem ;
+N+Msc:%^Fей аPlStem ;

LEXICON м_a2
+N+Msc: MSgStem ;
+N+Msc: ыPlStem_zero ;

LEXICON м_a[2]
м_a ;
+N+Msc+Gen+Pl+Fac: # ;

LEXICON м_a-
+N+Msc: MSgStem ;
+N+Msc: ыPlStem_Prb ;

LEXICON м_a-_П2
м_a- ;
MscLoc2 ;

LEXICON м_a-_П2_Р2
м_a- ;
MscLoc2 ;
MscGen2 ;

LEXICON м_a-_[П2]_Р2
м_a- ;
MscLoc2_Fac ;
MscGen2 ;

LEXICON м_a-_Р2
+N+Msc: MSgStem ;
+N+Msc: ыPlStem_Prb ;

LEXICON MscGen2
+N+Msc+Gen2+Sg:у # ;

LEXICON MscLoc2
+N+Msc+Loc2+Sg:у́ # ;

LEXICON MscLoc2_Fac
+N+Msc+Loc2+Sg+Fac:у́ # ;

LEXICON м_b
+N+Msc: MSgEnd ;
+N+Msc: ыPlEnd ;

LEXICON м_b_ок_ы
+N+Msc:%^Fо́к MSgEnd ;
+N+Msc: ыPlEnd ;

LEXICON мн._[м_b]
+N+Msc: ыPlEnd ;

LEXICON м_b_П2
м_b ;
MscLoc2 ;

LEXICON м_b_[П2]
м_b ;
MscLoc2_Fac ;

LEXICON м_b_П2_Р2
м_b ;
MscLoc2 ;
MscGen2 ;

LEXICON м_b_Р2
м_b ;
MscGen2 ;

LEXICON м_b1
+N+Msc: MSgEnd ;
+N+Msc: аPlEnd ;

LEXICON м_b1_JPl
+N+Msc: MSgEnd ;
+N+Msc:%^Fей аPlEnd ;

LEXICON м_b1_ь_JPl
+N+Msc:ь MSgEnd ;
+N+Msc:%^Fей аPlEnd ;

LEXICON мн._[м_b1]
+N+Msc: аPlEnd ;

LEXICON м_b2
+N+Msc: MSgEnd ;
+N+Msc: ыPlEnd_zero ;

LEXICON м_c
+N+Msc: MSgStem ;
+N+Msc: ыPlEnd ;

LEXICON м_c_П2
м_c ;
MscLoc2 ;

LEXICON м_c_[П2]
м_c ;
MscLoc2_Fac ;

LEXICON м_c_П2_Р2
м_c ;
MscLoc2 ;
MscGen2 ;

LEXICON м_c_[П2]_Р2
м_c ;
MscLoc2_Fac ;
MscGen2 ;

LEXICON м_c_Р2
м_c ;
MscGen2 ;

LEXICON м_c-
+N+Msc: MSgStem ;
+N+Msc: ыPlEnd_Prb ;

LEXICON м_c-_П2
+N+Msc: MSgStem ;
+N+Msc: ыPlEnd_Prb ;
MscLoc2 ;

LEXICON м_c-_[П2]_Р2
+N+Msc: MSgStem ;
+N+Msc: ыPlEnd_Prb ;
MscLoc2_Fac ;
MscGen2 ;

LEXICON м_c-_Р2
+N+Msc: MSgStem ;
+N+Msc: ыPlEnd_Prb ;
MscGen2 ;

LEXICON м_c1
+N+Msc: MSgStem ;
+N+Msc: аPlEnd ;

LEXICON м_c1_П2
м_c1 ;
MscLoc2 ;

LEXICON м_c1_П2_год
:го́д м_c1_П2 ;
+N+Msc+Gen+Pl:ле́т # ;

LEXICON м_c1_[П2]
м_c1 ;
MscLoc2_Fac ;

LEXICON м_c1_П2_Р2
м_c1 ;
MscLoc2 ;
MscGen2 ;

LEXICON м_c1_[П2]_Р2
м_c1 ;
MscLoc2_Fac ;
MscGen2 ;

LEXICON м_c1_Р2
м_c1 ;
MscGen2 ;

LEXICON м_c1-
+N+Msc: MSgStem ;
+N+Msc: аPlEnd_Prb ;

LEXICON м_c1-_Р2
м_c1- ;
MscGen2 ;

LEXICON м_c12_П2
+N+Msc: MSgStem ;
+N+Msc: аPlEnd_zero ;
MscLoc2 ;

LEXICON м_c2
+N+Msc: MSgStem ;
+N+Msc: ыPlEnd_zero ;

LEXICON м_d1_JPl
+N+Msc: MSgEnd ;
+N+Msc:%^Fей аPlStem ;

LEXICON м_d1_К_ЧЬ
+N+Msc:к MSgEnd ;
+N+Msc:ч%^Fей аPlStem ;

LEXICON м_d1_П2_JPl
+N+Msc: MSgEnd ;
+N+Msc:%^Fей аPlStem ;
MscLoc2 ;

LEXICON м_d1_П2_К_ЧЬ
+N+Msc:к MSgEnd ;
:к MscLoc2 ;
+N+Msc:ч%^Fей аPlStem ;

LEXICON м_d2
+N+Msc: MSgEnd ;
+N+Msc: ыPlStem_zero ;

LEXICON м_e
+N+Msc: MSgStem ;
+N+Msc: ыPlAnom ;

LEXICON м_e_П2
м_e ;
MscLoc2 ;

LEXICON м_e_П2_год
:го́д м_e_П2 ;
+N+Msc+Gen+Pl:ле́т # ;

LEXICON м_e2_волос
+N+Msc:ос MSgStem ;
+N+Msc:ос ыPlNomStem ;
+N+Msc:ос ыPlAccInanStem ;
+N+Msc+Gen+Pl:о́с # ;
+N+Msc:ос PlPeriphEnd ;

LEXICON мн._[м_e]
+N+Msc: ыPlAnom ;

LEXICON м_f
+N+Msc: MSgEnd ;
+N+Msc: ыPlAnom ;

LEXICON м_8b ! путь
+N+Msc: FьSgEnd_MostCases ;
+N+Msc+Ins+Sg:%>о́м # ;
+N+Msc: ыPlEnd ;

LEXICON м_[ж_a]
+N+Msc: FSgStem ;
+N+Msc: ыPlStem ;

LEXICON м_[п_a]
+N+Msc+Nom+Sg:%>ый # ;
+N+Msc+Acc+Sg+Inan:%>ый # ;
MSg_SubstAdj_Stem ;
+N+Msc+Nom+Pl:%>ые # ;
+N+Msc+Acc+Pl+Inan:%>ые # ;
Pl_SubstAdj_Stem ;

LEXICON м_[с_a1]
+N+Msc: NSgStem ;
+N+Msc: ыPlStem ;

LEXICON м_[с_a1]_5
м_[с_a1] ;

LEXICON м_[с_a[1]]
+N+Msc: NSgStem ;
+N+Msc: аPlStem ;
+N+Msc: ыPlStem_Fac ;

LEXICON MSg_SubstAdj_Stem
+N+Msc+Gen+Sg:%>ого # ;
+N+Msc+Loc+Sg:%>ом # ;
+N+Msc+Dat+Sg:%>ому # ;
+N+Msc+Ins+Sg:%>ым # ;

LEXICON Pl_SubstAdj_Stem
+N+Msc+Gen+Pl:%>ых # ;
+N+Msc+Loc+Pl:%>ых # ;
+N+Msc+Dat+Pl:%>ым # ;
+N+Msc+Ins+Pl:%>ыми # ;

LEXICON м_[п_b]
+N+Msc+Nom+Sg:%>о́й # ;
+N+Msc+Acc+Sg+Inan:%>о́й # ;
MSg_SubstAdj_End ;
+N+Msc+Nom+Pl:%>ы́е # ;
+N+Msc+Acc+Pl+Inan:%>ы́е # ;
Pl_SubstAdj_End ;

LEXICON MSg_SubstAdj_End
+N+Msc+Gen+Sg:%>о́го # ;
+N+Msc+Loc+Sg:%>о́м # ;
+N+Msc+Dat+Sg:%>о́му # ;
+N+Msc+Ins+Sg:%>ы́м # ;

LEXICON Pl_SubstAdj_End
+N+Msc+Gen+Pl:%>ы́х # ;
+N+Msc+Loc+Pl:%>ы́х # ;
+N+Msc+Dat+Pl:%>ы́м # ;
+N+Msc+Ins+Pl:%>ы́ми # ;

LEXICON мо_0
+N+Msc: Indecl_SgAnim ;
+N+Msc: Indecl_PlAnim ;

LEXICON мо_a
+N+Msc: MSgStem_anim ;
+N+Msc: ыPlStem_anim ;

LEXICON соб-мо_a
+N+Prop+Ant+Msc: MSgStem_anim ;
+N+Prop+Ant+Msc: ыPlStem_anim ;
о:%>о Patronymic_stem ;

LEXICON соб-мо_a_й
й+N+Prop+Ant+Msc:й MSgStem_anim ;
й+N+Prop+Ant+Msc:й ыPlStem_anim ;
е:%>е Patronymic_stem ;

LEXICON соб-мо_a_ь
ь+N+Prop+Ant+Msc:ь MSgStem_anim ;
ь+N+Prop+Ant+Msc:ь ыPlStem_anim ;
е:%>е Patronymic_stem ;

LEXICON соб-мо_a_26
ий+N+Prop+Ant+Msc:ий MSgStem_anim ;
ий+N+Prop+Ant+Msc:ий ыPlStem_anim ;
ье:%>ье Patronymic_stem ;

LEXICON Patronymic_stem
вич+N+Prop+Pat+Msc:вич MSgStem_anim ;
вич+N+Prop+Pat+Msc:вич ыPlStem_anim ;
вна+N+Prop+Pat+Fem:вна FSgStem_anim ;
вна+N+Prop+Pat+Fem:вна ыPlStem_anim ;

LEXICON мо_a_Plья_ьев
+N+Msc: MSgStem_anim ;
+N+Msc:%^Fей аPlStem_anim ;

LEXICON мо_a_ин_ева ! хозяин
+N+Msc:ин MSgStem_anim ;
+N+Msc:ев аPlStem_anim ;

LEXICON мо_a2
+N+Msc: MSgStem_anim ;
+N+Msc: ыPlStem_anim_zero ;

LEXICON мо_a2_0_е ! цыган
+N+Msc: MSgStem_anim ;
+N+Msc: Pl_ин ;

LEXICON мо_a[2]
+N+Msc: MSgStem_anim ;
+N+Msc: ыPlStem_anim ;
+N+Msc+Gen+Pl+Fac: # ;

LEXICON мо_a-
+N+Msc: MSgStem_anim ;
+N+Msc: ыPlStem_anim_Prb ;

LEXICON мо_a-_господь
+N+Msc+Nom+Sg:госпо́дь # ;
+N+Msc+Acc+Sg+Anim:го́спода # ;
+N+Msc:го́спод MSgObliqStem ;
+N+Msc:го́спод ыPlStem_anim_Prb ;

LEXICON мо_a_ин_Root
+N+Msc:ин MSgStem_anim ;
+N+Msc: Pl_ин ;

LEXICON мо_a_ин_Suffix
+N+Msc:и́н MSgStem_anim ;
+N+Msc: Pl_ин ;

LEXICON мо_a_ин_ы
+N+Msc:и́н MSgStem_anim ;
+N+Msc: ыPlStem_anim ;

LEXICON мо_a_шурин
+N+Msc:ин MSgStem_anim ;
+N+Msc:ин ыPlStem_anim ;
+N+Msc:%^Fей аPlEnd_anim ;

LEXICON мо_a_ёнок
+N+Msc:ён%^Fок MSgStem_anim ;
+N+Msc:я́та аPlStem_anim ;

LEXICON мо_a_ёнок_еня́та
+N+Msc:ён%^Fок MSgStem_anim ;
+N+Msc:еня́та аPlStem_anim ;

LEXICON мо_a_о́нок_еня́та
+N+Msc:о́н%^Fок MSgStem_anim ;
+N+Msc:еня́та аPlStem_anim ;

LEXICON мо_a_ёночек
+N+Msc:ёноч%^Fек MSgStem_anim ;
+N+Msc:я́т%^Fока ыPlStem_anim ;

LEXICON мо_a_ребёночек
+N+Msc:ребёноч%^Fек MSgStem_anim ;
+N+Msc:де́т%^Fоко ыPlStem_anim ;

LEXICON мо_a_о́нок
+N+Msc:о́н%^Fок MSgStem_anim ;
+N+Msc:а́та аPlStem_anim ;

LEXICON мо_a_о́ночек
+N+Msc:о́ноч%^Fек MSgStem_anim ;
+N+Msc:а́т%^Fока ыPlStem_anim ;

LEXICON мо_a_щено́чек
+N+Msc:о́ч%^Fек MSgStem_anim ;
+N+Msc:о́ч%^Fек ыPlStem_anim ;
+N+Msc:я́т%^Fока ыPlStem_anim ;

LEXICON мо_a_щено́к
+N+Msc:%^Fо́к MSgStem_anim ;
+N+Msc:%^Fо́к ыPlStem_anim ;
+N+Msc:я́та аPlStem_anim ;

LEXICON мн._[мо_a]
+N+Msc: ыPlStem_anim ;

LEXICON мо_a_SPl
+N+Msc: MSgStem_anim ;
+N+Msc:ь ыPlStem_anim ;

LEXICON мо_b
+N+Msc: MSgEnd_anim ;
+N+Msc: ыPlEnd_anim ;

LEXICON мо_b_христос
+N+Msc+Nom+Sg:о́с # ;
+N+Msc+Acc+Sg+Anim:а́ # ;
+N+Msc: MSgObliqEnd ;
+N+Msc: ыPlEnd_anim ;

LEXICON соб-мо_b_христос
+N+Prop+Ant+Msc+Nom+Sg:о́с # ;
+N+Prop+Ant+Msc+Acc+Sg+Anim:а́ # ;
+N+Prop+Ant+Msc: MSgObliqEnd ;
+N+Prop+Ant+Msc: ыPlEnd_anim ;

LEXICON соб-мо_b
+N+Prop+Ant+Msc: MSgEnd_anim ;
+N+Prop+Ant+Msc: ыPlEnd_anim ;
о:%>о́ Patronymic_stem ;

LEXICON мо_c
+N+Msc: MSgStem_anim ;
+N+Msc: ыPlEnd_anim ;

LEXICON мо_c_барин
+N+Msc:ба́рин MSgStem_anim ;
+N+Msc:госпо́да́ аPlEnd_anim ;

LEXICON мо_c1
+N+Msc: MSgStem_anim ;
+N+Msc: ыPlEnd_anim ;

LEXICON мо_c1_ин_а
+N+Msc:и́н MSgStem_anim ;
+N+Msc:о аPlEnd_anim ;

LEXICON мо_c1_JPl-ЕЙ
+N+Msc: MSgStem_anim ;
+N+Msc:%^Fе́йя́ аPlEnd_anim ;

LEXICON мо_c1_Plовья-ей
+N+Msc: MSgStem_anim ;
+N+Msc:ов%^Fе́йя́ аPlEnd_anim ;

LEXICON мо_c1_Plовья-ёв
+N+Msc: MSgStem_anim ;
+N+Msc:ов%^Fе́й аPlEnd_anim ;

LEXICON мо_c1_Г_ЗЬ
+N+Msc:г MSgStem_anim ;
+N+Msc:з%^Fе́йя́ аPlEnd_anim ;

LEXICON мо_c1_Ь_ЬЯ-ЕЙ
+N+Msc:ь MSgStem_anim ;
+N+Msc:%^Fе́йя аPlEnd_anim ;

LEXICON мо_c1_Ь_ЬЯ-ЁВ
+N+Msc:ь MSgStem_anim ;
+N+Msc:%^Fе́й аPlEnd_anim ;

LEXICON мо_d
+N+Msc: MSgEnd_anim ;
+N+Msc: ыPlStem_anim ;

LEXICON мо_e
+N+Msc: MSgStem_anim ;
+N+Msc: ыPlAnom_anim ;

LEXICON мо_e_черт
+N+Msc:ёрт MSgStem_anim ;
+N+Msc:е́рть ыPlAnom_anim ;

LEXICON мо_e_зверь
мо_e ;
+N+Msc+Ins+Pl+Fac:%>ьми́ # ;

LEXICON мо_f
+N+Msc: MSgEnd_anim ;
+N+Msc: ыPlAnom_anim ;

LEXICON мо_[со_a]
+N+Msc: NSgStem ;
+N+Msc: аPlStem_anim ;     !Cyrillic 'а' in 'аPl'

LEXICON мо_ребенок
+N+Msc:ребён%^Fок MSgStem_anim ;
+N+Msc:де́ть ыPlNomStem ;
+N+Msc+Acc+Pl+Anim:дете́й # ;
+N+Msc+Gen+Pl:дете́й # ;
+N+Msc+Loc+Pl:де́тях # ;
+N+Msc+Dat+Pl:де́тям # ;
+N+Msc+Ins+Pl:детьми́ # ;

LEXICON мо_человек
! +N+Msc:челове́к ыPlStem_anim_Use/Obs ;
+N+Msc+Gen+Pl:челове́к # ;
мо_человек2 ;

LEXICON мо_человек2
+N+Msc:челове́к NSgStem ;
+N+Msc:лю́дь ыPlNomStem ;
+N+Msc+Acc+Pl+Anim:люде́й # ;
+N+Msc+Gen+Pl:люде́й # ;
+N+Msc+Loc+Pl:лю́дях # ;
+N+Msc+Dat+Pl:лю́дям # ;
+N+Msc+Ins+Pl:людьми́ # ;

LEXICON мо-жо_0
мо_0 ;
жо_0 ;

LEXICON мо_[жо_a]
+N+Msc: FSgStem_anim ;
+N+Msc: ыPlStem_anim ;

LEXICON мо_[жо_a2]
+N+Msc: FSgStem_anim ;
+N+Msc: ыPlStem_anim_ей ;

LEXICON мо_[жо_a[2]]
+N+Msc: FSgStem_anim ;
+N+Msc: ыPlStem_anim ;
+N+Msc+Gen+Pl+Fac:%>ей # ;

LEXICON мо_[жо_c]_я_ья́/ёв
+N+Msc:я FSgStem_anim ;
+N+Msc:%^Fей аPlEnd_anim ;

LEXICON мо-жо_a
мо_[жо_a] ;
жо_a ;

LEXICON мо-жо_a2
мо_[жо_a2] ;
жо_a2 ;

LEXICON мо-жо_a[2]
мо_[жо_a[2]] ;
жо_a[2] ;

LEXICON соб-мо_[жо_a]
+N+Prop+Ant+Msc: FSgStem_anim ;
+N+Prop+Ant+Msc: ыPlStem_anim ;

LEXICON соб-мо-жо_a
+N+Prop+Ant+Msc: FSgStem_anim ;
+N+Prop+Ant+Msc: ыPlStem_anim ;
+N+Prop+Ant+Fem: FSgStem ;
+N+Prop+Ant+Fem: ыPlStem_anim ;

LEXICON мо_[жо_b] ! This also includes жо_b' because of the fleeting vowel rule in twolc
+N+Msc: FSgEnd_anim ;
+N+Msc: ыPlEnd_anim ;

LEXICON мо_[жо_b]_Р._мн._затрудн. ! This also includes жо_b' because of the fleeting vowel rule in twolc
+N+Msc: FSgEnd_anim ;
+N+Msc: ыPlEnd_anim_gPrb ;

LEXICON мо-жо_b
мо_[жо_b] ;
жо_b ;

LEXICON мо_[жо_b-] ! This also includes жо_b' because of the fleeting vowel rule in twolc
+N+Msc: FSgEnd_anim ;
+N+Msc: ыPlEnd_anim_Prb ;

LEXICON мо-жо_b-
мо_[жо_b-] ;
жо_b- ;

LEXICON мо_[жо_d]
+N+Msc: FSgEnd_anim ;
+N+Msc: ыPlStem_anim ;

LEXICON мо-жо_d
мо_[жо_d] ;
жо_d ;

LEXICON мо_[жо_f_ова]
+N+Msc:ова́ FSgEnd ;
+N+Msc:ова́ ыPlNomStem ;
+N+Msc+Acc+Pl+Anim:о́в # ;
+N+Msc+Gen+Pl:о́в # ;
+N+Msc:ова́ PlPeriphEnd ;

LEXICON мо_[п_a]
+N+Msc+Nom+Sg:%>ый # ;
+N+Msc+Acc+Sg+Anim:%>ого # ;
MSg_SubstAdj_Stem ;
+N+Msc+Nom+Pl:%>ые # ;
+N+Msc+Acc+Pl+Anim:%>ых # ;
Pl_SubstAdj_Stem ;

LEXICON мо_[п_a-]
+N+Msc+Nom+Sg:%>ый # ;
+N+Msc+Acc+Sg+Anim:%>ого # ;
MSg_SubstAdj_Stem ;
+N+Msc+Nom+Pl+Prb:%>ые # ;
+N+Msc+Acc+Pl+Anim+Prb:%>ых # ;
+Prb: Pl_SubstAdj_Stem ;

LEXICON мо_[п_b]
+N+Msc+Nom+Sg:%>о́й # ;
+N+Msc+Acc+Sg+Anim:%>о́го # ;
MSg_SubstAdj_End ;
+N+Msc+Nom+Pl:%>ы́е # ;
+N+Msc+Acc+Pl+Anim:%>ы́х # ;
Pl_SubstAdj_End ;

LEXICON MSgStem !Inanimate
MSgNomZ ;
MSgAccZ ;
MSgObliqStem ;

LEXICON MSgEnd !Inanimate
MSgNomZ ;
MSgAccZ ;
MSgObliqEnd ;

LEXICON MSgStem_anim
MSgNomZ ;
+Acc+Sg+Anim:%>а # ;
MSgObliqStem ;

LEXICON MSgEnd_anim
MSgNomZ ;
+Acc+Sg+Anim:%>а́ # ;
MSgObliqEnd ;

LEXICON MSgNomZ
+Nom+Sg: # ;

LEXICON MSgAccZ
+Acc+Sg+Inan: # ;

LEXICON MSgObliqStem
+Gen+Sg:%>а # ;
+Loc+Sg:%>е # ;
+Dat+Sg:%>у # ;
+Ins+Sg:%>ом # ;

LEXICON MSgObliqEnd
+Gen+Sg:%>а́ # ;
+Loc+Sg:%>е́ # ;
+Dat+Sg:%>у́ # ;
+Ins+Sg:%>о́м # ;

!!!!!!!!!!!!!!!!!!!!!!!!!NEUTER PARADIGMS!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON с_0
+N+Neu: Indecl_Sg ;
+N+Neu: Indecl_PlInan ;

LEXICON с_a
+N+Neu: NSgStem ;
+N+Neu: аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a_Р._мн._затрудн.
+N+Neu: NSgStem ;
+N+Neu: аPlStem_gPrb ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a_JPl
+N+Neu: NSgStem ;
+N+Neu:%^Fей аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a_о_и
+N+Neu:о NSgStem ;
+N+Neu:ь ыPlStem ;

LEXICON с_a_дерево
+N+Neu:де́рево NSgStem ;
+N+Neu:дере́в%^Fей аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a_озеро
+N+Neu:о́зеро NSgStem ;
+N+Neu:озёро аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a_утро
с_a ;
+N+Neu+Gen+Sg:%>а́ # ;
+N+Neu+Dat+Sg:%>у́ # ;
+N+Neu+Dat+Pl:%>а́м # ;
+N+Neu+Ins+Pl:%>а́ми # ;

LEXICON мн._[с_a]
+N+Neu: аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a1
+N+Neu: NSgStem ;
+N+Neu: ыPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a12
+N+Neu: NSgStem ;
+N+Neu: ыPlStem_ов ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a2
+N+Neu: NSgStem ;
+N+Neu: аPlStem_ов ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a[2]
+N+Neu: NSgStem ;
+N+Neu: аPlStem ;     !Cyrillic 'а' in 'аPl'
+N+Neu+Gen+Pl+Fac:%>ов%^G # ;

LEXICON мн._[с_a2]
+N+Neu: аPlStem_ов ;     !Cyrillic 'а' in 'аPl'

LEXICON мн._[с_a[2]]
+N+Neu: аPlStem ;     !Cyrillic 'а' in 'аPl'
+N+Neu+Gen+Pl+Fac:%>ов%^G # ;

LEXICON с_a-
+N+Neu: NSgStem ;
+N+Neu: аPlStem_Prb ;     !Cyrillic 'а' in 'аPl'

LEXICON с_b
+N+Neu: NSgEnd ;
+N+Neu: аPlEnd ;      !Cyrillic 'а' in 'аPl'

LEXICON с_b_OCS_ие
+N+Neu: NSgEnd_OCS_ие ;
+N+Neu: аPlEnd_OCS_ие ;      !Cyrillic 'а' in 'аPl'

LEXICON с_b12
+N+Neu: NSgEnd ;
+N+Neu: ыPlEnd_ов ;      !Cyrillic 'а' in 'аPl'

LEXICON с_b12-
+N+Neu: NSgEnd ;
+N+Neu: ыPlEnd_ов_Prb ;      !Cyrillic 'а' in 'аPl'

LEXICON с_b2
+N+Neu: NSgEnd ;
+N+Neu: аPlEnd_ов ;      !Cyrillic 'а' in 'аPl'

LEXICON мн._[с_b]
+N+Neu: аPlEnd ;      !Cyrillic 'а' in 'аPl'

LEXICON с_b-
+N+Neu: NSgEnd ;
+N+Neu: аPlEnd_Prb ;      !Cyrillic 'а' in 'аPl'

LEXICON с_b-_OCS_ие
+N+Neu: NSgEnd_OCS_ие ;
+N+Neu: аPlEnd_OCS_ие_Prb ;      !Cyrillic 'а' in 'аPl'

LEXICON NSgEnd_OCS_ие
+Nom+Sg:ие́ # ;
+Acc+Sg:ие́ # ;
+Gen+Sg:ия́ # ;
+Loc+Sg:ии́ # ;
+Dat+Sg:ию́ # ;
+Ins+Sg:ие́м # ;

LEXICON аPlEnd_OCS_ие
+Nom+Pl:ия́ # ;
+Acc+Pl:ия́ # ;
+Gen+Pl:и́й # ;
+Loc+Pl:ия́х # ;
+Dat+Pl:ия́м # ;
+Ins+Pl:ия́ми # ;

LEXICON аPlEnd_OCS_ие_Prb
+Nom+Pl+Prb:ия́ # ;
+Acc+Pl+Prb:ия́ # ;
+Gen+Pl+Prb:и́й # ;
+Loc+Pl+Prb:ия́х # ;
+Dat+Pl+Prb:ия́м # ;
+Ins+Pl+Prb:ия́ми # ;

LEXICON с_c
+N+Neu: NSgStem ;
+N+Neu: аPlEnd ;      !Cyrillic 'а' in 'аPl'

LEXICON с_c_есаPl
+N+Neu:о NSgStem ;
+N+Neu:е́са́ аPlEnd ;      !Cyrillic 'а' in 'аPl'

LEXICON с_c2
+N+Neu: NSgStem ;
+N+Neu: аPlEnd_ов ;      !Cyrillic 'а' in 'аPl'

LEXICON с_c[2]
+N+Neu: NSgStem ;
+N+Neu: аPlEnd ;      !Cyrillic 'а' in 'аPl'
+N+Neu+Gen+Pl+Fac:%>о́в%^G # ;

LEXICON с_c-
+N+Neu: NSgStem ;
+N+Neu: аPlEnd ;      !Cyrillic 'а' in 'аPl'

LEXICON с_d
+N+Neu: NSgEnd ;
+N+Neu: аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_d_JPl
+N+Neu:о́ NSgEnd ;
+N+Neu:%^Fей аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_d_дно
+N+Neu:д%^Fо́но́ NSgEnd ;
+N+Neu:до́н%^Fей аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_d1-
+N+Neu: NSgEnd ;
+N+Neu: ыPlStem_Prb ;     !Cyrillic 'а' in 'аPl'

LEXICON с_d2
+N+Neu: NSgEnd ;
+N+Neu: аPlStem_ов ;     !Cyrillic 'а' in 'аPl'

LEXICON с_d-
+N+Neu: NSgEnd ;
+N+Neu: аPlStem_Prb ;     !Cyrillic 'а' in 'аPl'

LEXICON с_e_око
+N+Neu:о́ко NSgEnd ;
+N+Neu:о́ч ыPlAnom ;

LEXICON с_e_ухо
+N+Neu:у́хо NSgEnd ;
+N+Neu:у́ш ыPlAnom ;     !Cyrillic 'а' in 'аPl'

LEXICON с_f
+N+Neu: NSgEnd ;
+N+Neu: аPlAnom ;     !Cyrillic 'а' in 'аPl'

LEXICON с_f1_плечо
+N+Neu: NSgEnd ;
+N+Neu: ыPlNomStem ;
+N+Neu: ыPlAccInanStem ;
+N+Neu+Gen+Pl:%> # ;
+N+Neu: PlPeriphEnd ;

LEXICON с_[п_b]
+N+Neu+Nom+Sg:%>о́е # ;
+N+Neu+Acc+Sg:%>о́е # ;
MSg_SubstAdj_End ;
+N+Neu+Nom+Pl:%>ы́е # ;
+N+Neu+Acc+Pl+Inan:%>ы́е # ;
Pl_SubstAdj_End ;

LEXICON со_0
+N+Neu: Indecl_Sg ;
+N+Neu: Indecl_PlAnim ;

LEXICON со_a
+N+Neu: NSgStem ;
+N+Neu: аPlStem_anim ;     !Cyrillic 'а' in 'аPl'

LEXICON со_a1
+N+Neu: NSgStem ;
+N+Neu: ыPlStem_anim ;

LEXICON мо_[со_a1]
+N+Msc: NSgStem_Manim ;
+N+Msc: ыPlStem_anim ;

LEXICON мо_[со_a1]_4
мо_[со_a1] ;
+N+Msc+Acc+Sg+Anim+Fac:%>у # ;

LEXICON мо_[со_a[1]]
+N+Msc: NSgStem_Manim ;
+N+Msc: аPlStem_anim ;
+N+Msc: ыPlStem_anim_Fac ;

LEXICON мо_[со_a[1]]_4
мо_[со_a[1]] ;
+N+Msc+Acc+Sg+Anim+Fac:%>у # ;

LEXICON со_a2
+N+Neu: NSgStem ;
+N+Neu: аPlStem_anim_ов ;     !Cyrillic 'а' in 'аPl'

LEXICON мо_[со_a2]
+N+Msc: NSgStem_Manim ;
+N+Msc: аPlStem_anim_ов ;     !Cyrillic 'а' in 'аPl'

LEXICON мн._[со_a]
+N+Neu: аPlStem_anim ;     !Cyrillic 'а' in 'аPl'

LEXICON со_b
+N+Neu: NSgEnd ;
+N+Neu: аPlEnd_anim ;      !Cyrillic 'а' in 'аPl'

LEXICON мо_[со_b12]
+N+Msc: NSgEnd_Manim ;
+N+Msc: ыPlEnd_anim_ов ;

LEXICON со_b12-
+N+Neu: NSgEnd ;
+N+Neu: ыPlEnd_anim_ов_Prb ;

LEXICON мо_[со_b12-]
+N+Msc: NSgEnd_Manim ;
+N+Msc: ыPlEnd_anim_ов_Prb ;

LEXICON со_d
+N+Neu: NSgEnd ;
+N+Neu: аPlStem_anim ;     !Cyrillic 'а' in 'аPl'

LEXICON со_d-
+N+Neu: NSgEnd ;
+N+Neu: аPlStem_anim_Prb ;     !Cyrillic 'а' in 'аPl'

LEXICON с_[п_a]
+N+Neu+Nom+Sg:%>ое # ;
+N+Neu+Acc+Sg:%>ое # ;
MSg_SubstAdj_Stem ;
+N+Neu+Nom+Pl:%>ые # ;
+N+Neu+Acc+Pl+Inan:%>ые # ;
Pl_SubstAdj_Stem ;

LEXICON со_[п_a]
+N+Neu+Nom+Sg:%>ое # ;
+N+Neu+Acc+Sg:%>ое # ;
MSg_SubstAdj_Stem ;
+N+Neu+Nom+Pl:%>ые # ;
+N+Neu+Acc+Pl+Anim:%>ых # ;
Pl_SubstAdj_Stem ;

LEXICON с_8c_ё
+N+Neu: мяSg ;
+N+Neu: мяPl_ё ;

LEXICON с_8c_я́
+N+Neu: мяSg ;
+N+Neu: мяPl_я́ ;

LEXICON мяSg
+Nom+Sg:%>я # ;
+Acc+Sg:%>я # ;
+Gen+Sg:%>ени # ;
+Loc+Sg:%>ени # ;
+Dat+Sg:%>ени # ;
+Ins+Sg:%>енем # ;

LEXICON мяPl_ё
:%>ен аPlNomEnd ;
+Acc+Pl+Inan:%>ена́ # ;
+Gen+Pl:%>ён # ;
:%>ен PlPeriphEnd ;

LEXICON мяPl_я́
:%>ен аPlNomEnd ;
+Acc+Pl+Inan:%>ена́ # ;
+Gen+Pl:%>я́н # ;
:%>ен PlPeriphEnd ;

LEXICON NSgStem
+Nom+Sg:%>о # ;
+Acc+Sg:%>о # ;
MSgObliqStem ;

LEXICON NSgStem_Minan ! Only for masculine nouns, e.g. м_[с_a]
+Nom+Sg:%>о # ;
+Acc+Sg+Inan:%>о # ;
MSgObliqStem ;

LEXICON NSgStem_Manim ! Only for masculine nouns, e.g. мо_[со_a]
+Nom+Sg:%>о # ;
+Acc+Sg+Anim:%>а # ;
MSgObliqStem ;

LEXICON NSgEnd
+Nom+Sg:%>о́ # ;
+Acc+Sg:%>о́ # ;
MSgObliqEnd ;

LEXICON NSgEnd_Minan ! Only for masculine nouns, e.g. м_[с_b]
+Nom+Sg:%>о́ # ;
+Acc+Sg+Inan:%>о́ # ;
MSgObliqEnd ;

LEXICON NSgEnd_Manim ! Only for masculine nouns, e.g. м_[с_b]
+Nom+Sg:%>о́ # ;
+Acc+Sg+Anim:%>а́ # ;
MSgObliqEnd ;

!!!!!!!!!!!!!!!!!!!!!!!!!FEMININE PARADIGMS!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON ж_0
+N+Fem: Indecl_Sg ;
+N+Fem: Indecl_PlInan ;

LEXICON ж_a
+N+Fem: FSgStem ;
+N+Fem: ыPlStem ;

LEXICON соб-ж_a
+N+Prop+Alt+Fem: FSgStem ;
+N+Prop+Alt+Fem: ыPlStem ;

LEXICON мн._[ж_a]
+N+Fem: ыPlStem ;

LEXICON мн._[ж_a]_хлопоты
+N+Fem:ота ыPlNomStem ;
+N+Fem:ота ыPlAccInanStem ;
+N+Fem+Gen+Pl:о́т # ;
+N+Fem:ота PlPeriphStem ;

LEXICON ж_a2
+N+Fem: FSgStem ;
+N+Fem: ыPlStem_ей ;

LEXICON ж_a[2]
+N+Fem: FSgStem ;
+N+Fem: ыPlStem ;
+N+Fem+Gen+Pl+Fac:ей # ;

LEXICON ж_a-
+N+Fem: FSgStem ;
+N+Fem: ыPlStem_Prb ;

LEXICON ж_8a
+N+Fem: FьSgStem ;
+N+Fem: ыPlStem ;

LEXICON мн._[ж_8a]
+N+Fem: ыPlStem ;

LEXICON ж_8a_П2
+N+Fem: FьSgStem ;
+N+Fem: ыPlStem ;
FemLoc2 ;

LEXICON ж_8a_[П2]
+N+Fem: FьSgStem ;
+N+Fem: ыPlStem ;
FemLoc2_Fac ;

LEXICON ж_8a-
+N+Fem: FьSgStem ;
+N+Fem: ыPlStem_Prb ;

LEXICON ж_8a-_П2
+N+Fem: FьSgStem ;
+N+Fem: ыPlStem_Prb ;
FemLoc2 ;

LEXICON FemLoc2
+N+Fem+Loc2+Sg:и́ # ;

LEXICON FemLoc2_Fac
+N+Fem+Loc2+Sg+Fac:и́ # ;

LEXICON ж_b ! This also includes ж_b' because of the fleeting vowel rule in twolc
+N+Fem: FSgEnd ;
+N+Fem: ыPlEnd ;

LEXICON мн._[ж_b]
+N+Fem: ыPlEnd ;

LEXICON ж_b_Р._мн._затрудн.
+N+Fem: FSgEnd ;
+N+Fem: ыPlEnd_gPrb ;

LEXICON ж_b- ! This also includes ж_b'- because of the fleeting vowel rule in twolc
+N+Fem: FSgEnd ;
+N+Fem: ыPlEnd_Prb ;

LEXICON соб-ж_b?
+N+Prop+Alt+Fem: FSgEnd ;
+N+Prop+Alt+Fem: ыPlEnd_gPrb ;

LEXICON ж_8b ! This also includes ж_b' because of the fleeting vowel rule in twolc
+N+Fem: FьSgEnd ;
+N+Fem: ыPlEnd ;

LEXICON ж_d
+N+Fem: FSgEnd ;
+N+Fem: ыPlStem ;

LEXICON ж_d-
+N+Fem: FSgEnd ;
+N+Fem: ыPlStem_Prb ;

LEXICON ж_d' ! Shifting retraction
+N+Fem: FSgRetr ;
+N+Fem: ыPlStem ;

LEXICON ж_e
+N+Fem: FSgStem ;
+N+Fem: ыPlAnom ;

LEXICON мн._[ж_e]
+N+Fem: ыPlAnom ;

LEXICON мн._[ж_e]_она
+N+Fem:она ыPlNomStem ;
+N+Fem:она ыPlAccInanStem ;
+N+Fem+Gen+Pl:о́н # ;
+N+Fem:она PlPeriphEnd ;

LEXICON мн._[ж_e]_оса
+N+Fem:оса ыPlNomStem ;
+N+Fem:оса ыPlAccInanStem ;
+N+Fem+Gen+Pl:о́с # ;
+N+Fem:оса PlPeriphEnd ;

LEXICON ж_8e
+N+Fem: FьSgStem ;
+N+Fem: ыPlAnom ;

LEXICON мн._[ж_8e]
+N+Fem: ыPlAnom ;

LEXICON ж_8e_церковь
:ь ж_8e ;
+N+Fem: PlPeriphEnd ;

LEXICON ж_8e_П2
+N+Fem: FьSgStem ;
+N+Fem: ыPlAnom ;
FemLoc2 ;

LEXICON ж_8e_П2_дверь
ж_8e_П2 ;
+N+Fem+Ins+Pl:%>ьми́ # ;

LEXICON ж_8e_[П2]
+N+Fem: FьSgStem ;
+N+Fem: ыPlAnom ;
FemLoc2_Fac ;

LEXICON ж_8e-
+N+Fem: FьSgStem ;
+N+Fem: ыPlAnom_Prb ;

LEXICON ж_8f ! This includes 8f'' (twolc handles InstSg stress)
+N+Fem: FьSgEnd ;
+N+Fem: ыPlAnom ;

LEXICON ж_8f_П2 ! This includes 8f'' (twolc handles InstSg stress)
+N+Fem: FьSgEnd ;
+N+Fem: ыPlAnom ;
FemLoc2 ;

LEXICON ж_f
+N+Fem: FSgEnd ;
+N+Fem: ыPlAnom ;

LEXICON ж_f_озда
+N+Fem:озда́ FSgEnd ;
+N+Fem:озда́ ыPlNomStem ;
+N+Fem:озда́ ыPlAccInanStem ;
+N+Fem+Gen+Pl:о́зд # ;
+N+Fem:озда́ PlPeriphEnd ;

LEXICON ж_f_ода
+N+Fem:ода́ FSgEnd ;
+N+Fem:ода́ ыPlNomStem ;
+N+Fem:ода́ ыPlAccInanStem ;
+N+Fem+Gen+Pl:о́д # ;
+N+Fem:ода́ PlPeriphEnd ;

LEXICON ж_f_ёза
+N+Fem:еза́ FSgEnd ;
+N+Fem:еза́ ыPlNomStem ;
+N+Fem:еза́ ыPlAccInanStem ;
+N+Fem+Gen+Pl:ёз # ;
+N+Fem:еза́ PlPeriphEnd ;

LEXICON ж_f_она
+N+Fem:она́ FSgEnd ;
+N+Fem:она́ ыPlNomStem ;
+N+Fem:она́ ыPlAccInanStem ;
+N+Fem+Gen+Pl:о́н # ;
+N+Fem:она́ PlPeriphEnd ;

LEXICON ж_f_ира
+N+Fem:ира́ FSgEnd ;
+N+Fem:ира́ ыPlNomStem ;
+N+Fem:ира́ ыPlAccInanStem ;
+N+Fem+Gen+Pl:и́р # ;
+N+Fem:ира́ PlPeriphEnd ;

LEXICON ж_f_ора
+N+Fem:ора́ FSgEnd ;
+N+Fem:ора́ ыPlNomStem ;
+N+Fem:ора́ ыPlAccInanStem ;
+N+Fem+Gen+Pl:о́р # ;
+N+Fem:ора́ PlPeriphEnd ;

LEXICON ж_f_оса
+N+Fem:оса́ FSgEnd ;
+N+Fem:оса́ ыPlNomStem ;
+N+Fem:оса́ ыPlAccInanStem ;
+N+Fem+Gen+Pl:о́с # ;
+N+Fem:оса́ PlPeriphEnd ;

LEXICON ж_f_простыня
+N+Fem:ыня́ FSgEnd ;
+N+Fem:ыня́ ыPlNomStem ;
+N+Fem:ыня́ ыPlAccInanStem ;
+N+Fem+Gen+Pl:ыне́й # ;
+N+Fem+Gen+Pl:ы́нь # ;
+N+Fem:ыня́ PlPeriphEnd ;

LEXICON ж_f' ! Anomolous retraction
+N+Fem: FSgRetr ;
+N+Fem: ыPlAnom ;

LEXICON ж_f'_ова
+N+Fem:ова́ FSgRetr ;
+N+Fem:ова́ ыPlNomStem ;
+N+Fem:ова́ ыPlAccInanStem ;
+N+Fem+Gen+Pl:о́в # ;
+N+Fem:ова́ PlPeriphEnd ;

LEXICON ж_f'_ёда
+N+Fem:еда́ FSgRetr ;
+N+Fem:еда́ ыPlNomStem ;
+N+Fem:еда́ ыPlAccInanStem ;
+N+Fem+Gen+Pl:ёд # ;
+N+Fem:еда́ PlPeriphEnd ;

LEXICON ж_f'_озда
+N+Fem:озда́ FSgRetr ;
+N+Fem:озда́ ыPlNomStem ;
+N+Fem:озда́ ыPlAccInanStem ;
+N+Fem+Gen+Pl:о́зд # ;
+N+Fem:озда́ PlPeriphEnd ;

LEXICON ж_f'_ода
+N+Fem:ода́ FSgRetr ;
+N+Fem:ода́ ыPlNomStem ;
+N+Fem:ода́ ыPlAccInanStem ;
+N+Fem+Gen+Pl:о́д # ;
+N+Fem:ода́ PlPeriphEnd ;

LEXICON ж_f'_она
+N+Fem:она́ FSgRetr ;
+N+Fem:она́ ыPlNomStem ;
+N+Fem:она́ ыPlAccInanStem ;
+N+Fem+Gen+Pl:о́н # ;
+N+Fem:она́ PlPeriphEnd ;

LEXICON ж_f'_оса
+N+Fem:оса́ FSgRetr ;
+N+Fem:оса́ ыPlNomStem ;
+N+Fem:оса́ ыPlAccInanStem ;
+N+Fem+Gen+Pl:о́с # ;
+N+Fem:оса́ PlPeriphEnd ;

LEXICON жо_0
+N+Fem: Indecl_Sg ;
+N+Fem: Indecl_PlAnim ;

LEXICON соб-жо_0
+N+Prop+Fem: Indecl_Sg ;
+N+Prop+Fem: Indecl_PlAnim ;

LEXICON жо_a
+N+Fem: FSgStem ;
+N+Fem: ыPlStem_anim ;

LEXICON жо_a_ица_ицы/ы
+N+Fem:ица FSgStem ;
+N+Fem:ица ыPlStem_anim ;
+N+Fem:а ыPlStem_anim ;

LEXICON соб-жо_a
+N+Prop+Fem: FSgStem ;
+N+Prop+Fem: ыPlStem_anim ;

LEXICON жо_a2
+N+Fem: FSgStem ;
+N+Fem: ыPlStem_anim_ей ;

LEXICON жо_a[2]
+N+Fem: FSgStem ;
+N+Fem: ыPlStem_anim ;
+N+Fem+Gen+Pl+Fac:%>ей # ;

LEXICON жо_8a
+N+Fem: FьSgStem ;
+N+Fem: ыPlStem_anim ;

LEXICON жо_b ! This also includes ж_b' because of the fleeting vowel rule in twolc
+N+Fem: FSgEnd ;
+N+Fem: ыPlEnd_anim ;

LEXICON жо_b_Р._мн._затрудн. ! This also includes ж_b' because of the fleeting vowel rule in twolc
+N+Fem: FSgEnd ;
+N+Fem: ыPlEnd_anim_gPrb ;

LEXICON жо_b- ! This also includes ж_b' because of the fleeting vowel rule in twolc
+N+Fem: FSgEnd ;
+N+Fem: ыPlEnd_anim_Prb ;

LEXICON жо_8b ! This also includes ж_b' because of the fleeting vowel rule in twolc
+N+Fem: FьSgEnd ;
+N+Fem: ыPlEnd_anim ;

LEXICON жо_d
+N+Fem: FSgEnd ;
+N+Fem: ыPlStem_anim ;

LEXICON жо_8e
+N+Fem: FьSgStem ;
+N+Fem: ыPlAnom_anim ;

LEXICON жо_8e_дочь
+N+Fem+Nom+Sg:до́чь # ;
+N+Fem+Acc+Sg:до́чь # ;
+N+Fem+Gen+Sg:до́чери # ;
+N+Fem+Loc+Sg:до́чери # ;
+N+Fem+Dat+Sg:до́чери # ;
+N+Fem+Ins+Sg:до́черью # ;
+N+Fem:до́черь ыPlAnom_anim ;
+N+Fem+Ins+Pl:дочерьми́ # ;

LEXICON жо_8e_лошадь
жо_8e ;
+N+Fem+Ins+Pl:ми́ # ;

LEXICON жо_8e_мать
+N+Fem+Nom+Sg:ма́ть # ;
+N+Fem+Acc+Sg:ма́ть # ;
+N+Fem+Gen+Sg:ма́тери # ;
+N+Fem+Loc+Sg:ма́тери # ;
+N+Fem+Dat+Sg:ма́тери # ;
+N+Fem+Ins+Sg:ма́терью # ;
+N+Fem:ма́терь ыPlAnom_anim ;

LEXICON жо_f
+N+Fem: FSgEnd ;
+N+Fem: ыPlAnom_anim ;

LEXICON ж_[п_a]
+N+Fem+Nom+Sg:%>ая # ;
+N+Fem+Acc+Sg:%>ую # ;
FSg_SubstAdj_Stem ;
+N+Fem+Nom+Pl:%>ые # ;
+N+Fem+Acc+Pl+Inan:%>ые # ;
Pl_SubstAdj_Stem ;

LEXICON ж_[п_b]
+N+Fem+Nom+Sg:%>а́я # ;
+N+Fem+Acc+Sg:%>у́ю # ;
FSg_SubstAdj_End ;
+N+Fem+Nom+Pl:%>ы́е # ;
+N+Fem+Acc+Pl+Inan:%>ы́е # ;
Pl_SubstAdj_End ;

LEXICON FSg_SubstAdj_Stem
+N+Fem+Gen+Sg:%>ой # ;
+N+Fem+Loc+Sg:%>ой # ;
+N+Fem+Dat+Sg:%>ой # ;
+N+Fem+Ins+Sg:%>ой # ;

LEXICON жо_[п_a]
+N+Fem+Nom+Sg:%>ая # ;
+N+Fem+Acc+Sg:%>ую # ;
FSg_SubstAdj_Stem ;
+N+Fem+Nom+Pl:%>ые # ;
+N+Fem+Acc+Pl+Anim:%>ых # ;
Pl_SubstAdj_Stem ;

LEXICON жо_[п_b]
+N+Fem+Nom+Sg:%>а́я # ;
+N+Fem+Acc+Sg:%>у́ю # ;
FSg_SubstAdj_End ;
+N+Fem+Nom+Pl:%>ы́е # ;
+N+Fem+Acc+Pl+Anim:%>ы́х # ;
Pl_SubstAdj_End ;

LEXICON ф._[мс_a]
+N+Prop+Sur: Surname_stem ;

LEXICON ф._0
+N+Prop+Sur: Surname_0 ;

LEXICON Surname_stem
+Msc+Nom+Sg: # ;
+Msc+Acc+Sg+Inan: # ;
+Msc+Acc+Sg+Anim:%>а # ;
+Msc+Gen+Sg:%>а # ;
+Msc+Loc+Sg:%>е # ;
+Msc+Dat+Sg:%>у # ;
+Msc+Ins+Sg:%>ым # ;
+Fem+Nom+Sg:%>а # ;
+Fem+Acc+Sg:%>у # ;
+Fem+Gen+Sg:%>ой # ;
+Fem+Loc+Sg:%>ой # ;
+Fem+Dat+Sg:%>ой # ;
+Fem+Ins+Sg:%>ой # ;
+Fem+Ins+Sg+Fac:%>ою # ;
+Nom+Pl:%>ы # ;
+Acc+Pl+Inan:%>ы # ;
+Acc+Pl+Anim:%>ых # ;
+Gen+Pl:%>ых # ;
+Loc+Pl:%>ых # ;
+Dat+Pl:%>ым # ;
+Ins+Pl:%>ыми # ;

LEXICON Surname_0
+Msc+Nom+Sg: # ;
+Msc+Acc+Sg+Inan: # ;
+Msc+Acc+Sg+Anim: # ;
+Msc+Gen+Sg: # ;
+Msc+Loc+Sg: # ;
+Msc+Dat+Sg: # ;
+Msc+Ins+Sg: # ;
+Fem+Nom+Sg: # ;
+Fem+Acc+Sg: # ;
+Fem+Gen+Sg: # ;
+Fem+Loc+Sg: # ;
+Fem+Dat+Sg: # ;
+Fem+Ins+Sg: # ;
+Fem+Ins+Sg+Fac: # ;
+Nom+Pl: # ;
+Acc+Pl+Inan: # ;
+Acc+Pl+Anim: # ;
+Gen+Pl: # ;
+Loc+Pl: # ;
+Dat+Pl: # ;
+Ins+Pl: # ;

LEXICON FSg_SubstAdj_End
+N+Msc+Gen+Sg:%>о́й # ;
+N+Msc+Loc+Sg:%>о́й # ;
+N+Msc+Dat+Sg:%>о́й # ;
+N+Msc+Ins+Sg:%>о́й # ;

LEXICON FSgStem
+Acc+Sg:%>у # ;
FSgObliqStem ;

LEXICON FSgStem_anim ! Only used for masculine nouns!
+Acc+Sg+Anim:%>у # ;
FSgObliqStem ;

LEXICON FSgObliqStem
+Nom+Sg:%>а # ;
+Gen+Sg:%>ы # ;
+Loc+Sg:%>е # ;
+Dat+Sg:%>е # ;
+Ins+Sg:%>ой # ;

LEXICON FSgEnd
+Acc+Sg:%>у́ # ;
FSgObliqEnd ;

LEXICON FSgEnd_anim
+Acc+Sg+Anim:%>у́ # ;
FSgObliqEnd ;

LEXICON FSgRetr
+Acc+Sg:%>у # ;
FSgObliqEnd ;

LEXICON FSgObliqEnd
+Nom+Sg:%>а́ # ;
+Gen+Sg:%>ы́ # ;
+Loc+Sg:%>е́ # ;
+Dat+Sg:%>е́ # ;
+Ins+Sg:%>о́й # ;

LEXICON FьSgStem
+Nom+Sg:%>ь # ;
+Acc+Sg:%>ь # ;
+Gen+Sg:%>и # ;
+Loc+Sg:%>и # ;
+Dat+Sg:%>и # ;
+Ins+Sg:%>ью # ;

LEXICON FьSgEnd
FьSgEnd_MostCases ;
+Ins+Sg:%>ью # ;

LEXICON FьSgEnd_MostCases
+Nom+Sg:%>ь # ;
+Acc+Sg:%>ь # ;
+Gen+Sg:%>и́ # ;
+Loc+Sg:%>и́ # ;
+Dat+Sg:%>и́ # ;

LEXICON Indecl_Sg
+Nom+Sg: # ;
+Acc+Sg: # ;
+Gen+Sg: # ;
+Loc+Sg: # ;
+Dat+Sg: # ;
+Ins+Sg: # ;

LEXICON Indecl_SgInan
+Nom+Sg: # ;
+Acc+Sg+Inan: # ;
+Gen+Sg: # ;
+Loc+Sg: # ;
+Dat+Sg: # ;
+Ins+Sg: # ;

LEXICON Indecl_SgAnim
+Nom+Sg: # ;
+Acc+Sg+Anim: # ;
+Gen+Sg: # ;
+Loc+Sg: # ;
+Dat+Sg: # ;
+Ins+Sg: # ;

LEXICON Indecl_PlInan
+Nom+Pl: # ;
+Acc+Pl+Inan: # ;
+Gen+Pl: # ;
+Loc+Pl: # ;
+Dat+Pl: # ;
+Ins+Pl: # ;

LEXICON Indecl_PlAnim
+Nom+Pl: # ;
+Acc+Pl+Anim: # ;
+Gen+Pl: # ;
+Loc+Pl: # ;
+Dat+Pl: # ;
+Ins+Pl: # ;

LEXICON ыPlStem      !Inanimate
ыPlNomStem ;
ыPlAccInanStem ;
PlObliqStem ;

LEXICON ыPlStem_Fac      !Inanimate
ыPlNomStem_Fac ;
ыPlAccInanStem_Fac ;
PlObliqStem_Fac ;

LEXICON ыPlStem_Prb      !Inanimate
ыPlNomStem_Prb ;
ыPlAccInanStem_Prb ;
PlObliqStem_Prb ;

LEXICON ыPlStem_ей      !Inanimate
ыPlNomStem ;
ыPlAccInanStem ;
+Gen+Pl:%>ей # ;
PlPeriphStem ;

LEXICON ыPlStem_ов      !Inanimate
ыPlNomStem ;
ыPlAccInanStem ;
+Gen+Pl:%>ов%^G # ;
PlPeriphStem ;

LEXICON ыPlStem_zero      !Inanimate
ыPlNomStem ;
ыPlAccInanStem ;
+Gen+Pl:%> # ;
PlPeriphStem ;

LEXICON ыPlEnd       !Inanimate
ыPlNomEnd ;
ыPlAccInanEnd ;
PlObliqEnd ;

LEXICON ыPlEnd_Prb       !Inanimate
ыPlNomEnd_Prb ;
ыPlAccInanEnd_Prb ;
PlObliqEnd_Prb ;

LEXICON ыPlEnd_gPrb
ыPlNomEnd ;
ыPlAccInanEnd ;
+Gen+Pl+Prb:%>о́в # ;
PlPeriphEnd ;

LEXICON ыPlEnd_ей      !Inanimate
ыPlNomEnd ;
ыPlAccInanEnd ;
+Gen+Pl:%>е́й # ;
PlPeriphEnd ;

LEXICON ыPlEnd_ов      !Inanimate
ыPlNomEnd ;
ыPlAccInanEnd ;
+Gen+Pl:%>о́в%^G # ;
PlPeriphEnd ;

LEXICON ыPlEnd_ов_Prb      !Inanimate
ыPlNomEnd_Prb ;
ыPlAccInanEnd_Prb ;
+Gen+Pl+Prb:%>о́в%^G # ;
PlPeriphEnd_Prb ;

LEXICON ыPlEnd_zero      !Inanimate
ыPlNomEnd ;
ыPlAccInanEnd ;
+Gen+Pl:%> # ;
PlPeriphEnd ;

LEXICON ыPlAnom      !Inanimate
ыPlNomStem ;
ыPlAccInanStem ;
PlObliqEnd ;

LEXICON ыPlAnom_Prb      !Inanimate
ыPlNomStem_Prb ;
ыPlAccInanStem_Prb ;
PlObliqEnd_Prb ;

LEXICON ыPlAnom_zero      !Inanimate
ыPlNomStem ;
ыPlAccInanStem ;
+Gen+Pl:%> # ;
PlPeriphEnd ;

LEXICON аPlStem      !Inanimate
аPlNomStem ;
аPlAccInanStem ;
PlObliqStem ;

LEXICON аPlStem_Prb      !Inanimate
аPlNomStem_Prb ;
аPlAccInanStem_Prb ;
PlObliqStem_Prb ;

LEXICON аPlStem_gPrb      !Inanimate
аPlNomStem ;
аPlAccInanStem ;
+Gen+Pl+Prb:%>ов # ;
PlPeriphStem ;

LEXICON аPlStem_ов      !Inanimate
аPlNomStem ;
аPlAccInanStem ;
+Gen+Pl:%>ов%^G # ;
PlPeriphStem ;

LEXICON аPlStem_zero      !Inanimate
аPlNomStem ;
аPlAccInanStem ;
+Gen+Pl:%> # ;
PlPeriphStem ;

LEXICON аPlEnd       !Inanimate
аPlNomEnd ;
аPlAccInanEnd ;
PlObliqEnd ;

LEXICON аPlEnd_Prb       !Inanimate
аPlNomEnd_Prb ;
аPlAccInanEnd_Prb ;
PlObliqEnd_Prb ;

LEXICON аPlEnd_ов      !Inanimate
аPlNomEnd ;
аPlAccInanEnd ;
+Gen+Pl:%>о́в%^G # ;
PlPeriphEnd ;

LEXICON аPlEnd_zero      !Inanimate
аPlNomEnd ;
аPlAccInanEnd ;
+Gen+Pl: # ;
PlPeriphEnd ;

LEXICON аPlAnom       !Inanimate
аPlNomStem ;
аPlAccInanStem ;
PlObliqEnd ;

LEXICON ыPlStem_anim
ыPlNomStem ;
PlAccAnimStem ;
PlObliqStem ;

LEXICON ыPlStem_anim_Fac
ыPlNomStem_Fac ;
PlAccAnimStem_Fac ;
PlObliqStem_Fac ;

LEXICON ыPlStem_anim_Prb
ыPlNomStem_Prb ;
PlAccAnimStem_Prb ;
PlObliqStem_Prb ;

LEXICON ыPlStem_anim_ей      !Inanimate
ыPlNomStem ;
+Acc+Pl+Anim:%>ей # ;
+Gen+Pl:%>ей # ;
PlPeriphStem ;

LEXICON ыPlStem_anim_zero
ыPlNomStem ;
+Acc+Pl+Anim:%> # ;
+Gen+Pl:%> # ;
PlObliqStem ;

LEXICON ыPlEnd_anim
ыPlNomEnd ;
PlAccAnimEnd ;
PlObliqEnd ;

LEXICON ыPlEnd_anim_Prb
ыPlNomEnd_Prb ;
PlAccAnimEnd_Prb ;
PlObliqEnd_Prb ;

LEXICON ыPlEnd_anim_gPrb
ыPlNomEnd ;
+Acc+Pl+Anim+Prb:%>о́в # ;
+Gen+Pl+Prb:%>о́в # ;
PlPeriphEnd ;

LEXICON ыPlEnd_anim_ов
ыPlNomEnd ;
+Acc+Pl+Anim:%>о́в%^G # ;
+Gen+Pl:%>о́в%^G # ;
PlPeriphEnd ;

LEXICON ыPlEnd_anim_ов_Prb
ыPlNomEnd_Prb ;
+Acc+Pl+Anim+Prb:%>о́в%^G # ;
+Gen+Pl+Prb:%>о́в%^G # ;
PlPeriphEnd_Prb ;

LEXICON ыPlAnom_anim
ыPlNomStem ;
PlAccAnimEnd ;
PlObliqEnd ;

LEXICON аPlStem_anim
аPlNomStem ;
PlAccAnimStem ;
PlObliqStem ;

LEXICON аPlStem_anim_Prb
аPlNomStem_Prb ;
PlAccAnimStem_Prb ;
PlObliqStem_Prb ;

LEXICON аPlStem_anim_ов
аPlNomStem ;
+Acc+Pl+Anim:%>о́в%^G # ;
+Gen+Pl:%>о́в%^G # ;
PlPeriphStem ;

LEXICON аPlStem_anim_zero
аPlNomStem ;
+Acc+Pl+Anim:%> # ;
+Gen+Pl:%> # ;
PlPeriphStem ;

LEXICON аPlEnd_anim
аPlNomEnd ;
PlAccAnimEnd ;
PlObliqEnd ;

LEXICON ыPlNomStem
+Nom+Pl:%>ы # ;

LEXICON ыPlNomStem_Fac
+Nom+Pl+Fac:%>ы # ;

LEXICON ыPlNomStem_Prb
+Nom+Pl+Prb:%>ы # ;

LEXICON ыPlNomEnd
+Nom+Pl:%>ы́ # ;

LEXICON ыPlNomEnd_Prb
+Nom+Pl+Prb:%>ы́ # ;

LEXICON ыPlAccInanStem
+Acc+Pl+Inan:%>ы # ;

LEXICON ыPlAccInanStem_Fac
+Acc+Pl+Inan+Fac:%>ы # ;

LEXICON ыPlAccInanStem_Prb
+Acc+Pl+Inan+Prb:%>ы # ;

LEXICON ыPlAccInanEnd
+Acc+Pl+Inan:%>ы́ # ;

LEXICON ыPlAccInanEnd_Prb
+Acc+Pl+Inan+Prb:%>ы́ # ;

LEXICON PlAccAnimStem
+Acc+Pl+Anim:%>ов # ;

LEXICON PlAccAnimStem_Fac
+Acc+Pl+Anim+Fac:%>ов # ;

LEXICON PlAccAnimStem_Prb
+Acc+Pl+Anim+Prb:%>ов # ;

LEXICON PlAccAnimEnd
+Acc+Pl+Anim:%>о́в # ;

LEXICON PlAccAnimEnd_Prb
+Acc+Pl+Anim+Prb:%>о́в # ;

LEXICON аPlNomStem
+Nom+Pl:%>а # ;

LEXICON аPlNomStem_Prb
+Nom+Pl+Prb:%>а # ;

LEXICON аPlNomEnd
+Nom+Pl:%>а́ # ;

LEXICON аPlNomEnd_Prb
+Nom+Pl+Prb:%>а́ # ;

LEXICON аPlAccInanStem
+Acc+Pl+Inan:%>а # ;

LEXICON аPlAccInanStem_Prb
+Acc+Pl+Inan+Prb:%>а # ;

LEXICON аPlAccInanEnd
+Acc+Pl+Inan:%>а́ # ;

LEXICON аPlAccInanEnd_Prb
+Acc+Pl+Inan+Prb:%>а́ # ;

LEXICON PlObliqStem
+Gen+Pl:%>ов # ;
PlPeriphStem ;

LEXICON PlObliqStem_Fac
+Gen+Pl+Fac:%>ов # ;
PlPeriphStem_Fac ;

LEXICON PlObliqStem_Prb
+Gen+Pl+Prb:%>ов # ;
PlPeriphStem_Prb ;

LEXICON PlPeriphStem
+Loc+Pl:%>ах # ;
+Dat+Pl:%>ам # ;
+Ins+Pl:%>ами # ;

LEXICON PlPeriphStem_Fac
+Loc+Pl+Fac:%>ах # ;
+Dat+Pl+Fac:%>ам # ;
+Ins+Pl+Fac:%>ами # ;

LEXICON PlPeriphStem_Prb
+Loc+Pl+Prb:%>ах # ;
+Dat+Pl+Prb:%>ам # ;
+Ins+Pl+Prb:%>ами # ;

LEXICON PlObliqEnd
+Gen+Pl:%>о́в # ;
PlPeriphEnd ;

LEXICON PlObliqEnd_Prb
+Gen+Pl+Prb:%>о́в # ;
PlPeriphEnd_Prb ;

LEXICON PlPeriphEnd
+Loc+Pl:%>а́х # ;
+Dat+Pl:%>а́м # ;
+Ins+Pl:%>а́ми # ;

LEXICON PlPeriphEnd_Prb
+Loc+Pl+Prb:%>а́х # ;
+Dat+Pl+Prb:%>а́м # ;
+Ins+Pl+Prb:%>а́ми # ;

LEXICON Pl_ин
+Nom+Pl:е # ;
+Acc+Pl+Anim: # ;
+Gen+Pl: # ;
PlPeriphEnd ;
