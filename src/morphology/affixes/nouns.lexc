LEXICON м_0
+N+Msc: Indecl_Sg_inan ;
+N+Msc: Indecl_Pl_inan ;

LEXICON соб-м_0
+N+Prop+Sem/Alt+Msc: Indecl_Sg_inan ;
+N+Prop+Sem/Alt+Msc: Indecl_Pl_inan ;

LEXICON м_a
+N+Msc: MSgStem ;
+N+Msc: ыPlStem ;

LEXICON соб-м_a
+N+Prop+Sem/Alt+Msc: MSgStem ;
+N+Prop+Sem/Alt+Msc: ыPlStem ;

LEXICON мн._[м_a]
+N+Msc: ыPlStem ;

LEXICON м_a_П2
м_a ;
MscLoc2_inan ;

LEXICON м_a_[П2]
м_a ;
MscLoc2_inan_Fac ;

LEXICON м_a_[П2]_Р2
м_a ;
MscLoc2_inan_Fac ;
MscGen2_inan ;

LEXICON м_a_Р2
м_a ;
MscGen2_inan ;

LEXICON м_a_ёнок
+N+Msc:ён%^Fок MSgStem ;
+N+Msc:я́т аPlStem_zero ;

LEXICON м_a1_JPl
+N+Msc: MSgStem ;
+N+Msc:%^Fей аPlStem ;

LEXICON м_a2
+N+Msc: MSgStem ;
+N+Msc: ыPlStem_zero ;

LEXICON м_a[2]
м_a ;
+N+Msc+Inan+Pl+Gen+Fac:%> # ;

LEXICON м_a_полдень
+N+Msc+Inan+Sg+Nom:по́лдень # ;
+N+Msc+Inan+Sg+Acc:по́лдень # ;
+N+Msc+Inan+Sg+Gen:полу́дня # ;
+N+Msc+Inan+Sg+Loc:полу́дне # ;
+N+Msc+Inan+Sg+Dat:полу́дню # ;
+N+Msc+Inan+Sg+Ins:полу́днем # ;
+N+Msc+Inan:полу́д%^Fень ыPlStem ;

LEXICON м_a-
+N+Msc: MSgStem ;
+N+Msc: ыPlStem_Prb ;

LEXICON м_a-_П2
м_a- ;
MscLoc2_inan ;

LEXICON м_a-_П2_Р2
м_a- ;
MscLoc2_inan ;
MscGen2_inan ;

LEXICON м_a-_[П2]_Р2
м_a- ;
MscLoc2_inan_Fac ;
MscGen2_inan ;

LEXICON м_a-_Р2
+N+Msc: MSgStem ;
+N+Msc: ыPlStem_Prb ;

LEXICON м_b
+N+Msc: MSgEnd ;
+N+Msc: ыPlEnd ;

LEXICON м_b_ок_ы
+N+Msc:%^Fо́к MSgEnd ;
+N+Msc: ыPlEnd ;

LEXICON мн._[м_b]
+N+Msc: ыPlEnd ;

LEXICON м_b_П2
м_b ;
MscLoc2_inan ;

LEXICON м_b_[П2]
м_b ;
MscLoc2_inan_Fac ;

LEXICON м_b_П2_Р2
м_b ;
MscLoc2_inan ;
MscGen2_inan ;

LEXICON м_b_Р2
м_b ;
MscGen2_inan ;

LEXICON м_b1
+N+Msc: MSgEnd ;
+N+Msc: аPlEnd ;

LEXICON м_b1_JPl
+N+Msc: MSgEnd ;
+N+Msc:%^Fей аPlEnd ;

LEXICON м_b1_ь_JPl
+N+Msc:ь MSgEnd ;
+N+Msc:%^Fей аPlEnd ;

LEXICON мн._[м_b1]
+N+Msc: аPlEnd ;

LEXICON м_b2
+N+Msc: MSgEnd ;
+N+Msc: ыPlEnd_zero ;

LEXICON м_c
+N+Msc: MSgStem ;
+N+Msc: ыPlEnd ;

LEXICON м_c_П2
м_c ;
MscLoc2_inan ;

LEXICON м_c_[П2]
м_c ;
MscLoc2_inan_Fac ;

LEXICON м_c_П2_Р2
м_c ;
MscLoc2_inan ;
MscGen2_inan ;

LEXICON м_c_[П2]_Р2
м_c ;
MscLoc2_inan_Fac ;
MscGen2_inan ;

LEXICON м_c_Р2
м_c ;
MscGen2_inan ;

LEXICON м_c-
+N+Msc: MSgStem ;
+N+Msc: ыPlEnd_Prb ;

LEXICON м_c-_П2
+N+Msc: MSgStem ;
+N+Msc: ыPlEnd_Prb ;
MscLoc2_inan ;

LEXICON м_c-_[П2]_Р2
+N+Msc: MSgStem ;
+N+Msc: ыPlEnd_Prb ;
MscLoc2_inan_Fac ;
MscGen2_inan ;

LEXICON м_c-_Р2
+N+Msc: MSgStem ;
+N+Msc: ыPlEnd_Prb ;
MscGen2_inan ;

LEXICON м_c1
+N+Msc: MSgStem ;
+N+Msc: аPlEnd ;

LEXICON м_c1_П2
м_c1 ;
MscLoc2_inan ;

LEXICON м_c1_П2_год
:го́д м_c1_П2 ;
+N+Msc+Inan+Pl+Gen:ле́т # ;

LEXICON м_c1_[П2]
м_c1 ;
MscLoc2_inan_Fac ;

LEXICON м_c1_П2_Р2
м_c1 ;
MscLoc2_inan ;
MscGen2_inan ;

LEXICON м_c1_[П2]_Р2
м_c1 ;
MscLoc2_inan_Fac ;
MscGen2_inan ;

LEXICON м_c1_Р2
м_c1 ;
MscGen2_inan ;

LEXICON м_c1-
+N+Msc: MSgStem ;
+N+Msc: аPlEnd_Prb ;

LEXICON м_c1-_Р2
м_c1- ;
MscGen2_inan ;

LEXICON м_c12_П2
+N+Msc: MSgStem ;
+N+Msc: аPlEnd_zero ;
MscLoc2_inan ;

LEXICON м_c2
+N+Msc: MSgStem ;
+N+Msc: ыPlEnd_zero ;

LEXICON м_d1_JPl
+N+Msc: MSgEnd ;
+N+Msc:%^Fей аPlStem ;

LEXICON м_d1_К_ЧЬ
+N+Msc:к MSgEnd ;
+N+Msc:ч%^Fей аPlStem ;

LEXICON м_d1_П2_JPl
+N+Msc: MSgEnd ;
+N+Msc:%^Fей аPlStem ;
MscLoc2_inan ;

LEXICON м_d1_П2_К_ЧЬ
+N+Msc:к MSgEnd ;
:к MscLoc2_inan ;
+N+Msc:ч%^Fей аPlStem ;

LEXICON м_d2
+N+Msc: MSgEnd ;
+N+Msc: ыPlStem_zero ;

LEXICON м_e
+N+Msc: MSgStem ;
+N+Msc: ыPlAnom ;

LEXICON м_e_П2
м_e ;
MscLoc2_inan ;

LEXICON м_e_П2_год
:го́д м_e_П2 ;
+N+Msc+Inan+Pl+Gen:ле́т # ;

LEXICON м_e2_волос
+N+Msc:ос MSgStem ;
+N+Msc:ос ыPlNomStem ;
+N+Msc:ос ыPlAccStem ;
+N+Msc+Inan+Pl+Gen:о́с # ;
+N+Msc:ос PlPeriphEnd ;

LEXICON мн._[м_e]
+N+Msc: ыPlAnom ;

LEXICON м_f
+N+Msc: MSgEnd ;
+N+Msc: ыPlAnom ;

LEXICON м_8b ! путь
+N+Msc: FьSgEnd_MostCases ;
+N+Msc+Inan+Sg+Ins:%>о́м # ;
+N+Msc: ыPlEnd ;

LEXICON м_[ж_a]
+N+Msc: FSgStem ;
+N+Msc: ыPlStem ;

LEXICON м_[п_a]
+N+Msc+Inan+Sg+Nom:%>ый # ;
+N+Msc+Inan+Sg+Acc:%>ый # ;
MSg_SubstAdj_Stem ;
+N+Msc+Inan+Pl+Nom:%>ые # ;
+N+Msc+Inan+Pl+Acc:%>ые # ;
+N+Msc: Pl_SubstAdj_Stem ;

LEXICON м_[с_a1]
+N+Msc: NSgStem ;
+N+Msc: ыPlStem ;

LEXICON м_[с_a1]_5
м_[с_a1] ;

LEXICON м_[с_a[1]]
+N+Msc: NSgStem ;
+N+Msc: аPlStem ;
+N+Msc: ыPlStem_Fac ;

LEXICON м_[п_b]
+N+Msc+Inan+Sg+Nom:%>о́й # ;
+N+Msc+Inan+Sg+Acc:%>о́й # ;
MSg_SubstAdj_End ;
+N+Msc+Inan+Pl+Nom:%>ы́е # ;
+N+Msc+Inan+Pl+Acc:%>ы́е # ;
+N+Msc: Pl_SubstAdj_End ;

LEXICON мо_0
+N+Msc: Indecl_Sg_anim ;
+N+Msc: Indecl_Pl_anim ;

LEXICON мо_a
+N+Msc: MSgStem_anim ;
+N+Msc: ыPlStem_anim ;

LEXICON соб-мо_a
+N+Prop+Sem/Ant+Msc: MSgStem_anim ;
+N+Prop+Sem/Ant+Msc: ыPlStem_anim ;
о:%>о Patronymic_stem ;

LEXICON соб-мо_a_й
й+N+Prop+Sem/Ant+Msc:й MSgStem_anim ;
й+N+Prop+Sem/Ant+Msc:й ыPlStem_anim ;
е:%>е Patronymic_stem ;

LEXICON соб-мо_a_ь
ь+N+Prop+Sem/Ant+Msc:ь MSgStem_anim ;
ь+N+Prop+Sem/Ant+Msc:ь ыPlStem_anim ;
е:%>е Patronymic_stem ;

LEXICON соб-мо_a_26
ий+N+Prop+Sem/Ant+Msc:ий MSgStem_anim ;
ий+N+Prop+Sem/Ant+Msc:ий ыPlStem_anim ;
ье:%>ье Patronymic_stem ;

LEXICON мо_a_Plья_ьев
+N+Msc: MSgStem_anim ;
+N+Msc:%^Fей аPlStem_anim ;

LEXICON мо_a_ин_ева ! хозяин
+N+Msc:ин MSgStem_anim ;
+N+Msc:ево аPlStem_anim ;

LEXICON мо_a2
+N+Msc: MSgStem_anim ;
+N+Msc: ыPlStem_anim_zero ;

LEXICON мо_a2_0_е ! цыган
+N+Msc: MSgStem_anim ;
+N+Msc: Pl_ин_anim ;

LEXICON мо_a[2]
+N+Msc: MSgStem_anim ;
+N+Msc: ыPlStem_anim ;
+N+Msc+Anim+Pl+Gen+Fac:%> # ;

LEXICON мо_a-
+N+Msc: MSgStem_anim ;
+N+Msc: ыPlStem_anim_Prb ;

LEXICON мо_a-_господь
+N+Msc+Anim+Sg+Nom:госпо́дь # ;
+N+Msc+Anim+Sg+Acc:го́спода # ;
+N+Msc:го́спод MSgObliqStem_anim ;
+N+Msc:го́спод ыPlStem_anim_Prb ;

LEXICON мо_a_ин_Root
+N+Msc:ин MSgStem_anim ;
+N+Msc: Pl_ин_anim ;

LEXICON мо_a_ин_Suffix
+N+Msc:и́н MSgStem_anim ;
+N+Msc: Pl_ин_anim ;

LEXICON мо_a_ин_ы
+N+Msc:и́н MSgStem_anim ;
+N+Msc: ыPlStem_anim ;

LEXICON мо_a_шурин
+N+Msc:ин MSgStem_anim ;
+N+Msc:ин ыPlStem_anim ;
+N+Msc:%^Fей аPlEnd_anim ;

LEXICON мо_a_ёнок
+N+Msc:ён%^Fок MSgStem_anim ;
+N+Msc:я́та аPlStem_anim ;

LEXICON мо_a_ёнок_еня́та
+N+Msc:ён%^Fок MSgStem_anim ;
+N+Msc:еня́та аPlStem_anim ;

LEXICON мо_a_о́нок_еня́та
+N+Msc:о́н%^Fок MSgStem_anim ;
+N+Msc:еня́та аPlStem_anim ;

LEXICON мо_a_ёночек
+N+Msc:ёноч%^Fек MSgStem_anim ;
+N+Msc:я́т%^Fока ыPlStem_anim ;

LEXICON мо_a_ребёночек
+N+Msc:ребёноч%^Fек MSgStem_anim ;
+N+Msc:де́т%^Fоко ыPlStem_anim ;

LEXICON мо_a_о́нок
+N+Msc:о́н%^Fок MSgStem_anim ;
+N+Msc:а́та аPlStem_anim ;

LEXICON мо_a_о́ночек
+N+Msc:о́ноч%^Fек MSgStem_anim ;
+N+Msc:а́т%^Fока ыPlStem_anim ;

LEXICON мо_a_щено́чек
+N+Msc:о́ч%^Fек MSgStem_anim ;
+N+Msc:о́ч%^Fек ыPlStem_anim ;
+N+Msc:я́т%^Fока ыPlStem_anim ;

LEXICON мо_a_щено́к
+N+Msc:%^Fо́к MSgStem_anim ;
+N+Msc:%^Fо́к ыPlStem_anim ;
+N+Msc:я́та аPlStem_anim ;

LEXICON мн._[мо_a]
+N+Msc: ыPlStem_anim ;

LEXICON мо_a_SPl
+N+Msc: MSgStem_anim ;
+N+Msc:ь ыPlStem_anim ;

LEXICON мо_b
+N+Msc: MSgEnd_anim ;
+N+Msc: ыPlEnd_anim ;

LEXICON мо_b_христос
+N+Msc+Anim+Sg+Nom:о́с # ;
+N+Msc+Anim+Sg+Acc:а́ # ;
+N+Msc: MSgObliqEnd_anim ;
+N+Msc: ыPlEnd_anim ;

LEXICON соб-мо_b_христос
+N+Prop+Sem/Ant+Msc+Anim+Sg+Nom:о́с # ;
+N+Prop+Sem/Ant+Msc+Anim+Sg+Acc:а́ # ;
+N+Prop+Sem/Ant+Msc: MSgObliqEnd_anim ;
+N+Prop+Sem/Ant+Msc: ыPlEnd_anim ;

LEXICON соб-мо_b
+N+Prop+Sem/Ant+Msc: MSgEnd_anim ;
+N+Prop+Sem/Ant+Msc: ыPlEnd_anim ;
о:%>о́ Patronymic_stem ;

LEXICON мо_c
+N+Msc: MSgStem_anim ;
+N+Msc: ыPlEnd_anim ;

LEXICON мо_c_барин
+N+Msc:ба́рин MSgStem_anim ;
+N+Msc:госпо́да́ аPlEnd_anim ;

LEXICON мо_c1
+N+Msc: MSgStem_anim ;
+N+Msc: ыPlEnd_anim ;

LEXICON мо_c1_ин_а
+N+Msc:и́н MSgStem_anim ;
+N+Msc:о аPlEnd_anim ;

LEXICON мо_c1_JPl-ЕЙ
+N+Msc: MSgStem_anim ;
+N+Msc:%^Fе́йя́ аPlEnd_anim ;

LEXICON мо_c1_Plовья-ей
+N+Msc: MSgStem_anim ;
+N+Msc:ов%^Fе́йя́ аPlEnd_anim ;

LEXICON мо_c1_Plовья-ёв
+N+Msc: MSgStem_anim ;
+N+Msc:ов%^Fе́й аPlEnd_anim ;

LEXICON мо_c1_Г_ЗЬ
+N+Msc:г MSgStem_anim ;
+N+Msc:з%^Fе́йя́ аPlEnd_anim ;

LEXICON мо_c1_Ь_ЬЯ-ЕЙ
+N+Msc:ь MSgStem_anim ;
+N+Msc:%^Fе́йя аPlEnd_anim ;

LEXICON мо_c1_Ь_ЬЯ-ЁВ
+N+Msc:ь MSgStem_anim ;
+N+Msc:%^Fе́й аPlEnd_anim ;

LEXICON мо_d
+N+Msc: MSgEnd_anim ;
+N+Msc: ыPlStem_anim ;

LEXICON мо_e
+N+Msc: MSgStem_anim ;
+N+Msc: ыPlAnom_anim ;

LEXICON мо_e_черт
+N+Msc:ёрт MSgStem_anim ;
+N+Msc:е́рть ыPlAnom_anim ;

LEXICON мо_e_зверь
мо_e ;
+N+Msc+Anim+Pl+Ins+Fac:%>ьми́ # ;

LEXICON мо_f
+N+Msc: MSgEnd_anim ;
+N+Msc: ыPlAnom_anim ;

LEXICON мо_[со_a]
+N+Msc: NSgStem_anim ;
+N+Msc: аPlStem_anim ;     !Cyrillic 'а' in 'аPl'

LEXICON мо_ребенок
+N+Msc:ребён%^Fок MSgStem_anim ;
+N+Msc:де́ть ыPlNomStem_anim ;
+N+Msc+Anim+Pl+Acc:дете́й # ;
+N+Msc+Anim+Pl+Gen:дете́й # ;
+N+Msc+Anim+Pl+Loc:де́тях # ;
+N+Msc+Anim+Pl+Dat:де́тям # ;
+N+Msc+Anim+Pl+Ins:детьми́ # ;

LEXICON мо_человек
! +N+Msc:челове́к ыPlStem_anim_Use/Obs ;
+N+Msc+Anim+Pl+Gen:челове́к # ;
мо_человек2 ;

LEXICON мо_человек2
+N+Msc:челове́к MSgStem_anim ;
+N+Msc:лю́дь ыPlNomStem_anim ;
+N+Msc+Anim+Pl+Acc:люде́й # ;
+N+Msc+Anim+Pl+Gen:люде́й # ;
+N+Msc+Anim+Pl+Loc:лю́дях # ;
+N+Msc+Anim+Pl+Dat:лю́дям # ;
+N+Msc+Anim+Pl+Ins:людьми́ # ;

LEXICON мо-жо_0
мо_0 ;
жо_0 ;

LEXICON мо_[жо_a]
+N+Msc: FSgStem_anim ;
+N+Msc: ыPlStem_anim ;

LEXICON мо_[жо_a2]
+N+Msc: FSgStem_anim ;
+N+Msc: ыPlStem_anim_ей ;

LEXICON мо_[жо_a[2]]
+N+Msc: FSgStem_anim ;
+N+Msc: ыPlStem_anim ;
+N+Msc+Anim+Pl+Gen+Fac:%>ей # ;

LEXICON мо_[жо_c]_я_ья́/ёв
+N+Msc:я FSgStem_anim ;
+N+Msc:%^Fей аPlEnd_anim ;

LEXICON мо_[мс_a]
+N+Msc+Sg+Nom: # ;
+N+Msc+Anim+Sg+Acc:%>ого # ;
+N+Msc+Sg+Gen:%>ого # ;
+N+Msc+Sg+Loc:%>ом # ;
+N+Msc+Sg+Dat:%>ому # ;
+N+Msc+Sg+Ins:%>ым # ;
+N+Pl+Nom:%>ы # ;
+N+Anim+Pl+Acc:%>ых # ;
+N+Pl+Gen:%>ых # ;
+N+Pl+Loc:%>ых # ;
+N+Pl+Dat:%>ым # ;
+N+Pl+Ins:%>ыми # ;

LEXICON мо_[мс-п_b]
+N+Msc+Sg+Nom: # ;
+N+Msc+Anim+Sg+Acc:%>ого́ # ;
+N+Msc+Sg+Gen:%>ого́ # ;
+N+Msc+Sg+Loc:%>о́м # ;
+N+Msc+Sg+Dat:%>ому́ # ;
+N+Msc+Sg+Ins:%>ы́м # ;
+N+Pl+Nom:%>ы́ # ;
+N+Anim+Pl+Acc:%>ы́х # ;
+N+Pl+Gen:%>ы́х # ;
+N+Pl+Loc:%>ы́х # ;
+N+Pl+Dat:%>ы́м # ;
+N+Pl+Ins:%>ы́ми # ;

LEXICON мо-жо_a
мо_[жо_a] ;
жо_a ;

LEXICON мо-жо_a2
мо_[жо_a2] ;
жо_a2 ;

LEXICON мо-жо_a[2]
мо_[жо_a[2]] ;
жо_a[2] ;

LEXICON соб-мо_[жо_a]
+N+Prop+Sem/Ant+Msc: FSgStem_anim ;
+N+Prop+Sem/Ant+Msc: ыPlStem_anim ;

LEXICON соб-мо-жо_a
+N+Prop+Sem/Ant+Msc: FSgStem_anim ;
+N+Prop+Sem/Ant+Msc: ыPlStem_anim ;
+N+Prop+Sem/Ant+Fem: FSgStem ;
+N+Prop+Sem/Ant+Fem: ыPlStem_anim ;

LEXICON мо_[жо_b] ! This also includes жо_b' because of the fleeting vowel rule in twolc
+N+Msc: FSgEnd_anim ;
+N+Msc: ыPlEnd_anim ;

LEXICON мо_[жо_b]_Р._мн._затрудн. ! This also includes жо_b' because of the fleeting vowel rule in twolc
+N+Msc: FSgEnd_anim ;
+N+Msc: ыPlEnd_anim_gPrb ;

LEXICON мо-жо_b
мо_[жо_b] ;
жо_b ;

LEXICON мо_[жо_b-] ! This also includes жо_b' because of the fleeting vowel rule in twolc
+N+Msc: FSgEnd_anim ;
+N+Msc: ыPlEnd_anim_Prb ;

LEXICON мо-жо_b-
мо_[жо_b-] ;
жо_b- ;

LEXICON мо_[жо_d]
+N+Msc: FSgEnd_anim ;
+N+Msc: ыPlStem_anim ;

LEXICON мо-жо_d
мо_[жо_d] ;
жо_d ;

LEXICON мо_[жо_f_ова]
+N+Msc:ова́ FSgEnd_anim ;
+N+Msc:ова́ ыPlNomStem_anim ;
+N+Msc+Anim+Pl+Acc:о́в # ;
+N+Msc+Anim+Pl+Gen:о́в # ;
+N+Msc:ова́ PlPeriphEnd_anim ;

LEXICON мо_[п_a]
+N+Msc+Anim+Sg+Nom:%>ый # ;
+N+Msc+Anim+Sg+Acc:%>ого # ;
MSg_SubstAdj_Stem_anim ;
+N+Msc+Anim+Pl+Nom:%>ые # ;
+N+Msc+Anim+Pl+Acc:%>ых # ;
+N+Msc: Pl_SubstAdj_Stem_anim ;

LEXICON мо_[п_a-]
+N+Msc+Anim+Sg+Nom:%>ый # ;
+N+Msc+Anim+Sg+Acc:%>ого # ;
MSg_SubstAdj_Stem_anim ;
+N+Msc+Anim+Pl+Nom+Prb:%>ые # ;
+N+Msc+Anim+Pl+Acc+Prb:%>ых # ;
+N+Msc: Pl_SubstAdj_Stem_anim_Prb ;

LEXICON мо_[п_b]
+N+Msc+Anim+Sg+Nom:%>о́й # ;
+N+Msc+Anim+Sg+Acc:%>о́го # ;
MSg_SubstAdj_End_anim ;
+N+Msc+Anim+Pl+Nom:%>ы́е # ;
+N+Msc+Anim+Pl+Acc:%>ы́х # ;
+N+Msc: Pl_SubstAdj_End_anim ;

LEXICON MSgStem !Inanimate
MSgNomZ ;
MSgAccZ ;
MSgObliqStem ;

LEXICON MSgStem_anim
MSgNomZ_anim ;
+Anim+Sg+Acc:%>а # ;
MSgObliqStem_anim ;

LEXICON MSgEnd !Inanimate
MSgNomZ ;
MSgAccZ ;
MSgObliqEnd ;

LEXICON MSgEnd_anim
MSgNomZ_anim ;
+Anim+Sg+Acc:%>а́ # ;
MSgObliqEnd_anim ;

LEXICON MSgNomZ
+Inan+Sg+Nom: # ;

LEXICON MSgNomZ_anim
+Anim+Sg+Nom: # ;

LEXICON MSgAccZ
+Inan+Sg+Acc: # ;

LEXICON MSgAccZ_anim
+Anim+Sg+Acc: # ;

LEXICON MscGen2_inan
+N+Msc+Inan+Sg+Gen2:у # ;

LEXICON MscLoc2_inan
+N+Msc+Inan+Sg+Loc2:у́ # ;

LEXICON MscLoc2_inan_Fac
+N+Msc+Inan+Sg+Loc2+Fac:у́ # ;

LEXICON MSgObliqStem
+Inan+Sg+Gen:%>а # ;
+Inan+Sg+Loc:%>е # ;
+Inan+Sg+Dat:%>у # ;
+Inan+Sg+Ins:%>ом # ;

LEXICON MSgObliqStem_anim
+Anim+Sg+Gen:%>а # ;
+Anim+Sg+Loc:%>е # ;
+Anim+Sg+Dat:%>у # ;
+Anim+Sg+Ins:%>ом # ;

LEXICON MSgObliqEnd
+Inan+Sg+Gen:%>а́ # ;
+Inan+Sg+Loc:%>е́ # ;
+Inan+Sg+Dat:%>у́ # ;
+Inan+Sg+Ins:%>о́м # ;

LEXICON MSgObliqEnd_anim
+Anim+Sg+Gen:%>а́ # ;
+Anim+Sg+Loc:%>е́ # ;
+Anim+Sg+Dat:%>у́ # ;
+Anim+Sg+Ins:%>о́м # ;

LEXICON MSg_SubstAdj_Stem
+N+Msc+Inan+Sg+Gen:%>ого # ;
+N+Msc+Inan+Sg+Loc:%>ом # ;
+N+Msc+Inan+Sg+Dat:%>ому # ;
+N+Msc+Inan+Sg+Ins:%>ым # ;

LEXICON MSg_SubstAdj_Stem_anim
+N+Msc+Anim+Sg+Gen:%>ого # ;
+N+Msc+Anim+Sg+Loc:%>ом # ;
+N+Msc+Anim+Sg+Dat:%>ому # ;
+N+Msc+Anim+Sg+Ins:%>ым # ;

LEXICON MSg_SubstAdj_End
+N+Msc+Inan+Sg+Gen:%>о́го # ;
+N+Msc+Inan+Sg+Loc:%>о́м # ;
+N+Msc+Inan+Sg+Dat:%>о́му # ;
+N+Msc+Inan+Sg+Ins:%>ы́м # ;

LEXICON MSg_SubstAdj_End_anim
+N+Msc+Anim+Sg+Gen:%>о́го # ;
+N+Msc+Anim+Sg+Loc:%>о́м # ;
+N+Msc+Anim+Sg+Dat:%>о́му # ;
+N+Msc+Anim+Sg+Ins:%>ы́м # ;

LEXICON Pl_SubstAdj_Stem
+Inan+Pl+Gen:%>ых # ;
+Inan+Pl+Loc:%>ых # ;
+Inan+Pl+Dat:%>ым # ;
+Inan+Pl+Ins:%>ыми # ;

LEXICON Pl_SubstAdj_Stem_anim
+Anim+Pl+Gen:%>ых # ;
+Anim+Pl+Loc:%>ых # ;
+Anim+Pl+Dat:%>ым # ;
+Anim+Pl+Ins:%>ыми # ;

LEXICON Pl_SubstAdj_Stem_anim_Prb
+Anim+Pl+Gen+Prb:%>ых # ;
+Anim+Pl+Loc+Prb:%>ых # ;
+Anim+Pl+Dat+Prb:%>ым # ;
+Anim+Pl+Ins+Prb:%>ыми # ;

LEXICON Pl_SubstAdj_End
+Inan+Pl+Gen:%>ы́х # ;
+Inan+Pl+Loc:%>ы́х # ;
+Inan+Pl+Dat:%>ы́м # ;
+Inan+Pl+Ins:%>ы́ми # ;

LEXICON Pl_SubstAdj_End_anim
+Anim+Pl+Gen:%>ы́х # ;
+Anim+Pl+Loc:%>ы́х # ;
+Anim+Pl+Dat:%>ы́м # ;
+Anim+Pl+Ins:%>ы́ми # ;

!!!!!!!!!!!!!!!!!!!!!!!!!NEUTER PARADIGMS!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON с_0
+N+Neu: Indecl_Sg_inan ;
+N+Neu: Indecl_Pl_inan ;

LEXICON Time_halfhour ! e.g. полпервого
+N+Neu: Indecl_Sg_inan ;

LEXICON с_a
+N+Neu: NSgStem ;
+N+Neu: аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a_Р._мн._затрудн.
+N+Neu: NSgStem ;
+N+Neu: аPlStem_gPrb ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a_JPl
+N+Neu: NSgStem ;
+N+Neu:%^Fей аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a_о_и
+N+Neu:о NSgStem ;
+N+Neu:ь ыPlStem ;

LEXICON с_a_дерево
+N+Neu:де́рево NSgStem ;
+N+Neu:дере́в%^Fей аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a_озеро
+N+Neu:о́зеро NSgStem ;
+N+Neu:озёро аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a_утро
с_a ;
+N+Neu+Sg+Gen:%>а́ # ;
+N+Neu+Sg+Dat:%>у́ # ;
+N+Neu+Inan+Pl+Dat:%>а́м # ;
+N+Neu+Inan+Pl+Ins:%>а́ми # ;

LEXICON мн._[с_a]
+N+Neu: аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a1
+N+Neu: NSgStem ;
+N+Neu: ыPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a12
+N+Neu: NSgStem ;
+N+Neu: ыPlStem_ов ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a2
+N+Neu: NSgStem ;
+N+Neu: аPlStem_ов ;     !Cyrillic 'а' in 'аPl'

LEXICON с_a[2]
+N+Neu: NSgStem ;
+N+Neu: аPlStem ;     !Cyrillic 'а' in 'аPl'
+N+Neu+Inan+Pl+Gen+Fac:%>ов%^G # ;

LEXICON мн._[с_a2]
+N+Neu: аPlStem_ов ;     !Cyrillic 'а' in 'аPl'

LEXICON мн._[с_a[2]]
+N+Neu: аPlStem ;     !Cyrillic 'а' in 'аPl'
+N+Neu+Inan+Pl+Gen+Fac:%>ов%^G # ;

LEXICON с_a-
+N+Neu: NSgStem ;
+N+Neu: аPlStem_Prb ;     !Cyrillic 'а' in 'аPl'

LEXICON с_b
+N+Neu: NSgEnd ;
+N+Neu: аPlEnd ;      !Cyrillic 'а' in 'аPl'

LEXICON с_b_OCS_ие
+N+Neu: NSgEnd_OCS_ие ;
+N+Neu: аPlEnd_OCS_ие ;      !Cyrillic 'а' in 'аPl'

LEXICON с_b12
+N+Neu: NSgEnd ;
+N+Neu: ыPlEnd_ов ;      !Cyrillic 'а' in 'аPl'

LEXICON с_b12-
+N+Neu: NSgEnd ;
+N+Neu: ыPlEnd_ов_Prb ;      !Cyrillic 'а' in 'аPl'

LEXICON с_b2
+N+Neu: NSgEnd ;
+N+Neu: аPlEnd_ов ;      !Cyrillic 'а' in 'аPl'

LEXICON мн._[с_b]
+N+Neu: аPlEnd ;      !Cyrillic 'а' in 'аPl'

LEXICON с_b-
+N+Neu: NSgEnd ;
+N+Neu: аPlEnd_Prb ;      !Cyrillic 'а' in 'аPl'

LEXICON с_b-_OCS_ие
+N+Neu: NSgEnd_OCS_ие ;
+N+Neu: аPlEnd_OCS_ие_Prb ;      !Cyrillic 'а' in 'аPl'

LEXICON NSgEnd_OCS_ие
+Inan+Sg+Nom:ие́ # ;
+Inan+Sg+Acc:ие́ # ;
+Inan+Sg+Gen:ия́ # ;
+Inan+Sg+Loc:ии́ # ;
+Inan+Sg+Dat:ию́ # ;
+Inan+Sg+Ins:ие́м # ;

LEXICON аPlEnd_OCS_ие
+Inan+Pl+Nom:ия́ # ;
+Inan+Pl+Acc:ия́ # ;
+Inan+Pl+Gen:и́й # ;
+Inan+Pl+Loc:ия́х # ;
+Inan+Pl+Dat:ия́м # ;
+Inan+Pl+Ins:ия́ми # ;

LEXICON аPlEnd_OCS_ие_Prb
+Inan+Pl+Nom+Prb:ия́ # ;
+Inan+Pl+Acc+Prb:ия́ # ;
+Inan+Pl+Gen+Prb:и́й # ;
+Inan+Pl+Loc+Prb:ия́х # ;
+Inan+Pl+Dat+Prb:ия́м # ;
+Inan+Pl+Ins+Prb:ия́ми # ;

LEXICON с_c
+N+Neu: NSgStem ;
+N+Neu: аPlEnd ;      !Cyrillic 'а' in 'аPl'

LEXICON с_c_есаPl
+N+Neu:о NSgStem ;
+N+Neu:е́са́ аPlEnd ;      !Cyrillic 'а' in 'аPl'

LEXICON с_c2
+N+Neu: NSgStem ;
+N+Neu: аPlEnd_ов ;      !Cyrillic 'а' in 'аPl'

LEXICON с_c[2]
+N+Neu: NSgStem ;
+N+Neu: аPlEnd ;      !Cyrillic 'а' in 'аPl'
+N+Neu+Inan+Pl+Gen+Fac:%>о́в%^G # ;

LEXICON с_c-
+N+Neu: NSgStem ;
+N+Neu: аPlEnd ;      !Cyrillic 'а' in 'аPl'

LEXICON с_d
+N+Neu: NSgEnd ;
+N+Neu: аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_d_JPl
+N+Neu:о́ NSgEnd ;
+N+Neu:%^Fей аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_d_дно
+N+Neu:д%^Fо́но́ NSgEnd ;
+N+Neu:до́н%^Fей аPlStem ;     !Cyrillic 'а' in 'аPl'

LEXICON с_d1-
+N+Neu: NSgEnd ;
+N+Neu: ыPlStem_Prb ;     !Cyrillic 'а' in 'аPl'

LEXICON с_d2
+N+Neu: NSgEnd ;
+N+Neu: аPlStem_ов ;     !Cyrillic 'а' in 'аPl'

LEXICON с_d-
+N+Neu: NSgEnd ;
+N+Neu: аPlStem_Prb ;     !Cyrillic 'а' in 'аPl'

LEXICON с_e_око
+N+Neu:о́ко NSgEnd ;
+N+Neu:о́ч ыPlAnom ;

LEXICON с_e_ухо
+N+Neu:у́хо NSgEnd ;
+N+Neu:у́ш ыPlAnom ;     !Cyrillic 'а' in 'аPl'

LEXICON с_f
+N+Neu: NSgEnd ;
+N+Neu: аPlAnom ;     !Cyrillic 'а' in 'аPl'

LEXICON с_f1_плечо
+N+Neu: NSgEnd ;
+N+Neu: ыPlNomStem ;
+N+Neu: ыPlAccStem ;
+N+Neu+Inan+Pl+Gen:%> # ;
+N+Neu: PlPeriphEnd ;

LEXICON с_[п_b]
+N+Neu+Sg+Nom:%>о́е # ;
+N+Neu+Sg+Acc:%>о́е # ;
MSg_SubstAdj_End ;
+N+Neu+Inan+Pl+Nom:%>ы́е # ;
+N+Neu+Inan+Pl+Acc:%>ы́е # ;
+N+Neu: Pl_SubstAdj_End ;

LEXICON с_[мс_a]
+N: мс-п_a_Neu ;
+N: мс-п_a_Pl_inan ;

LEXICON с_[мс_b]
+N: мс-п_b_Neu ;
+N: мс-п_b_Pl_inan ;

LEXICON со_0
+N+Neu: Indecl_Sg_anim ;
+N+Neu: Indecl_Pl_anim ;

LEXICON со_a
+N+Neu: NSgStem ;
+N+Neu: аPlStem_anim ;     !Cyrillic 'а' in 'аPl'

LEXICON со_a1
+N+Neu: NSgStem ;
+N+Neu: ыPlStem_anim ;

LEXICON со_дитя
+N+Neu+Sg+Nom:дитя́ # ;
+N+Neu+Sg+Acc:дитя́ # ;
+N+Neu+Sg+Gen:дитя́ти # ;
+N+Neu+Sg+Loc:дитя́ти # ;
+N+Neu+Sg+Dat:дитя́ти # ;
+N+Neu+Sg+Ins:дитя́тей # ;
+N+Neu:де́ть ыPlNomStem_anim ;
+N+Neu+Anim+Pl+Acc:дете́й # ;
+N+Neu+Anim+Pl+Gen:дете́й # ;
+N+Neu+Anim+Pl+Loc:де́тях # ;
+N+Neu+Anim+Pl+Dat:де́тям # ;
+N+Neu+Anim+Pl+Ins:детьми́ # ;

LEXICON мо_[со_a1]
+N+Msc: NSgStem_anim ;
+N+Msc: ыPlStem_anim ;

LEXICON мо_[со_a1]_4
мо_[со_a1] ;
+N+Msc+Anim+Sg+Acc+Fac:%>у # ;

LEXICON мо_[со_a[1]]
+N+Msc: NSgStem_anim ;
+N+Msc: аPlStem_anim ;
+N+Msc: ыPlStem_anim_Fac ;

LEXICON мо_[со_a[1]]_4
мо_[со_a[1]] ;
+N+Msc+Anim+Sg+Acc+Fac:%>у # ;

LEXICON со_a2
+N+Neu: NSgStem ;
+N+Neu: аPlStem_anim_ов ;     !Cyrillic 'а' in 'аPl'

LEXICON мо_[со_a2]
+N+Msc: NSgStem_anim ;
+N+Msc: аPlStem_anim_ов ;     !Cyrillic 'а' in 'аPl'

LEXICON мн._[со_a]
+N+Neu: аPlStem_anim ;     !Cyrillic 'а' in 'аPl'

LEXICON со_b
+N+Neu: NSgEnd ;
+N+Neu: аPlEnd_anim ;      !Cyrillic 'а' in 'аPl'

LEXICON мо_[со_b12]
+N+Msc: NSgEnd_anim ;
+N+Msc: ыPlEnd_anim_ов ;

LEXICON со_b12-
+N+Neu: NSgEnd ;
+N+Neu: ыPlEnd_anim_ов_Prb ;

LEXICON мо_[со_b12-]
+N+Msc: NSgEnd_anim ;
+N+Msc: ыPlEnd_anim_ов_Prb ;

LEXICON со_d
+N+Neu: NSgEnd ;
+N+Neu: аPlStem_anim ;     !Cyrillic 'а' in 'аPl'

LEXICON со_d-
+N+Neu: NSgEnd ;
+N+Neu: аPlStem_anim_Prb ;     !Cyrillic 'а' in 'аPl'

LEXICON с_[п_a]
+N+Neu+Sg+Nom:%>ое # ;
+N+Neu+Sg+Acc:%>ое # ;
MSg_SubstAdj_Stem ;
+N+Neu+Inan+Pl+Nom:%>ые # ;
+N+Neu+Inan+Pl+Acc:%>ые # ;
+N+Neu: Pl_SubstAdj_Stem ;

LEXICON со_[п_a]
+N+Neu+Sg+Nom:%>ое # ;
+N+Neu+Sg+Acc:%>ое # ;
MSg_SubstAdj_Stem ;
+N+Neu+Anim+Pl+Nom:%>ые # ;
+N+Neu+Anim+Pl+Acc:%>ых # ;
+N+Neu: Pl_SubstAdj_Stem ;

LEXICON с_8a_ё_-ёна,_-ён,_-ёнам
+N+Neu: мяSg ;
+N+Neu:ёно аPlStem ;

LEXICON с_8c_ё
+N+Neu: мяSg ;
+N+Neu: мяPl_ё ;

LEXICON с_8c-_ё
+N+Neu: мяSg ;
+N+Neu: мяPl_ё_Prb ;

LEXICON с_8c_я́
+N+Neu: мяSg ;
+N+Neu: мяPl_я́ ;

LEXICON мяSg
+Inan+Sg+Nom:%>я # ;
+Inan+Sg+Acc:%>я # ;
+Inan+Sg+Gen:%>ени # ;
+Inan+Sg+Loc:%>ени # ;
+Inan+Sg+Dat:%>ени # ;
+Inan+Sg+Ins:%>енем # ;

LEXICON мяPl_ё
:%>ен аPlNomEnd ;
+Inan+Pl+Acc:%>ена́ # ;
+Inan+Pl+Gen:%>ён # ;
:%>ен PlPeriphEnd ;

LEXICON мяPl_ё_Prb
:%>ен аPlNomEnd_Prb ;
+Inan+Pl+Acc+Prb:%>ена́ # ;
+Inan+Pl+Gen+Prb:%>ён # ;
:%>ен PlPeriphEnd_Prb ;

LEXICON мяPl_я́
:%>ен аPlNomEnd ;
+Inan+Pl+Acc:%>ена́ # ;
+Inan+Pl+Gen:%>я́н # ;
:%>ен PlPeriphEnd ;

LEXICON NSgStem
+Inan+Sg+Nom:%>о # ;
+Inan+Sg+Acc:%>о # ;
MSgObliqStem ;

LEXICON NSgStem_anim
+Anim+Sg+Nom:%>о # ;
+Anim+Sg+Acc:%>а # ;
MSgObliqStem ;

LEXICON NSgEnd
+Inan+Sg+Nom:%>о́ # ;
+Inan+Sg+Acc:%>о́ # ;
MSgObliqEnd ;

LEXICON NSgEnd_anim
+Anim+Sg+Nom:%>о́ # ;
+Anim+Sg+Acc:%>а́ # ;
MSgObliqEnd ;

!!!!!!!!!!!!!!!!!!!!!!!!!FEMININE PARADIGMS!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON ж_0
+N+Fem: Indecl_Sg_inan ;
+N+Fem: Indecl_Pl_inan ;

LEXICON ж_a
+N+Fem: FSgStem ;
+N+Fem: ыPlStem ;

LEXICON соб-ж_a
+N+Prop+Sem/Alt+Fem: FSgStem ;
+N+Prop+Sem/Alt+Fem: ыPlStem ;

LEXICON мн._[ж_a]
+N+Fem: ыPlStem ;

LEXICON мн._[ж_a]_хлопоты
+N+Fem:ота ыPlNomStem ;
+N+Fem:ота ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:о́т # ;
+N+Fem:ота PlPeriphStem ;

LEXICON ж_a2
+N+Fem: FSgStem ;
+N+Fem: ыPlStem_ей ;

LEXICON ж_a[2]
+N+Fem: FSgStem ;
+N+Fem: ыPlStem ;
+N+Fem+Inan+Pl+Gen+Fac:ей # ;

LEXICON ж_a-
+N+Fem: FSgStem ;
+N+Fem: ыPlStem_Prb ;

LEXICON ж_8a
+N+Fem: FьSgStem ;
+N+Fem: ыPlStem ;

LEXICON ж_8a_полночь
+N+Fem+Sg+Nom:по́лночь # ;
+N+Fem+Sg+Acc:по́лночь # ;
+N+Fem+Sg+Gen:полу́ночи # ;
+N+Fem+Sg+Loc:полу́ночи # ;
+N+Fem+Sg+Dat:полу́ночи # ;
+N+Fem+Sg+Ins:полу́ночью # ;
+N+Fem:полу́ночь ыPlStem ;

LEXICON мн._[ж_8a]
+N+Fem: ыPlStem ;

LEXICON ж_8a_П2
+N+Fem: FьSgStem ;
+N+Fem: ыPlStem ;
FemLoc2_inan ;

LEXICON ж_8a_[П2]
+N+Fem: FьSgStem ;
+N+Fem: ыPlStem ;
FemLoc2_inan_Fac ;

LEXICON ж_8a-
+N+Fem: FьSgStem ;
+N+Fem: ыPlStem_Prb ;

LEXICON ж_8a-_П2
+N+Fem: FьSgStem ;
+N+Fem: ыPlStem_Prb ;
FemLoc2_inan ;

LEXICON FemLoc2_inan
+N+Fem+Sg+Loc2:и́ # ;

LEXICON FemLoc2_inan_Fac
+N+Fem+Sg+Loc2+Fac:и́ # ;

LEXICON ж_b ! This also includes ж_b' because of the fleeting vowel rule in twolc
+N+Fem: FSgEnd ;
+N+Fem: ыPlEnd ;

LEXICON мн._[ж_b]
+N+Fem: ыPlEnd ;

LEXICON ж_b_Р._мн._затрудн.
+N+Fem: FSgEnd ;
+N+Fem: ыPlEnd_gPrb ;

LEXICON ж_b- ! This also includes ж_b'- because of the fleeting vowel rule in twolc
+N+Fem: FSgEnd ;
+N+Fem: ыPlEnd_Prb ;

LEXICON соб-ж_b?
+N+Prop+Sem/Alt+Fem: FSgEnd ;
+N+Prop+Sem/Alt+Fem: ыPlEnd_gPrb ;

LEXICON ж_8b ! This also includes ж_b' because of the fleeting vowel rule in twolc
+N+Fem: FьSgEnd ;
+N+Fem: ыPlEnd ;

LEXICON ж_d
+N+Fem: FSgEnd ;
+N+Fem: ыPlStem ;

LEXICON ж_d_заря
+N+Fem:аря́ FSgEnd ;
+N+Fem:о́ря ыPlStem ;

LEXICON ж_d-
+N+Fem: FSgEnd ;
+N+Fem: ыPlStem_Prb ;

LEXICON ж_d' ! Shifting retraction
+N+Fem: FSgRetr ;
+N+Fem: ыPlStem ;

LEXICON ж_e
+N+Fem: FSgStem ;
+N+Fem: ыPlAnom ;

LEXICON мн._[ж_e]
+N+Fem: ыPlAnom ;

LEXICON мн._[ж_e]_она
+N+Fem:она ыPlNomStem ;
+N+Fem:она ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:о́н # ;
+N+Fem:она PlPeriphEnd ;

LEXICON мн._[ж_e]_оса
+N+Fem:оса ыPlNomStem ;
+N+Fem:оса ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:о́с # ;
+N+Fem:оса PlPeriphEnd ;

LEXICON ж_8e
+N+Fem: FьSgStem ;
+N+Fem: ыPlAnom ;

LEXICON мн._[ж_8e]
+N+Fem: ыPlAnom ;

LEXICON ж_8e_церковь
:ь ж_8e ;
+N+Fem: PlPeriphEnd ;

LEXICON ж_8e_П2
+N+Fem: FьSgStem ;
+N+Fem: ыPlAnom ;
FemLoc2_inan ;

LEXICON ж_8e_П2_дверь
ж_8e_П2 ;
+N+Fem+Inan+Pl+Ins:%>ьми́ # ;

LEXICON ж_8e_[П2]
+N+Fem: FьSgStem ;
+N+Fem: ыPlAnom ;
FemLoc2_inan_Fac ;

LEXICON ж_8e-
+N+Fem: FьSgStem ;
+N+Fem: ыPlAnom_Prb ;

LEXICON ж_8f ! This includes 8f'' (twolc handles InstSg stress)
+N+Fem: FьSgEnd ;
+N+Fem: ыPlAnom ;

LEXICON ж_8f_П2 ! This includes 8f'' (twolc handles InstSg stress)
+N+Fem: FьSgEnd ;
+N+Fem: ыPlAnom ;
FemLoc2_inan ;

LEXICON ж_f
+N+Fem: FSgEnd ;
+N+Fem: ыPlAnom ;

LEXICON ж_f_озда
+N+Fem:озда́ FSgEnd ;
+N+Fem:озда́ ыPlNomStem ;
+N+Fem:озда́ ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:о́зд # ;
+N+Fem:озда́ PlPeriphEnd ;

LEXICON ж_f_ода
+N+Fem:ода́ FSgEnd ;
+N+Fem:ода́ ыPlNomStem ;
+N+Fem:ода́ ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:о́д # ;
+N+Fem:ода́ PlPeriphEnd ;

LEXICON ж_f_ёза
+N+Fem:еза́ FSgEnd ;
+N+Fem:еза́ ыPlNomStem ;
+N+Fem:еза́ ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:ёз # ;
+N+Fem:еза́ PlPeriphEnd ;

LEXICON ж_f_она
+N+Fem:она́ FSgEnd ;
+N+Fem:она́ ыPlNomStem ;
+N+Fem:она́ ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:о́н # ;
+N+Fem:она́ PlPeriphEnd ;

LEXICON ж_f_ира
+N+Fem:ира́ FSgEnd ;
+N+Fem:ира́ ыPlNomStem ;
+N+Fem:ира́ ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:и́р # ;
+N+Fem:ира́ PlPeriphEnd ;

LEXICON ж_f_ора
+N+Fem:ора́ FSgEnd ;
+N+Fem:ора́ ыPlNomStem ;
+N+Fem:ора́ ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:о́р # ;
+N+Fem:ора́ PlPeriphEnd ;

LEXICON ж_f_оса
+N+Fem:оса́ FSgEnd ;
+N+Fem:оса́ ыPlNomStem ;
+N+Fem:оса́ ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:о́с # ;
+N+Fem:оса́ PlPeriphEnd ;

LEXICON ж_f_простыня
+N+Fem:ыня́ FSgEnd ;
+N+Fem:ыня́ ыPlNomStem ;
+N+Fem:ыня́ ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:ыне́й # ;
+N+Fem+Inan+Pl+Gen:ы́нь # ;
+N+Fem:ыня́ PlPeriphEnd ;

LEXICON ж_f' ! Anomolous retraction
+N+Fem: FSgRetr ;
+N+Fem: ыPlAnom ;

LEXICON ж_f'_ова
+N+Fem:ова́ FSgRetr ;
+N+Fem:ова́ ыPlNomStem ;
+N+Fem:ова́ ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:о́в # ;
+N+Fem:ова́ PlPeriphEnd ;

LEXICON ж_f'_ёда
+N+Fem:еда́ FSgRetr ;
+N+Fem:еда́ ыPlNomStem ;
+N+Fem:еда́ ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:ёд # ;
+N+Fem:еда́ PlPeriphEnd ;

LEXICON ж_f'_озда
+N+Fem:озда́ FSgRetr ;
+N+Fem:озда́ ыPlNomStem ;
+N+Fem:озда́ ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:о́зд # ;
+N+Fem:озда́ PlPeriphEnd ;

LEXICON ж_f'_ода
+N+Fem:ода́ FSgRetr ;
+N+Fem:ода́ ыPlNomStem ;
+N+Fem:ода́ ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:о́д # ;
+N+Fem:ода́ PlPeriphEnd ;

LEXICON ж_f'_она
+N+Fem:она́ FSgRetr ;
+N+Fem:она́ ыPlNomStem ;
+N+Fem:она́ ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:о́н # ;
+N+Fem:она́ PlPeriphEnd ;

LEXICON ж_f'_оса
+N+Fem:оса́ FSgRetr ;
+N+Fem:оса́ ыPlNomStem ;
+N+Fem:оса́ ыPlAccStem ;
+N+Fem+Inan+Pl+Gen:о́с # ;
+N+Fem:оса́ PlPeriphEnd ;

LEXICON жо_0
+N+Fem: Indecl_Sg_anim ;
+N+Fem: Indecl_Pl_anim ;

LEXICON соб-жо_0
+N+Prop+Fem: Indecl_Sg_anim ;
+N+Prop+Fem: Indecl_Pl_anim ;

LEXICON жо_a
+N+Fem: FSgStem_anim ;
+N+Fem: ыPlStem_anim ;

LEXICON жо_a_ица_ицы/ы
+N+Fem:ица FSgStem_anim ;
+N+Fem:ица ыPlStem_anim ;
+N+Fem:а ыPlStem_anim ;

LEXICON соб-жо_a
+N+Prop+Fem: FSgStem_anim ;
+N+Prop+Fem: ыPlStem_anim ;

LEXICON жо_a2
+N+Fem: FSgStem_anim ;
+N+Fem: ыPlStem_anim_ей ;

LEXICON жо_a[2]
+N+Fem: FSgStem_anim ;
+N+Fem: ыPlStem_anim ;
+N+Fem+Anim+Pl+Gen+Fac:%>ей # ;

LEXICON жо_8a
+N+Fem: FьSgStem_anim ;
+N+Fem: ыPlStem_anim ;

LEXICON жо_b ! This also includes ж_b' because of the fleeting vowel rule in twolc
+N+Fem: FSgEnd_anim ;
+N+Fem: ыPlEnd_anim ;

LEXICON жо_b_Р._мн._затрудн. ! This also includes ж_b' because of the fleeting vowel rule in twolc
+N+Fem: FSgEnd_anim ;
+N+Fem: ыPlEnd_anim_gPrb ;

LEXICON жо_b- ! This also includes ж_b' because of the fleeting vowel rule in twolc
+N+Fem: FSgEnd_anim ;
+N+Fem: ыPlEnd_anim_Prb ;

LEXICON жо_8b ! This also includes ж_b' because of the fleeting vowel rule in twolc
+N+Fem: FьSgEnd_anim ;
+N+Fem: ыPlEnd_anim ;

LEXICON жо_d
+N+Fem: FSgEnd_anim ;
+N+Fem: ыPlStem_anim ;

LEXICON жо_8e
+N+Fem: FьSgStem_anim ;
+N+Fem: ыPlAnom_anim ;

LEXICON жо_8e_дочь
+N+Fem+Anim+Sg+Nom:до́чь # ;
+N+Fem+Anim+Sg+Acc:до́чь # ;
+N+Fem+Anim+Sg+Gen:до́чери # ;
+N+Fem+Anim+Sg+Loc:до́чери # ;
+N+Fem+Anim+Sg+Dat:до́чери # ;
+N+Fem+Anim+Sg+Ins:до́черью # ;
+N+Fem:до́черь ыPlAnom_anim ;
+N+Fem+Anim+Pl+Ins:дочерьми́ # ;

LEXICON жо_8e_лошадь
жо_8e ;
+N+Fem+Anim+Pl+Ins:ми́ # ;

LEXICON жо_8e_мать
+N+Fem+Anim+Sg+Nom:ма́ть # ;
+N+Fem+Anim+Sg+Acc:ма́ть # ;
+N+Fem+Anim+Sg+Gen:ма́тери # ;
+N+Fem+Anim+Sg+Loc:ма́тери # ;
+N+Fem+Anim+Sg+Dat:ма́тери # ;
+N+Fem+Anim+Sg+Ins:ма́терью # ;
+N+Fem:ма́терь ыPlAnom_anim ;

LEXICON жо_f
+N+Fem: FSgEnd_anim ;
+N+Fem: ыPlAnom_anim ;

LEXICON ж_[п_a]
+N+Fem+Inan+Sg+Nom:%>ая # ;
+N+Fem+Inan+Sg+Acc:%>ую # ;
FSg_SubstAdj_Stem ;
+N+Fem+Inan+Pl+Nom:%>ые # ;
+N+Fem+Inan+Pl+Acc:%>ые # ;
+N+Fem: Pl_SubstAdj_Stem ;

LEXICON ж_[п_b]
+N+Fem+Inan+Sg+Nom:%>а́я # ;
+N+Fem+Inan+Sg+Acc:%>у́ю # ;
FSg_SubstAdj_End ;
+N+Fem+Inan+Pl+Nom:%>ы́е # ;
+N+Fem+Inan+Pl+Acc:%>ы́е # ;
+N+Fem: Pl_SubstAdj_End ;

LEXICON ж_[мс_a]
+N: мс-п_a_Fem ;
+N: мс-п_a_Pl_inan ;

LEXICON ж_[мс_b]
+N: мс-п_b_Fem ;
+N: мс-п_b_Pl_inan ;

LEXICON FSg_SubstAdj_Stem
+N+Fem+Inan+Sg+Gen:%>ой # ;
+N+Fem+Inan+Sg+Loc:%>ой # ;
+N+Fem+Inan+Sg+Dat:%>ой # ;
+N+Fem+Inan+Sg+Ins:%>ой # ;

LEXICON FSg_SubstAdj_Stem_anim
+N+Fem+Anim+Sg+Gen:%>ой # ;
+N+Fem+Anim+Sg+Loc:%>ой # ;
+N+Fem+Anim+Sg+Dat:%>ой # ;
+N+Fem+Anim+Sg+Ins:%>ой # ;

LEXICON жо_[п_a]
+N+Fem+Anim+Sg+Nom:%>ая # ;
+N+Fem+Anim+Sg+Acc:%>ую # ;
FSg_SubstAdj_Stem_anim ;
+N+Fem+Anim+Pl+Nom:%>ые # ;
+N+Fem+Anim+Pl+Acc:%>ых # ;
+N+Fem: Pl_SubstAdj_Stem_anim ;

LEXICON жо_[п_b]
+N+Fem+Anim+Sg+Nom:%>а́я # ;
+N+Fem+Anim+Sg+Acc:%>у́ю # ;
FSg_SubstAdj_End_anim ;
+N+Fem+Anim+Pl+Nom:%>ы́е # ;
+N+Fem+Anim+Pl+Acc:%>ы́х # ;
+N+Fem: Pl_SubstAdj_End_anim ;

LEXICON ф._[мс_a]
+N+Prop+Sem/Sur: Surname_stem ;

LEXICON ф._0
+N+Prop+Sem/Sur: Surname_0 ;

LEXICON Surname_stem
+Msc+Anim+Sg+Nom: # ;
+Msc+Anim+Sg+Acc:%>а # ;
+Msc+Anim+Sg+Gen:%>а # ;
+Msc+Anim+Sg+Loc:%>е # ;
+Msc+Anim+Sg+Dat:%>у # ;
+Msc+Anim+Sg+Ins:%>ым # ;
+Fem+Anim+Sg+Nom:%>а # ;
+Fem+Anim+Sg+Acc:%>у # ;
+Fem+Anim+Sg+Gen:%>ой # ;
+Fem+Anim+Sg+Loc:%>ой # ;
+Fem+Anim+Sg+Dat:%>ой # ;
+Fem+Anim+Sg+Ins:%>ой # ;
+Fem+Anim+Sg+Ins+Fac:%>ою # ;
+Anim+Pl+Nom:%>ы # ;
+Anim+Pl+Acc:%>ых # ;
+Anim+Pl+Gen:%>ых # ;
+Anim+Pl+Loc:%>ых # ;
+Anim+Pl+Dat:%>ым # ;
+Anim+Pl+Ins:%>ыми # ;

LEXICON Surname_0
+Msc+Anim+Sg+Nom: # ;
+Msc+Anim+Sg+Acc: # ;
+Msc+Anim+Sg+Gen: # ;
+Msc+Anim+Sg+Loc: # ;
+Msc+Anim+Sg+Dat: # ;
+Msc+Anim+Sg+Ins: # ;
+Fem+Anim+Sg+Nom: # ;
+Fem+Anim+Sg+Acc: # ;
+Fem+Anim+Sg+Gen: # ;
+Fem+Anim+Sg+Loc: # ;
+Fem+Anim+Sg+Dat: # ;
+Fem+Anim+Sg+Ins: # ;
+Fem+Anim+Sg+Ins+Fac: # ;
+Anim+Pl+Nom: # ;
+Anim+Pl+Acc: # ;
+Anim+Pl+Gen: # ;
+Anim+Pl+Loc: # ;
+Anim+Pl+Dat: # ;
+Anim+Pl+Ins: # ;

LEXICON Patronymic_stem
вич+N+Prop+Sem/Pat+Msc:вич MSgStem_anim ;
вич+N+Prop+Sem/Pat+Msc:вич ыPlStem_anim ;
вна+N+Prop+Sem/Pat+Fem:вна FSgStem_anim ;
вна+N+Prop+Sem/Pat+Fem:вна ыPlStem_anim ;

LEXICON FSg_SubstAdj_End
+N+Fem+Inan+Sg+Gen:%>о́й # ;
+N+Fem+Inan+Sg+Loc:%>о́й # ;
+N+Fem+Inan+Sg+Dat:%>о́й # ;
+N+Fem+Inan+Sg+Ins:%>о́й # ;

LEXICON FSg_SubstAdj_End_anim
+N+Fem+Anim+Sg+Gen:%>о́й # ;
+N+Fem+Anim+Sg+Loc:%>о́й # ;
+N+Fem+Anim+Sg+Dat:%>о́й # ;
+N+Fem+Anim+Sg+Ins:%>о́й # ;

LEXICON FSgStem
+Inan+Sg+Acc:%>у # ;
FSgObliqStem ;

LEXICON FSgStem_anim
+Anim+Sg+Acc:%>у # ;
FSgObliqStem_anim ;

LEXICON FSgObliqStem_ext
+Sg+Nom:%>а # ;
+Sg+Gen:%>ы # ;
+Sg+Loc:%>е # ;
+Sg+Dat:%>е # ;
+Sg+Ins:%>ой # ;
+Sg+Ins+Fac:%>ою # ;

LEXICON FSgObliqStem
+Inan: FSgObliqStem_ext ;

LEXICON FSgObliqStem_anim
+Anim: FSgObliqStem_ext ;

LEXICON FSgObliqEnd_ext
+Sg+Nom:%>а́ # ;
+Sg+Gen:%>ы́ # ;
+Sg+Loc:%>е́ # ;
+Sg+Dat:%>е́ # ;
+Sg+Ins:%>о́й # ;
+Sg+Ins+Fac:%>о́ю # ;

LEXICON FSgObliqEnd
+Inan: FSgObliqEnd_ext ;

LEXICON FSgObliqEnd_anim ! Only used for masculine nouns!
+Anim: FSgObliqEnd_ext ;

LEXICON FSgEnd
+Inan+Sg+Acc:%>у́ # ;
FSgObliqEnd ;

LEXICON FSgEnd_anim ! Only used for masculine nouns!
+Anim+Sg+Acc:%>у́ # ;
FSgObliqEnd_anim ;

LEXICON FSgRetr
+Inan+Sg+Acc:%>у # ;
FSgObliqEnd ;

LEXICON FSgRetr_anim
+Anim+Sg+Acc:%>у # ;
FSgObliqEnd_anim ;

LEXICON FьSgStem
+Sg+Nom:%>ь # ;
+Sg+Acc:%>ь # ;
+Sg+Gen:%>и # ;
+Sg+Loc:%>и # ;
+Sg+Dat:%>и # ;
+Sg+Ins:%>ью # ;

LEXICON FьSgStem_anim
+Sg+Nom:%>ь # ;
+Sg+Acc:%>ь # ;
+Sg+Gen:%>и # ;
+Sg+Loc:%>и # ;
+Sg+Dat:%>и # ;
+Sg+Ins:%>ью # ;

LEXICON FьSgEnd
+Inan: FьSgEnd_MostCases ;
+Inan+Sg+Ins:%>ью # ;

LEXICON FьSgEnd_anim
+Anim: FьSgEnd_MostCases ;
+Anim+Sg+Ins:%>ью # ;

LEXICON FьSgEnd_MostCases
+Sg+Nom:%>ь # ;
+Sg+Acc:%>ь # ;
+Sg+Gen:%>и́ # ;
+Sg+Loc:%>и́ # ;
+Sg+Dat:%>и́ # ;

LEXICON Indecl_Sg_inan
+Inan+Sg+Nom: # ;
+Inan+Sg+Acc: # ;
+Inan+Sg+Gen: # ;
+Inan+Sg+Loc: # ;
+Inan+Sg+Dat: # ;
+Inan+Sg+Ins: # ;

LEXICON Indecl_Sg_anim
+Anim+Sg+Nom: # ;
+Anim+Sg+Acc: # ;
+Anim+Sg+Gen: # ;
+Anim+Sg+Loc: # ;
+Anim+Sg+Dat: # ;
+Anim+Sg+Ins: # ;

LEXICON Indecl_Pl_inan
+Inan+Pl+Nom: # ;
+Inan+Pl+Acc: # ;
+Inan+Pl+Gen: # ;
+Inan+Pl+Loc: # ;
+Inan+Pl+Dat: # ;
+Inan+Pl+Ins: # ;

LEXICON Indecl_Pl_anim
+Anim+Pl+Nom: # ;
+Anim+Pl+Acc: # ;
+Anim+Pl+Gen: # ;
+Anim+Pl+Loc: # ;
+Anim+Pl+Dat: # ;
+Anim+Pl+Ins: # ;

LEXICON ыPlStem      !Inanimate
ыPlNomStem ;
ыPlAccStem ;
PlObliqStem ;

LEXICON ыPlStem_Fac      !Inanimate
ыPlNomStem_Fac ;
ыPlAccStem_Fac ;
PlObliqStem_Fac ;

LEXICON ыPlStem_Prb      !Inanimate
ыPlNomStem_Prb ;
ыPlAccStem_Prb ;
PlObliqStem_Prb ;

LEXICON ыPlStem_ей      !Inanimate
ыPlNomStem ;
ыPlAccStem ;
+Inan+Pl+Gen:%>ей # ;
PlPeriphStem ;

LEXICON ыPlStem_ов      !Inanimate
ыPlNomStem ;
ыPlAccStem ;
+Inan+Pl+Gen:%>ов%^G # ;
PlPeriphStem ;

LEXICON ыPlStem_zero      !Inanimate
ыPlNomStem ;
ыPlAccStem ;
+Inan+Pl+Gen:%> # ;
PlPeriphStem ;

LEXICON ыPlEnd       !Inanimate
ыPlNomEnd ;
ыPlAccEnd ;
PlObliqEnd ;

LEXICON ыPlEnd_Prb       !Inanimate
ыPlNomEnd_Prb ;
ыPlAccEnd_Prb ;
PlObliqEnd_Prb ;

LEXICON ыPlEnd_gPrb
ыPlNomEnd ;
ыPlAccEnd ;
+Inan+Pl+Gen+Prb:%>о́в # ;
PlPeriphEnd ;

LEXICON ыPlEnd_ей      !Inanimate
ыPlNomEnd ;
ыPlAccEnd ;
+Inan+Pl+Gen:%>е́й # ;
PlPeriphEnd ;

LEXICON ыPlEnd_ов      !Inanimate
ыPlNomEnd ;
ыPlAccEnd ;
+Inan+Pl+Gen:%>о́в%^G # ;
PlPeriphEnd ;

LEXICON ыPlEnd_ов_Prb      !Inanimate
ыPlNomEnd_Prb ;
ыPlAccEnd_Prb ;
+Inan+Pl+Gen+Prb:%>о́в%^G # ;
PlPeriphEnd_Prb ;

LEXICON ыPlEnd_zero      !Inanimate
ыPlNomEnd ;
ыPlAccEnd ;
+Inan+Pl+Gen:%> # ;
PlPeriphEnd ;

LEXICON ыPlAnom      !Inanimate
ыPlNomStem ;
ыPlAccStem ;
PlObliqEnd ;

LEXICON ыPlAnom_Prb      !Inanimate
ыPlNomStem_Prb ;
ыPlAccStem_Prb ;
PlObliqEnd_Prb ;

LEXICON ыPlAnom_zero      !Inanimate
ыPlNomStem ;
ыPlAccStem ;
+Inan+Pl+Gen:%> # ;
PlPeriphEnd ;

LEXICON аPlStem      !Inanimate
аPlNomStem ;
аPlAccStem ;
PlObliqStem ;

LEXICON аPlStem_Prb      !Inanimate
аPlNomStem_Prb ;
аPlAccStem_Prb ;
PlObliqStem_Prb ;

LEXICON аPlStem_gPrb      !Inanimate
аPlNomStem ;
аPlAccStem ;
+Inan+Pl+Gen+Prb:%>ов # ;
PlPeriphStem ;

LEXICON аPlStem_ов      !Inanimate
аPlNomStem ;
аPlAccStem ;
+Inan+Pl+Gen:%>ов%^G # ;
PlPeriphStem ;

LEXICON аPlStem_zero      !Inanimate
аPlNomStem ;
аPlAccStem ;
+Inan+Pl+Gen:%> # ;
PlPeriphStem ;

LEXICON аPlEnd       !Inanimate
аPlNomEnd ;
аPlAccEnd ;
PlObliqEnd ;

LEXICON аPlEnd_Prb       !Inanimate
аPlNomEnd_Prb ;
аPlAccEnd_Prb ;
PlObliqEnd_Prb ;

LEXICON аPlEnd_ов      !Inanimate
аPlNomEnd ;
аPlAccEnd ;
+Inan+Pl+Gen:%>о́в%^G # ;
PlPeriphEnd ;

LEXICON аPlEnd_zero      !Inanimate
аPlNomEnd ;
аPlAccEnd ;
+Inan+Pl+Gen:%> # ;
PlPeriphEnd ;

LEXICON аPlAnom       !Inanimate
аPlNomStem ;
аPlAccStem ;
PlObliqEnd ;

LEXICON ыPlStem_anim
ыPlNomStem_anim ;
PlAccStem_anim ;
PlObliqStem_anim ;

LEXICON ыPlStem_anim_Fac
ыPlNomStem_anim_Fac ;
PlAccStem_anim_Fac ;
PlObliqStem_anim_Fac ;

LEXICON ыPlStem_anim_Prb
ыPlNomStem_anim_Prb ;
PlAccStem_anim_Prb ;
PlObliqStem_anim_Prb ;

LEXICON ыPlStem_anim_ей      !Inanimate
ыPlNomStem_anim ;
+Anim+Pl+Acc:%>ей # ;
+Anim+Pl+Gen:%>ей # ;
PlPeriphStem_anim ;

LEXICON ыPlStem_anim_zero
ыPlNomStem_anim ;
+Anim+Pl+Acc:%> # ;
+Anim+Pl+Gen:%> # ;
PlObliqStem_anim ;

LEXICON ыPlEnd_anim
ыPlNomEnd_anim ;
PlAccEnd_anim ;
PlObliqEnd_anim ;

LEXICON ыPlEnd_anim_Prb
ыPlNomEnd_anim_Prb ;
PlAccEnd_anim_Prb ;
PlObliqEnd_anim_Prb ;

LEXICON ыPlEnd_anim_gPrb
ыPlNomEnd_anim ;
+Anim+Pl+Acc+Prb:%>о́в # ;
+Anim+Pl+Gen+Prb:%>о́в # ;
PlPeriphEnd_anim ;

LEXICON ыPlEnd_anim_ов
ыPlNomEnd_anim ;
+Anim+Pl+Acc:%>о́в%^G # ;
+Anim+Pl+Gen:%>о́в%^G # ;
PlPeriphEnd_anim ;

LEXICON ыPlEnd_anim_ов_Prb
ыPlNomEnd_anim_Prb ;
+Anim+Pl+Acc+Prb:%>о́в%^G # ;
+Anim+Pl+Gen+Prb:%>о́в%^G # ;
PlPeriphEnd_anim_Prb ;

LEXICON ыPlAnom_anim
ыPlNomStem_anim ;
PlAccEnd_anim ;
PlObliqEnd_anim ;

LEXICON аPlStem_anim
аPlNomStem_anim ;
PlAccStem_anim ;
PlObliqStem_anim ;

LEXICON аPlStem_anim_Prb
аPlNomStem_anim_Prb ;
PlAccStem_anim_Prb ;
PlObliqStem_anim_Prb ;

LEXICON аPlStem_anim_ов
аPlNomStem_anim ;
+Anim+Pl+Acc:%>о́в%^G # ;
+Anim+Pl+Gen:%>о́в%^G # ;
PlPeriphStem_anim ;

LEXICON аPlStem_anim_zero
аPlNomStem_anim ;
+Anim+Pl+Acc:%> # ;
+Anim+Pl+Gen:%> # ;
PlPeriphStem_anim ;

LEXICON аPlEnd_anim
аPlNomEnd_anim ;
PlAccEnd_anim ;
PlObliqEnd_anim ;

LEXICON ыPlNomStem
+Inan+Pl+Nom:%>ы # ;

LEXICON ыPlNomStem_Fac
+Inan+Pl+Nom+Fac:%>ы # ;

LEXICON ыPlNomStem_Prb
+Inan+Pl+Nom+Prb:%>ы # ;

LEXICON ыPlNomStem_anim
+Anim+Pl+Nom:%>ы # ;

LEXICON ыPlNomStem_anim_Fac
+Anim+Pl+Nom+Fac:%>ы # ;

LEXICON ыPlNomStem_anim_Prb
+Anim+Pl+Nom+Prb:%>ы # ;

LEXICON ыPlNomEnd
+Inan+Pl+Nom:%>ы́ # ;

LEXICON ыPlNomEnd_Prb
+Inan+Pl+Nom+Prb:%>ы́ # ;

LEXICON ыPlNomEnd_anim
+Anim+Pl+Nom:%>ы́ # ;

LEXICON ыPlNomEnd_anim_Prb
+Anim+Pl+Nom+Prb:%>ы́ # ;

LEXICON ыPlAccStem
+Inan+Pl+Acc:%>ы # ;

LEXICON ыPlAccStem_Fac
+Inan+Pl+Acc+Fac:%>ы # ;

LEXICON ыPlAccStem_Prb
+Inan+Pl+Acc+Prb:%>ы # ;

LEXICON ыPlAccEnd
+Inan+Pl+Acc:%>ы́ # ;

LEXICON ыPlAccEnd_Prb
+Inan+Pl+Acc+Prb:%>ы́ # ;

LEXICON PlAccStem_anim
+Anim+Pl+Acc:%>ов # ;

LEXICON PlAccStem_anim_Fac
+Anim+Pl+Acc+Fac:%>ов # ;

LEXICON PlAccStem_anim_Prb
+Anim+Pl+Acc+Prb:%>ов # ;

LEXICON PlAccEnd_anim
+Anim+Pl+Acc:%>о́в # ;

LEXICON PlAccEnd_anim_Prb
+Anim+Pl+Acc+Prb:%>о́в # ;

LEXICON аPlNomStem
+Inan+Pl+Nom:%>а # ;

LEXICON аPlNomStem_Prb
+Inan+Pl+Nom+Prb:%>а # ;

LEXICON аPlNomStem_anim
+Anim+Pl+Nom:%>а # ;

LEXICON аPlNomStem_anim_Prb
+Anim+Pl+Nom+Prb:%>а # ;

LEXICON аPlNomEnd
+Inan+Pl+Nom:%>а́ # ;

LEXICON аPlNomEnd_Prb
+Inan+Pl+Nom+Prb:%>а́ # ;

LEXICON аPlNomEnd_anim
+Anim+Pl+Nom:%>а́ # ;

LEXICON аPlNomEnd_anim_Prb
+Anim+Pl+Nom+Prb:%>а́ # ;

LEXICON аPlAccStem
+Inan+Pl+Acc:%>а # ;

LEXICON аPlAccStem_Prb
+Inan+Pl+Acc+Prb:%>а # ;

LEXICON аPlAccEnd
+Inan+Pl+Acc:%>а́ # ;

LEXICON аPlAccEnd_Prb
+Inan+Pl+Acc+Prb:%>а́ # ;

LEXICON PlObliqStem
+Inan+Pl+Gen:%>ов # ;
PlPeriphStem ;

LEXICON PlObliqStem_Fac
+Inan+Pl+Gen+Fac:%>ов # ;
PlPeriphStem_Fac ;

LEXICON PlObliqStem_Prb
+Inan+Pl+Gen+Prb:%>ов # ;
PlPeriphStem_Prb ;

LEXICON PlObliqStem_anim
+Anim+Pl+Gen:%>ов # ;
PlPeriphStem_anim ;

LEXICON PlObliqStem_anim_Fac
+Anim+Pl+Gen+Fac:%>ов # ;
PlPeriphStem_anim_Fac ;

LEXICON PlObliqStem_anim_Prb
+Anim+Pl+Gen+Prb:%>ов # ;
PlPeriphStem_anim_Prb ;

LEXICON PlPeriphStem
+Inan+Pl+Loc:%>ах # ;
+Inan+Pl+Dat:%>ам # ;
+Inan+Pl+Ins:%>ами # ;

LEXICON PlPeriphStem_Fac
+Inan+Pl+Loc+Fac:%>ах # ;
+Inan+Pl+Dat+Fac:%>ам # ;
+Inan+Pl+Ins+Fac:%>ами # ;

LEXICON PlPeriphStem_Prb
+Inan+Pl+Loc+Prb:%>ах # ;
+Inan+Pl+Dat+Prb:%>ам # ;
+Inan+Pl+Ins+Prb:%>ами # ;

LEXICON PlPeriphStem_anim
+Anim+Pl+Loc:%>ах # ;
+Anim+Pl+Dat:%>ам # ;
+Anim+Pl+Ins:%>ами # ;

LEXICON PlPeriphStem_anim_Fac
+Anim+Pl+Loc+Fac:%>ах # ;
+Anim+Pl+Dat+Fac:%>ам # ;
+Anim+Pl+Ins+Fac:%>ами # ;

LEXICON PlPeriphStem_anim_Prb
+Anim+Pl+Loc+Prb:%>ах # ;
+Anim+Pl+Dat+Prb:%>ам # ;
+Anim+Pl+Ins+Prb:%>ами # ;

LEXICON PlObliqEnd
+Inan+Pl+Gen:%>о́в # ;
PlPeriphEnd ;

LEXICON PlObliqEnd_Prb
+Inan+Pl+Gen+Prb:%>о́в # ;
PlPeriphEnd_Prb ;

LEXICON PlObliqEnd_anim
+Anim+Pl+Gen:%>о́в # ;
PlPeriphEnd_anim ;

LEXICON PlObliqEnd_anim_Prb
+Anim+Pl+Gen+Prb:%>о́в # ;
PlPeriphEnd_anim_Prb ;

LEXICON PlPeriphEnd
+Inan+Pl+Loc:%>а́х # ;
+Inan+Pl+Dat:%>а́м # ;
+Inan+Pl+Ins:%>а́ми # ;

LEXICON PlPeriphEnd_Prb
+Inan+Pl+Loc+Prb:%>а́х # ;
+Inan+Pl+Dat+Prb:%>а́м # ;
+Inan+Pl+Ins+Prb:%>а́ми # ;

LEXICON PlPeriphEnd_anim
+Anim+Pl+Loc:%>а́х # ;
+Anim+Pl+Dat:%>а́м # ;
+Anim+Pl+Ins:%>а́ми # ;

LEXICON PlPeriphEnd_anim_Prb
+Anim+Pl+Loc+Prb:%>а́х # ;
+Anim+Pl+Dat+Prb:%>а́м # ;
+Anim+Pl+Ins+Prb:%>а́ми # ;

LEXICON Pl_ин_anim
+Anim+Pl+Nom:е # ;
+Anim+Pl+Acc: # ;
+Anim+Pl+Gen: # ;
PlPeriphEnd_anim ;

LEXICON пол-NomAccOnly ! see Zaliznjak §1 (pg 73)
+N+Neu+Sg+Nom: # ;
+N+Neu+Sg+Acc: # ;
+N+Neu+Inan+Pl+Nom: # ;
+N+Neu+Inan+Pl+Acc: # ;

LEXICON пол-м_a
+N+Msc: MSgObliqStem ;
+N+Msc: ыPlStem ;

LEXICON пол-м_b
+N+Msc: MSgObliqEnd ;
+N+Msc: ыPlEnd ;

LEXICON пол-м_c
+N+Msc: MSgObliqStem ;
+N+Msc: ыPlEnd ;

LEXICON пол-м_c1
+N+Msc: MSgObliqStem ;
+N+Msc: аPlEnd ;

LEXICON пол-м_e
+N+Msc: MSgObliqStem ;
+N+Msc: ыPlAnom ;

LEXICON пол-с_b12
+N+Neu: MSgObliqEnd ;
+N+Neu: ыPlEnd_ов ;

LEXICON пол-с_d
+N+Neu: MSgObliqEnd ;
+N+Neu: аPlStem ;

LEXICON пол-ж_a
+N+Fem: FSgObliqStem ;
+N+Fem: ыPlStem ;

LEXICON пол-ж_d
+N+Fem: FSgObliqEnd ;
+N+Fem: ыPlStem ;

LEXICON полкилометра
:по̀лкиломе́тра пол-NomAccOnly ;
:полукиломе́тр пол-м_a ;

LEXICON пол-литра
:по̀л-ли́тра пол-NomAccOnly ;
:полули́тр пол-м_a ;

LEXICON полметра
:по̀лме́тра пол-NomAccOnly ;
:полуме́тр пол-м_a ;

LEXICON полмиллиона
:по̀лмиллио́на пол-NomAccOnly ;
:полумиллио́н пол-м_a ;

LEXICON полфунта
:по̀лфу́нта пол-NomAccOnly ;
:полуфу́нт пол-м_a ;

LEXICON пол-листа
:по̀л-листа́ пол-NomAccOnly ;
:полули́ст пол-м_b ;

LEXICON полчаса
:по̀лчаса́ пол-NomAccOnly ;
:получа́с пол-м_c ;

LEXICON полшага
:по̀лшага́ пол-NomAccOnly ;
:полуша́г пол-м_c ;

LEXICON полвека
:по̀лве́ка пол-NomAccOnly ;
:полуве́к пол-м_c1 ;

LEXICON полгода
:по̀лго́да пол-NomAccOnly ;
:полуго́д пол-м_e ;

LEXICON пол-очка
:по̀л-очка́ пол-NomAccOnly ;
:полуочко́ пол-с_b12 ;

LEXICON полведра
:по̀лведра́ пол-NomAccOnly ;
:полувёдро́ пол-с_d ;

LEXICON полминуты
:по̀лмину́ты пол-NomAccOnly ;
:полумину́та пол-ж_a ;

LEXICON полтонны
:по̀лто́нны пол-NomAccOnly ;
:полуто́нна пол-ж_a ;

LEXICON полдюжины
:по̀лдю́жины пол-NomAccOnly ;
:полудю́жина пол-ж_a ;

LEXICON полсотни
:по̀лсо́тни пол-NomAccOnly ;
:полусо́т%^Fеня пол-ж_a ;

LEXICON полтысячи
:по̀лты́сячи пол-NomAccOnly ;
:полуты́сяча пол-ж_a ;

LEXICON полверсты
:по̀лверсты́ пол-NomAccOnly ;
:полувёрста́ пол-ж_d ;
