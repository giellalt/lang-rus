Alphabet

      %>:0 !End of stem
      F:0  ! placed immediately before fleeting vowels Fа Fе Fо Fи Fя Fа́ Fе́ Fё Fо́ Fи́ Fя́
      а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я 
      а́         е́       и́           о́         у́               ы́   э́ ю́ я́ ;

Sets

Letter = а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я 
         а́         е́       и́           о́         у́               ы́   э́ ю́ я́ ;
C = б в г д ж з й к л м н п р с т ф х ц ч ш щ ;
HUSH = ж ш щ ч ;
VEL = г к х ;
PC = б в д з л м н п р с т ф ;
V = а э о у ы я е ё ю и а́ е́ и́ о́ у́ ы́ э́ ю́ я́ ;
StressedV = а́ е́ ё и́ о́ у́ ы́ э́ ю́ я́ ;

Definitions

End = [ а́:0 | о́:0 | а:0 | о:0 | я́:0 | ё:0 | е́:0 | я:0 | е:0 | ь:0 | й:0 ] ;
VELEndV = VEL [ а́:0 | о́:0 | а:0 | о:0 ] %>:0 ;     ! тоска́, молоко́, кни́га, я́блоко
VELEndC = VEL %>:0 ;                               ! уче́бник
VELEnd = [ VELEndV | VELEndC ] ;
HUSHEndV = HUSH [ а́:0 | о́:0 | а:0 | е:0 ] %>:0 ;   ! моча́, плечо́, пи́ща, чудо́вище
HUSHEndC = HUSH ( ь:0 ) %>:0 ;                     ! мяч, вещь
HUSHEnd = [ HUSHEndV | HUSHEndC ] ;
цEndV = ц [ а́:0 | о́:0 | а:0 | е:0 ] %>:0 ;         ! рысца́, мясцо́, учи́тельница, се́рдце
цEndC = ц %>:0 ;                                   ! молоде́ц
цEnd = [ цEndV | цEndC ] ;
SCEndVN = PC [ е́:0 | е:0 | ё:0 ] %>:0 ;            ! мо́ре 
SCEndVF = PC [ я́:0 | я:0 ] %>:0 ;                  ! земля́, неде́ля
SCEndV = [ SCEndVN | SCEndVF ] ;
SCEndC = PC [ ь:0 ] %>:0 ;                         ! конь
SCEnd = [ SCEndV | SCEndC ] ;
HCEndV = PC [ а́:0 | о́:0 | а:0 | о:0 ] %>:0 ;       ! красота́, окно́, ве́ра, сло́во
HCEndC = PC %>:0 ;                                 ! пол
HCEnd = [ HCEndV | HCEndC ] ;
VEndV = V [ я́:0 | ё:0 | е́:0 | я:0 | е:0 ] %>:0 ;   ! судия́, остриё, житие́, Мари́я, поня́тие
VEndC = V [ й:0 ] %>:0 ;                           ! музе́й
VEnd = [ VEndV | VEndC ] ;
JEndV = F:0 V: й: [ я́:0 | ё:0 | я:0 | е:0 ] %>:0 ; ! семья́, бельё, колдунья, счастье
JEndC = F:0 V: й: %>:0 ;                           ! мураве́й
JEnd = [ JEndV | JEndC ] ;

Rules

"Remove extra stress marks on stem: (1) if there is a stressed ending, (2) last syllable if the ending is not a GenPl zero"
Vx:Vy <=> .#. C* _ \[ %>:0 ] $[ :StressedV ] ; !First syllable of stem
          .#. [ Letter:* - [ :StressedV ] ] _ C+ (End) %>:0 :StressedV ; !Last syllable of stem without shifting stress
          .#. C+ StressedV: (C+ [V - StressedV]) C+ _ C+ (End) %>:0 \[ о́: ] \[в:0] ; !Last syllable of stem with shifting stress
where Vx in ( а́ э́ о́ у́ ы́ я́ е́ ё ю́ и́ )
      Vy in ( а э о у ы я е е ю и )
    matched ;

"Delete lexical vowel endings (o/о́ are covered in another rule), and delete most fleeting vowels with syllabic endings"
Vx:0 <=> .#. [ Letter: | F:0 ]+ _ %>:0 ; 
         F:0 _ [ Letter ]+ (End) %>:0 :V ; ! This does not delete люб(о)вью, since the ь is 1st 
where Vx in ( а е ё и я а́ е́ и́ я́ ) ;

"Delete о/о́ in lexical endings, fleeting vowels, and GenPl after hard consonants"
[ о:0 | о́:0 ] <=> .#. [ Letter: | F:0 ]+ _  %>:0 ; ! lexical ending
                  F:0 _ [ Letter ]+ (End) %>:0 :V ; ! fleeting vowel
                  [ VELEndV | HUSHEndV | цEndV | HCEndV | JEndV ] _ в:0 .#. ; ! zero GenPl

"Delete ь in lexical endings, and before fleeting vowels"
ь:0 <=> .#. [ Letter: | F:0 ]+ _  %>:0 ;
         .#. Letter+ _ F:0 V ;

"Delete й in lexical endings, and before fleeting vowels"
й:0 <=> .#. [ ?* - [ F:0 ] ] V _  %>:0 ;
         .#. Letter+ _ F:0 V ;

"stem-й > ь before vowels ( муравей > муравья; семей > семья )"
й:ь <=> F:0 V:0 _ ([ я́:0 | ё:0 | е́:0 | я:0 | е:0 ]) %>:0 :V ;

"о>е"
о:е <=> [ HUSHEnd | цEnd | VEnd | SCEnd | JEnd ] _ [ ?* - [ в:0 ?* ] ] .#. ;

"ы>и (stressed or unstressed)"
Vx:Vy <=> [ VELEnd | HUSHEnd | VEnd | SCEnd | JEnd ] _ ;
where Vx in ( ы ы́ )
      Vy in ( и и́ )
    matched;

"ю>у and я>а (stressed or unstressed) after a velar, husher, or ц"
Vx:Vy <=> [ VELEnd | HUSHEnd | цEnd ] _ ;
where Vx in ( ю ю́ я я́ )
      Vy in ( у у́ а а́ )
    matched;

"[а у а́ о́ у́] > [я ю я́ ё ю́] after soft-consonant stems (including й: муравей, семья, бельё, etc) and vowel stems (музей, критерий, понятие, Мария, etc)"
Vx:Vy <=> [ SCEnd | VEnd | JEnd ] _ [ ?* - [ [ в:й | в:0 ] ?* ] ] .#. ;
where Vx in ( а у а́ о́ у́ )
      Vy in ( я ю я́ ё ю́ )
    matched;

"е>и after и-stems (e.g. о Марии)"
е:и <=> [ и | и́ ] [ я́:0 | ё:0 | е́:0 | я:0 | е:0 | й:0 ]  %>:0 _ .#. ;

"GenPl ей: в>й"
в:й <=> [ HUSHEndC | SCEndC ] [ о: | о́: ] _ .#. ;
        [ HUSHEndV | SCEndV ] о́: _ .#. ;
"GenPl: stressed е́й"
о́:е́ <=> [ HUSHEndC | HUSHEndV | SCEndC | SCEndV ] _ в:й .#. ;

"Genitive Plural: zero в:0"
в:0 <=> [ VELEndV | цEndV | HCEndV | VEndV | JEndV ] [ о: | о́: ] _ .#. ;
        HUSHEndV о: _ .#. ;
        SCEndV о: _ .#. ;
"Genitive Plural: zero after soft C = ь"
о:ь <=> SCEndV _ в:0 .#. ; ! BUG: Fem 2*a in ~ня go to 0, not ь (characterized by F V н я)
"Genitive Plural: zero after V = й"
[ о:й | о́:й ] <=> VEndV _ в:0 .#. ;
