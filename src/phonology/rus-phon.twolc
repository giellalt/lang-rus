Alphabet

      %>:0 !End of stem
      J    ! Marks й-stems like муравей, семья, etc.
      F:0  ! placed immediately before fleeting vowels Fа Fе Fо Fи Fя Fа́ Fе́ Fё Fо́ Fи́ Fя́
      а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я 
      а́         е́       и́           о́         у́               ы́   э́ ю́ я́ ;

Sets

Letter = а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я 
         а́         е́       и́           о́         у́               ы́   э́ ю́ я́ ;
C = б в г д ж з й к л м н п р с т ф х ц ч ш щ ;
HUSH = ж ш щ ч ;
VEL = г к х ;
PC = б в д з л м н п р с т ф ;
V = а э о у ы я е ё ю и а́ е́ и́ о́ у́ ы́ э́ ю́ я́ ;
StressedV = а́ е́ ё и́ о́ у́ ы́ э́ ю́ я́ ;

Definitions

!End = [ а́:0 | о́:0 | а:0 | о:0 | я́:0 | ё:0 | е́:0 | я:0 | е:0 | ь:0 | й:0 ] ;
VELEndV = VEL [ а́:0 | о́:0 | а:0 | о:0 ] %>:0 ;
VELEndC = VEL %>:0 ;
VELEnd = [ VELEndV | VELEndC ] ;
HUSHEndV = HUSH [ а́:0 | о́:0 | а:0 | е:0 ] %>:0 ;
HUSHEndC = HUSH ( ь:0 ) %>:0 ;
HUSHEnd = [ HUSHEndV | HUSHEndC ] ;
цEndV = ц [ а́:0 | о́:0 | а:0 | е:0 ] %>:0 ;
цEndC = ц %>:0 ;
цEnd = [ цEndV | цEndC ] ;
SCEndVN = PC [ ё:0 | е́:0 | е:0 ] %>:0 ;
SCEndVF = PC [ я́:0 | я:0 ] %>:0 ;
SCEndV = [ SCEndVN | SCEndVF ] ;
SCEndC = PC [ ь:0 ] %>:0 ;
SCEnd = [ SCEndV | SCEndC ] ;
HCEndV = PC [ а́:0 | о́:0 | а:0 | о:0 ] %>:0 ;
HCEndC = PC %>:0 ;
HCEnd = [ HCEndV | HCEndC ] ;
VEndV = V [ я́:0 | ё:0 | е́:0 | я:0 | е:0 ] %>:0 ;
VEndC = V [ й:0 ] %>:0 ;
VEnd = [ VEndV | VEndC ] ;
JEndV = [ J: | :ь ] [ я́:0 | ё:0 ] %>:0 ;
JEndC = [ J: | :ь ] %>:0 ;
JEnd = [ JEndV | JEndC ] ;

Rules

"Remove all but right-most stress mark"
Vx:Vy <=> .#. Letter* _ \[ %>:0 ] $[ :StressedV ] ;
where Vx in ( а́ э́ о́ у́ ы́ я́ е́ ё ю́ и́ )
      Vy in ( а э о у ы я е е ю и )
    matched ;

"Delete lexical vowel endings (o/о́ are covered in another rule), and delete fleeting vowels with syllabic endings"
Vx:0 <=> .#. [ Letter: | F:0 | J: ]+ _ %>:0 ; 
         F:0 _ $[ :V ] ;
where Vx in ( а е ё и я а́ е́ и́ я́ ) ;

"Delete о/о́ in lexical endings, fleeting vowels, and GenPl after hard consonants"
[ о:0 | о́:0 ] <=> .#. [ Letter: | F:0 | J: ]+ _  %>:0 ; ! lexical ending
                  F:0 _ $[ :V ] ; ! fleeting vowel
                  [ VELEndV | HUSHEndV | цEndV | HCEndV | JEndV ] _ в:0 .#. ; ! zero GenPl

"Delete ь and й in lexical endings, and before fleeting vowels"
Lx:0 <=> .#. [ Letter: | F:0 | J: ]+ _  %>:0 ;
         .#. Letter+ _ F:0 V ;
where Lx in ( ь й ) ;

"J > й before zero ending (муравей, семей)"
J:й <=> _  :0* .#. ;

"J > ь before vowels ( муравей > муравья; семей > семья )"
J:ь <=> F:0 V:0 _ ([ я́:0 | ё:0 | е́:0 | я:0 | е:0 ]) %>:0 :V ;

"о>е"
о:е <=> [ HUSHEnd | цEnd | VEnd | SCEnd | JEnd ] _ [ ?* - [ в:0 ?* ] ] .#. ;

"ы>и (stressed or unstressed)"
Vx:Vy <=> [ VELEnd | HUSHEnd | VEnd | SCEnd | JEnd ] _ ;
where Vx in ( ы ы́ )
      Vy in ( и и́ )
    matched;

"ю>у and я>а (stressed or unstressed) after a velar, husher, or ц"
Vx:Vy <=> [ VELEnd | HUSHEnd | цEnd ] _ ;
where Vx in ( ю ю́ я я́ )
      Vy in ( у у́ а а́ )
    matched;

"[а у а́ о́ у́] > [я ю я́ ё ю́] after soft-consonant stems (including й: муравей, семья, бельё, etc) and vowel stems (музей, критерий, понятие, Мария, etc)"
Vx:Vy <=> [ SCEnd | VEnd | JEnd ] _ [ ?* - [ [ в:й | в:0 ] ?* ] ] .#. ;
where Vx in ( а у а́ о́ у́ )
      Vy in ( я ю я́ ё ю́ )
    matched;

"е>и (stressed or unstressed) after и-stems (e.g. о Марии)"
Vx:Vy <=> [ и | и́ ] [ я́:0 | ё:0 | е́:0 | я:0 | е:0 | й:0 ]  %>:0 _ .#. ;
where Vx in ( е е́ )
      Vy in ( и и́ )
    matched;

"GenPl ей: в>й"
в:й <=> [ HUSHEndC | SCEndC | SCEndVN ] [ о: | о́: ] _ .#. ;
"GenPl: stressed е́й"
о́:е́ <=> [ HUSHEndC | SCEndC | SCEndVN ] _ в:й .#. ;

"Genitive Plural: zero в:0"
в:0 <=> [ VELEndV | HUSHEndV | цEndV | SCEndVF | HCEndV | VEndV | JEndV ] [ о: | о́: ] _ .#. ;
"Genitive Plural: zero after soft C = ь"
[ о:ь | о́:ь ] <=> SCEndV _ в:0 .#. ;
"Genitive Plural: zero after V = й"
[ о:й | о́:й ] <=> VEndV _ в:0 .#. ;
