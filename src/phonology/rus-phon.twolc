Alphabet

      %>:0   ! End of nominal stem
      %<:0   ! End of verbal stem
      %^F:0  ! placed immediately before fleeting vowels Fа Fе Fо Fи Fя Fа́ Fе́ Fё Fо́ Fи́ Fя́
      %^G:0  ! Facilitates GenPl ов/ев when it "should" be zero or ей (its presence blocks the transformation rule)
      %^Z:0  ! "Zero ending" resolves to 0/ь/й (used for short-form adj's)
      %^M:0  ! Verb stem mutation
      %^Iu:ь %^Iu:й %^Iu:и   ! Imperative ending (unstressed)
      %^Is:й %^Is:и́ %^Is:0   ! Imperative ending (stressed)

      А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
      А́         Е́ Ё́     И́           О́         У́               Ы́   Э́ Ю́ Я́
      А̀         Ѐ Ё̀     Ѝ           О̀         У̀               Ы̀   Э̀ Ю̀ Я̀
      а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я 
      а́         е́ ё́     и́           о́         у́               ы́   э́ ю́ я́   ! ё́ > ё in spellrelax?
      а̀         ѐ ё̀     ѝ           о̀         у̀               ы̀   э̀ ю̀ я̀ ;

Sets

Letter = А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
         А́         Е́ Ё́     И́           О́         У́               Ы́   Э́ Ю́ Я́
         А̀         Ѐ Ё̀     Ѝ           О̀         У̀               Ы̀   Э̀ Ю̀ Я̀
         а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я
         а́         е́ ё́     и́           о́         у́               ы́   э́ ю́ я́
         а̀         ѐ ё̀     ѝ           о̀         у̀               ы̀   э̀ ю̀ я̀ ;
! Consonants
C = Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц Ч Ш Щ 
    б в г д ж з й к л м н п р с т ф х ц ч ш щ ;
! Consonants, excluding { Д д Т т }
CNoDT = Б В Г Ж З Й К Л М Н П Р С Ф Х Ц Ч Ш Щ
        б в г ж з й к л м н п р с ф х ц ч ш щ ;
! Unvoiced consonants
UnvoicedC = К П С Т Ф Х Ц Ч Ш Щ
            к п с т ф х ц ч ш щ ;
! Hushers
HUSH = Ж Ш Щ Ч
       ж ш щ ч ;
! Velars
VEL = Г К Х
      г к х ;
! Paired consonants (consonants that have both a hard and soft variant not predicted by the following vowel)
PC = Б В Д З Л М Н П Р С Т Ф
     б в д з л м н п р с т ф ;
! Paired consonants, excluding { Н н }
PCNoN = Б В Д З Л М П Р С Т Ф
        б в д з л м п р с т ф ;
! Vowels
V = А Е Ё И О У Ы Э Ю Я
    А́ Е́ Ё́ И́ О́ У́ Ы́ Э́ Ю́ Я́
    А̀ Ѐ Ё̀ Ѝ О̀ У̀ Ы̀ Э̀ Ю̀ Я̀
    а е ё и о у ы э ю я
    а́ е́ ё́ и́ о́ у́ ы́ э́ ю́ я́ 
    а̀ ѐ ё̀ ѝ о̀ у̀ ы̀ э̀ ю̀ я̀ ;
! Stressed vowels
StressedV = А́ Е́ Ё́ И́ О́ У́ Ы́ Э́ Ю́ Я́
            а́ е́ ё и́ о́ у́ ы́ э́ ю́ я́ ;

Definitions

End = [ а́:0 | о́:0 | а:0 | о:0 | я́:0 | ё:0 | е́:0 | я:0 | е:0 | ь:0 | й:0 ] ;
VELEndV = :VEL [ а́:0 | о́:0 | а:0 | о:0 ] %>:0 ;     ! тоска́, молоко́, кни́га, я́блоко
VELEndC = :VEL %>:0 ;                               ! уче́бник
VELEnd = [ VELEndV | VELEndC ] ;
HUSHEndV = :HUSH [ а́:0 | о́:0 | а:0 | е:0 ] %>:0 ;   ! моча́, плечо́, пи́ща, чудо́вище
HUSHEndC = :HUSH ( ь:0 ) %>:0 ;                     ! мяч, вещь
HUSHEnd = [ HUSHEndV | HUSHEndC ] ;
цEndV = :ц [ а́:0 | о́:0 | а:0 | е:0 ] %>:0 ;         ! рысца́, мясцо́, учи́тельница, се́рдце
цEndC = :ц %>:0 ;                                   ! молоде́ц
цEnd = [ цEndV | цEndC ] ;
SCEndV = :PC [ е́:0 | е:0 | ё:0 | я́:0 | я:0 ] %>:0 ; ! мо́ре, земля́, неде́ля
SCEndVNoN = :PCNoN [ е́:0 | е:0 | ё:0 | я́:0 | я:0 ] %>:0 ; ! GenPl of e.g. башня and MscShortForm of e.g. столетний
FVn0 = %^F:0 V: :н [ ь:0 | я:0 ] %>:0 ; ! GenPl of e.g. башня and MscShortForm of e.g. столетний
SCEndC = :PC ь:0 %>:0 ;                         ! конь
SCEnd = [ SCEndV | SCEndC ] ;
HCEndV = :PC [ а́:0 | о́:0 | а:0 | о:0 ] %>:0 ;       ! красота́, окно́, ве́ра, сло́во
HCEndC = :PC %>:0 ;                                 ! пол
HCEnd = [ HCEndV | HCEndC ] ;
VEndV = :V [ я́:0 | ё:0 | е́:0 | я:0 | е:0 ] %>:0 ;   ! судия́, остриё, житие́, Мари́я, поня́тие
VEndC = :V й:0 %>:0 ;                           ! музе́й
VEnd = [ VEndV | VEndC ] ;
JEndV = %^F:0 V: й: [ я́:0 | ё:0 | я:0 | е:0 ] %>:0 ; ! семья́, бельё, колдунья, счастье
JEndC = %^F:0 V: й: %>:0 ;                           ! мураве́й
JEnd = [ JEndV | JEndC ] ;
jEndV = \%^F:0 V: й:й [ я́:0 | ё:0 | я:0 | е:0 ] %>:0 ; ! вайя
jEndC = \%^F:0 V: й:й %>:0 ;                           ! Are there any like this?
jEnd = [ jEndV | jEndC ] ;
1ConjV = V %<:0 ;                            ! читать, умолять, белеть, воевать, etc.?
1ConjJ = (%^F:0 V:) :ь %<:0 ;                  ! бить, бей, бью
2ConjHUSH = :HUSH (%^M:0) %<:0 ;             ! слышу (ю)

Rules

"Remove extra stress marks on stem (е́>е is covered in another rule): (1) if there is a stressed ending, (2) last syllable if the ending is not a GenPl zero"
! сло́ва́
! слова́
Vx:Vy <=> .#. C* _ \[ %>:0 ] $[ :StressedV ] ; ! First syllable of stem
          .#. [ [ Letter: | %^M:]* - [ :StressedV ] ] _ C+ (End) %>:0 :StressedV ; ! Last syllable of stem without shifting stress
          .#. C+ StressedV: (C+ [V - StressedV]) C+ _ C+ (End) %>:0 ~[ о́: в:0 ] ; ! Last syllable of stem with shifting stress
          .#. Letter* _ [Letter | %^F:0 | %^M: ]* %<:0 $[:StressedV] ; ! Verbs (end-stressed)
          .#. Letter* _ [Letter | %^F:0 | %^M: ]* :StressedV [Letter | %^F:0 | %^M: ]* %<:0 ;   ! Verbs (later-on-stem-stressed)
where Vx in ( а́ э́ о́ у́ ы́ я́ е́ ё ю́ и́ )
      Vy in ( а э о у ы я е е ю и )
    matched ;

"е́>е in comparative adjective ending after velar mutation"
е́:е <=> [ г: | к: | х: ] %>:0 _ [ е: | й: ] .#. ; ! After velar mutation with comparative adjectives

"V>0: Delete lexical vowel endings (o/о́ and е are covered in other rules), and delete most fleeting vowels with syllabic endings"
Vx:0 <=> .#. [ Letter: | %^F:0 ]+ _ %>:0 ; 
         %^F:0 _ [ Letter ]+ (End) %>:0 :V ; ! This does not delete люб(о)вью, since the ь is 1st
         %^F:0 _ й: %<:0 :V ;
         %^F:0 _ %<:0 :л :V ( :с :ь ) .#. ;
where Vx in ( а е́ ё и я а́ и́ я́ ) ;

"е>0: 1) Delete lexical vowel endings; 2) Delete fleeting vowels where ending begins with a vowel; 3) Delete е/е́ after comparative adjective stem mutation."
е:0 <=> .#. [ Letter: | %^F:0 ]+ _ %>:0 ; 
         %^F:0 _ [ Letter ]+ (End) %>:0 :V ; ! This correctly does not delete люб(о)вью, since the ь is 1st
         [ г: | к: | х: ] %>:0 [ е | е́: ] _ .#. ; ! After comparative adjective mutation

"о/о́>0 in lexical endings, fleeting vowels (including verbal prefixes), and GenPl after hard consonants"
[ о:0 | о́:0 ] <=> .#. [ Letter: | %^F:0 ]+ _  %>:0 ; ! lexical ending
                  %^F:0 _ [ Letter ]+ (End) %>:0 :V ; ! fleeting vowel
                  [ в | н а д | о б | о т | п о д | п р е д | с | в з: | в о з: | и з: | н и з: | р а з: ] %^F:0 _ C (%^F:0) V ; ! Fleeting vowels in prefixes, like об(о)звал ~ об()зову
                  [ VELEndV | HUSHEndV | цEndV | HCEndV | FVn0 | JEndV | jEnd ] _ в:0 .#. ; ! zero GenPl

"з>с in some verbal prefixes with dropped fleeting vowels, when the following consonant is unvoiced"
з:с <=> [ в | в о | и | н и | р а ] _ %^F:0 о:0 UnvoicedC ;

"ь>0 in lexical endings, and before fleeting vowels"
ь:0 <=> .#. [ Letter: | %^F:0 ]+ _  %>:0 ;
        .#. Letter+ _ %^F:0 V ;

"й>0 in lexical endings, and before fleeting vowels, and in comparative adjectives with velar mutation"
й:0 <=> .#. [ ?* - [ %^F:0 ] ] V _  %>:0 ; ! in lexical ending
        .#. Letter+ _ %^F:0 V ; ! before fleeting vowel
        [ г: | к: | х: ] %>:0 [ е | е́: ] _ .#. ; ! comparative adjectives with velar mutation

"й>ь (1) when after a fleeting vowel and before a vowel ( муравей > муравья; семей > семья ) (2) in verbs with й-stems, such as бить (3) after consonants not followed by a fleeting vowel (e.g. imperative endings)"
й:ь <=> %^F:0 V:0 _ ([ я́:0 | ё:0 | е́:0 | я:0 | е:0 ]) %>:0 :V ;
        %^F:0 V:0 _ %<:0 :V ;
        C: _ \[%^F:0] ;

"Z>0"
%^Z:0 <=> [ VELEnd | HUSHEnd | цEnd | HCEnd | JEnd | FVn0 ] _ .#. ;

"Z>ь"
%^Z:ь <=>         :PCNoN ь:0 %>:0 _ .#. ;
          \[%^F:0] V: н: ь:0 %>:0 _ .#. ; ! Fem 2*a in н, e.g. столетний~столетен
                   C: н: ь:0 %>:0 _ .#. ;

"Z>й"
%^Z:й <=> VЕnd _ .#. ; ! This rule is technically true, but probably has no actual use.

"о>е"
о:е <=> [ HUSHEnd | цEnd | VEnd | SCEnd | JEnd | jEnd ] _ [ ?* - [ в:0 ?* ] ] .#. ;

"ы>и (stressed or unstressed)"
Vx:Vy <=> [ VELEnd | HUSHEnd | VEnd | SCEnd | JEnd | jEnd ] _ ;
where Vx in ( ы ы́ )
      Vy in ( и и́ )
    matched;

"ю>у and я>а (stressed or unstressed) after a velar, husher, or ц"
Vx:Vy <=> [ VELEnd | HUSHEnd | цEnd | 2ConjHUSH ] _ ;
where Vx in ( ю ю́ я я́ )
      Vy in ( у у́ а а́ )
    matched;

"[а а́ о́] > [я я́ ё] after soft-consonant stems (including й: муравей, семья, бельё, etc) and vowel stems (музей, критерий, понятие, Мария, etc) including 1st conj verbs like читать, болеть, воевать, etc."
Vx:Vy <=> [ SCEnd | VEnd | JEnd | jEnd | 1ConjV ] _ [ ?* - [ [ в:й | в:0 ] ?* ] ] .#. ;
where Vx in ( а а́ о́ )
      Vy in ( я я́ ё )
    matched;

"у>ю (1) after soft-consonant stems (including й: муравей, семья, бельё, etc) and vowel stems (музей, критерий, понятие, Мария, etc) including 1st conj verbs like читать, болеть, воевать, etc. (2) in 1st conjugation after л or ь"
Vx:Vy <=> [ SCEnd | VEnd | JEnd | jEnd | 1ConjV | 1ConjJ ] _ [ ?* - [ [ в:й | в:0 ] ?* ] ] .#. ;
          :л %<:0 _ ;
where Vx in ( у у́ )
      Vy in ( ю ю́ )
    matched;

"е>и after и-stems (e.g. о Марии)"
е:и <=> [ и | и́ ] [ я́:0 | ё:0 | е́:0 | я:0 | е:0 | й:0 ]  %>:0 _ .#. ;

"в>й: GenPl ей"
в:й <=> [ HUSHEndC | SCEndC ] [ о: | о́: ] _ .#. ;
        [ HUSHEndV | SCEndV ] о́: _ .#. ;
"о́>е́: GenPl stressed е́й"
о́:е́ <=> [ HUSHEndC | HUSHEndV | SCEndC | SCEndV ] _ в:й .#. ;

"в>0: (1) GenPl zero and (2) Masc past tense endings or past deverbals, after consonant stems"
в:0 <=> [ VELEndV | цEndV | HCEndV | VEndV | JEndV | jEnd ] [ о: | о́: ] _ .#. ;
        HUSHEndV о: _ .#. ;
        SCEndV о: _ .#. ;
        C: %<:0 _ ( с я ) .#. ; ! Past verbal adverb
        C: %<:0 _ (ш и ( с :ь ) ) .#. ; ! Past verbal adverb reflexive
        C: %<:0 _ ш %>:0 :V [ C (V) | V ] ( с я ) .#. ; ! Past active ppl BROKEN? BROKEN? BROKEN?
"о>ь: GenPl zero after soft C"
о:ь <=> SCEndVNoN _ в:0 .#. ; ! However, Fem 2*a in ~ня go to 0, not ь (characterized by F V н я)
        \[%^F:0] V: н: я:0 %>:0 _ в:0 .#. ; ! However, Fem 2*a in н, e.g. башня~башен
        C: н: [ е́:0 | е:0 | ё:0 | я́:0 ] %>:0 _ в:0 .#. ;
"о/о́>й: GenPl zero after V"
[ о:й | о́:й ] <=> VEndV _ в:0 .#. ;

"г>ж (1) with comparative(-ее)/superlative(-ейший) adjectives (2) with ^M symbol (verbs) (3) in pres/fut before е/ё"
г:ж <=> _ %>:0 [ е | е́: ] [ е: | й: ] ( ш: V: Letter: (V:) ) .#. ;
          _ %^M: ;
          _ %<:0 [ е: | ё: ] ;

"х>ш (1) with comparative(-ее)/superlative(-ейший) adjectives (2) with ^M symbol (verbs)"
х:ш <=> _ %>:0 [ е | е́: ] [ е: | й: ] ( ш: V: Letter: (V:) ) .#. ;
          _ %^M: ;

"к>ч Velar mutation (1) with comparative(-ее)/superlative(-ейший) adjectives (2) with ^M symbol (verbs) (as opposed to ск>щ) (3) in pres/fut before е/ё"
к:ч <=> _ %>:0 [ е | е́: ] [ е: | й: ] ( ш: V: Letter: (V:) ) .#. ;
        \с:0 _ %^M: ;
        _ %<:0 [ е: | ё: ] ;

"т>ч (1) Pres/Fut mutation of т (as opposed to ст>щ); (2) гть/кть > 0чь"
т:ч <=> \с: _ %^M: ;
        [ г:0 | к:0 ] %<:0 _ ь ( с я ) .#. ;

"г/к>0 before infinitive ending: гть/кть > чь"
Cx:0 <=> _ %<:0 т:ч ь ( с я ) .#. ;
where Cx in ( г к ) ;

"ё>е́ in some infinitives (чь and )"
ё:е́ <=> _ [ г:0 | к:0 ] %<:0 т:ч ь ( с я ) .#. ;
        _ [ б:с | д:с | с | т:с | [ с т:с ] ] %<:0 т ь ( с я ) .#. ;

"д/т>0 before past tense"
Cx:0 <=> _ %<:0 л ( [ V ( с :ь ) | с [ :я | :я́ ] ] ) .#.  ;
where Cx in ( д т ) ;

"[бдт]>с in before infinitive endings"
Cx:с <=> _ %<:0 т ( [ и́ ( с :ь ) | ь ( с я ) ] ) .#.  ;
where Cx in ( б д т ) ;

"с>0 before т>щ and к>щ stem mutations"
с:0 <=> _ [ к: | т: ] %^M: ;

"с[ к т ]>щ Mutation of ск and ст"
Cx:щ <=> с:0 _ %^M: ;
where Cx in ( к т ) ;

"[ з с д ] > [ ж ш ж ] Verb stem mutations with ^M symbol (г к х covered in another rule)"
Cx:Cy <=> _ %^M: ;
where Cx in ( д з с )
      Cy in ( ж ж ш )
      matched ;

"^M>л Addition of л after labials in 'mutation' with ^M symbol (verbs)"
%^M:л <=> [ б: | п: | в: | ф: | м: ] _ ;

"л>0 in Masc past tense after consonant stems"
л:0 <=> CNoDT: %<:0 _ ( с я ) .#. ;

"Is>0 in imperatives with й stems (e.g. бить, вить, лить, пить, шить)"
%^Is:0 <=> й %<:0 _ (т е) (с [ :я | :ь ]) .#. ;

"Is>и́ in imperatives with consonant stems"
%^Is:и́ <=> [ :C - :й ] (%^M:) %<:0 _ (т е) (с [ :я | :ь ]) .#. ;

"Is/Iu>й in Imperatives with vowel stems"
Ex:й <=> :V %<:0 _ (т е) (с [ :я | :ь ]) .#. ;
where Ex in ( %^Iu %^Is ) ;

"Iu>и in Imperatives with double-consonant stems, and щ stems"
%^Iu:и <=> :C (:ь) :C (%^M:0) %<:0 _ (т е) (с [ :я | :ь ]) .#. ;
           :щ (%^M:0) %<:0 _ (т е) (с [ :я | :ь ]) .#. ; !Is this true? Examples?
!           .#. ( :п :о ) :в :ы́ [ :Letter | %^F:0 ]*-:V :C (%^M:0) %<:0 _ (т е) (с [ :я | :ь ]) .#. ; ! BROKEN
                ! по́мн<I   вы́став<I   обры́скM<I  мо́рщ<I
                ! по́мн0и   вы́став0и   обры́0щ00и  мо́рщ0и

"Iu>ь in Imperatives with consonant stem (exceptions in previous rule)"
%^Iu:ь <=> :V [ :C - :щ ] (%^M:0) %<:0 _ (т е) (с [ :я | :ь ]) .#. ;
                ! отве́т<I
                ! отве́т0ь

"я>ь (i.e. ся>сь) in reflexive imperatives"
я:ь <=> %<:0 [ :и | :и́ ] с: _ .#. ;
        %<:0 [ :и | :и́ | :ь | :й ] т е с _ .#. ;
