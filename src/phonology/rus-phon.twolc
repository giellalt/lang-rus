Alphabet

      %>:0 !End of stem
      %^GenPl:0 %^GenPlEnd:0
      M:0 ! Marks masculine stems (used for GenPl)
      S:0 ! Marks soft-consonant stems with a zero NomSg (also could be marked by ь+M, redundant?)
      s:0 ! Marks soft-consonant stems with a vowel NomSg (also could be marked by ь-M, redundant?)
      Z:0 Z:ь Z:й ! Zero NomSg
      F:0 ! placed immediately before fleeting vowels Fа Fе Fо Fи Fя Fа́ Fе́ Fё Fо́ Fи́ Fя́
      а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я 
      А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я 
      а́         е́       и́           о́         у́               ы́   э́ ю́ я́
      А́         Е́       И́           О́         У́               Ы́   Э́ Ю́ Я́ ;

Definitions

Sets

Letter = а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я 
         А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я 
         а́         е́       и́           о́         у́               ы́   э́ ю́ я́
         А́         Е́       И́           О́         У́               Ы́   Э́ Ю́ Я́ ;
C = б в г д ж з й к л м н п р с т ф х ц ч ш щ 
    Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц Ч Ш Щ ;
HUSH = ж ш щ ч 
       Ж Ш Щ Ч ;
VEL = г к х 
      Г К Х ;
V = а э о у ы я е ё ю и а́ е́ и́ о́ у́ ы́ э́ ю́ я́
    А Э О У Ы Я Е Ё Ю И А́ Е́ И́ О́ У́ Ы́ Э́ Ю́ Я́ ;
StressedV = а́ е́ ё и́ о́ у́ ы́ э́ ю́ я́
            А́ Е́ Ё И́ О́ У́ Ы́ Э́ Ю́ Я́ ;

Rules

"о>е"
о:е <=> [ HUSH | ц | Ц | V ] %>:0 (й:0) (M:0) _ ;
        [ C - [ VEL | HUSH | ц | Ц ] ] %>:0 [ S:0 | s:0 ] (M:0) _ ;

"ы>и (stressed or unstressed)"
Vx:Vy <=> [ VEL | HUSH | V ] %>:0 ([ й:0 | S:0 | s:0 ]) (M:0) _ ;
          [ C - [ й: | :ь | VEL | HUSH | ц | Ц ] ] %>:0 [ S:0 | s:0 ] (M:0) _ ;
          [ й: | :ь ] %>:0 (M:0) _ ;
where Vx in ( ы ы́ )
      Vy in ( и и́ )
    matched;

"ю>у and я>а (stressed or unstressed) after a velar, husher, or ц"
Vx:Vy <=> [ VEL | HUSH | ц | Ц ] %>:0 (M:0) _ ;
where Vx in ( ю ю́ я я́ )
      Vy in ( у у́ а а́ )
    matched;

"Masc NomSg zero spelling after hard-consonant stems and й-stems"
Z:0 <=> [ C | й: | :ь ] %>:0 (M:0) _ ;

"Masc NomSg ь spelling after soft-consonant stems"
Z:ь <=> [ C - [ й | Й | VEL | HUSH | ц | Ц ] ] %>:0 S:0 (M:0) _ ;

"Masc NomSg й spelling after vowel stems"
Z:й <=> V %>:0 (й:0) (M:0) _ ;

"[а у а́ о́ у́] > [я ю я́ ё ю́] after soft-consonant stems (including й: муравей, семья, бельё, etc) and vowel stems (музей, критерий, понятие, Мария, etc)"
Vx:Vy <=> [ C - [ й: | :ь | VEL | HUSH | ц | Ц ] ] %>:0 [ S:0 | s:0 ] (M:0) _ ;
          [ й: | :ь ] %>:0 (M:0) _ ;
          V %>:0 (й:0) (M:0) _ ;
where Vx in ( а у а́ о́ у́ )
      Vy in ( я ю я́ ё ю́ )
    matched;

"е>и (stressed or unstressed) after и-stems"
Vx:Vy <=> [ и | И | и́ | И́ ] %>:0 (й:0) (M:0) _ ;
where Vx in ( е е́ )
      Vy in ( и и́ )
    matched;

"Fleeting vowel deletion with vowel-containing ending"
Fx:0 <=> F:0 _ ?* %>:0 ([ й:0 | S:0 | s:0 ]) (M:0) [%^GenPl:ов | %^GenPlEnd:о́в | %^GenPl:ев | %^GenPlEnd:ёв | %^GenPl:ей | %^GenPlEnd:е́й | :V ] ;
where Fx in ( а е ё о и я а́ е́ о́ и́ я́ ) ;

"Remove all but right-most stress mark"
Vx:Vy <=> .#. Letter* _ $[ %^GenPlEnd:о́в | %^GenPlEnd:ёв | %^GenPlEnd:е́й | :StressedV ] ;
where Vx in ( а́ э́ о́ у́ ы́ я́ е́ ё ю́ и́ )
      Vy in ( а э о у ы я е е ю и )
    matched ;

"й/ь deletion before fleeting vowels (копейка~копеек, яйцо~яиц)"
[ й:0 | ь:0 ] <=> _ F:0 V ;

"й>ь alternation before vowels (муравей > муравья)"
й:ь <=> F:0 V:0 _ %>:0 ([ й:0 | S:0 | s:0 ]) (M:0) [%^GenPl:ов | %^GenPlEnd:о́в | %^GenPl:ев | %^GenPlEnd:ёв | :V ] ;

"ь>й alternation before zero (семья > семей)"
ь:й <=> F:0 V _ %>:0 :0+ .#. ;

"Genitive Plural: unstressed ов"
%^GenPl:ов <=> [ C - [ й | Й | HUSH | ц | Ц ] ] %>:0 M:0 _ ;
"Genitive Plural: stressed о́в"
%^GenPlEnd:о́в <=> [ C - [ й | Й | HUSH ] ] %>:0 M:0 _ ;
"Genitive Plural: unstressed ев"
%^GenPl:ев <=> V %>:0 (й:0) M:0 _ ;
               ц %>:0 M:0 _ ;
"Genitive Plural: stressed ёв"
%^GenPlEnd:ёв <=> й:ь %>:0 M:0 _ ;

"Genitive Plural: ей"
Ix:Iy <=> HUSH %>:0 M:0 _ ;
          [ C - [ й | Й | VEL | ц | Ц ] ] %>:0 S:0 ( M:0 ) _ ;
where Ix in ( %^GenPl %^GenPlEnd )
      Iy in ( ей е́й )
    matched;

"Genitive Plural: zero after hard C = 0"
[ %^GenPl:0 | %^GenPlEnd:0 ] <=> C %>:0 _ ;
"Genitive Plural: zero after soft C =  ь"
[ %^GenPl:ь | %^GenPlEnd:ь ] <=> [ C - [ й | Й | VEL | HUSH | ц | Ц ] ] %>:0 s:0 _ ;
"Genitive Plural: zero after V = й"
[ %^GenPl:й | %^GenPlEnd:й ] <=> V %>:0 ( й:0 ) _ ;
