Alphabet

      %>:0   ! End of nominal stem
      %<:0   ! End of verbal stem
      %^F:0  ! placed immediately before fleeting vowels Fа Fе Fо Fи Fя Fа́ Fе́ Fё Fо́ Fи́ Fя́
      %^pFо:о %^pFо́:о́ ! verbal prefix fleeting vowels
      %^G:0  ! Facilitates GenPl ов/ев when it "should" be zero or ей (its presence blocks the transformation rule)
      %^Z:0  ! "Zero ending" resolves to 0/ь/й (used for short-form adj's)
      %^M:0  ! Verb stem mutation
      %^Iu:ь %^Iu:й %^Iu:и   ! Imperative ending (unstressed)
      %^Is:й %^Is:и́ %^Is:0   ! Imperative ending (stressed)
      
      %[%>%]:%> %[%<%]:%< ! angle brackets (this is to avoid the internal use of < and >)
      
      %%
      %  ! space
      %-

      ¹ ² ³ ⁴ ⁵ ⁶ ⁷ ⁸ ⁹ ⁰ ! Used to enumerate homonymous lemmas

      P S ! as in, PS
      
      А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
      А́         Е́ Ё́     И́           О́         У́               Ы́   Э́ Ю́ Я́
      А̀         Ѐ Ё̀     Ѝ           О̀         У̀               Ы̀   Э̀ Ю̀ Я̀
      а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я 
      а́         е́ ё́     и́           о́         у́               ы́   э́ ю́ я́   ! ё́ is not currently used, since = ё
      а̀         ѐ ё̀     ѝ           о̀         у̀               ы̀   э̀ ю̀ я̀ ;

Sets

Letter = А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
         А́         Е́ Ё́     И́           О́         У́               Ы́   Э́ Ю́ Я́
         А̀         Ѐ Ё̀     Ѝ           О̀         У̀               Ы̀   Э̀ Ю̀ Я̀
         а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я
         а́         е́ ё́     и́           о́         у́               ы́   э́ ю́ я́
         а̀         ѐ ё̀     ѝ           о̀         у̀               ы̀   э̀ ю̀ я̀ 
         %- %  ;
! Consonants
C = Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц Ч Ш Щ 
    б в г д ж з й к л м н п р с т ф х ц ч ш щ ;
! Consonants, excluding { Д д Т т }
CNoDT = Б В Г Ж З Й К Л М Н П Р С Ф Х Ц Ч Ш Щ
        б в г ж з й к л м н п р с ф х ц ч ш щ ;
! Unvoiced consonants
UnvoicedC = К П С Т Ф Х Ц Ч Ш Щ
            к п с т ф х ц ч ш щ ;
! Hushers
HUSH = Ж Ш Щ Ч
       ж ш щ ч ;
! Velars
VEL = Г К Х
      г к х ;
! Paired consonants (consonants that have both a hard and soft variant, excluding Velars)
PC = Б В Д З Л М Н П Р С Т Ф
     б в д з л м н п р с т ф ;
! Paired consonants, excluding { Н н }
PCNoN = Б В Д З Л М П Р С Т Ф
        б в д з л м п р с т ф ;
! Vowels
V = А Е Ё И О У Ы Э Ю Я
    А́ Е́ Ё́ И́ О́ У́ Ы́ Э́ Ю́ Я́
    А̀ Ѐ Ё̀ Ѝ О̀ У̀ Ы̀ Э̀ Ю̀ Я̀
    а е ё и о у ы э ю я
    а́ е́ ё́ и́ о́ у́ ы́ э́ ю́ я́ 
    а̀ ѐ ё̀ ѝ о̀ у̀ ы̀ э̀ ю̀ я̀ ;
! Stressed vowels
StressedV = А́ Е́ Ё́ Ё И́ О́ У́ Ы́ Э́ Ю́ Я́
            а́ е́ ё́ ё и́ о́ у́ ы́ э́ ю́ я́ ;

UnstressedV = А Е И О У Ы Э Ю Я
              а е и о у ы э ю я ;

Definitions

End = [ а́:0 | о́:0 | а:0 | о:0 | я́:0 | ё:0 | е́:0 | я:0 | е:0 | ь:0 | й:0 ] ;
VELEndV = :VEL [ а́:0 | о́:0 | а:0 | о:0 ] %>:0 ;     ! тоска́, молоко́, кни́га, я́блоко
VELEndC = :VEL %>:0 ;                               ! уче́бник
VELEnd = [ VELEndV | VELEndC ] ;
HUSHEndV = :HUSH [ а́:0 | о́:0 | а:0 | е:0 ] %>:0 ;   ! моча́, плечо́, пи́ща, чудо́вище
HUSHEndC = :HUSH ( ь:0 ) %>:0 ;                     ! мяч, вещь
HUSHEnd = [ HUSHEndV | HUSHEndC ] ;
цEndV = :ц [ а́:0 | о́:0 | а:0 | е:0 ] %>:0 ;         ! рысца́, мясцо́, учи́тельница, се́рдце
цEndC = :ц %>:0 ;                                   ! молоде́ц
цEnd = [ цEndV | цEndC ] ;
SCEndV = :PC [ е́:0 | е:0 | ё:0 | я́:0 | я:0 ] %>:0 ; ! мо́ре, земля́, неде́ля
SCEndVNoN = :PCNoN [ е́:0 | е:0 | ё:0 | я́:0 | я:0 ] %>:0 ;
! SCEndVNoNcleanup = \%^F:0 ? :н [ е́:0 | е:0 | ё:0 | я́:0 | я:0 ] %>:0 ;
! SCEndVNoN = [ SCEndVNoNmostly | SCEndVNoNcleanup ] ;
FVn0 = %^F:0 V: :н [ ь:0 | я:0 ] %>:0 ; ! GenPl of e.g. башня and MscShortForm of e.g. столетний
SCEndC = :PC ь:0 %>:0 ;                         ! конь
SCEnd = [ SCEndV | SCEndC ] ;
HCEndV = :PC [ а́:0 | о́:0 | а:0 | о:0 ] %>:0 ;       ! красота́, окно́, ве́ра, сло́во
HCEndC = :PC %>:0 ;                                 ! пол
HCEnd = [ HCEndV | HCEndC ] ;
VEndV = V: [ я́:0 | ё:0 | е́:0 | я:0 | е:0 ] %>:0 ;   ! судия́, остриё, житие́, Мари́я, поня́тие
VEndC = \%^F:0 V: й:0 %>:0 ;                               ! музе́й
VEnd = [ VEndV | VEndC ] ;
JEndV = %^F:0 V: й: [ я́:0 | ё:0 | я:0 | е:0 ] %>:0 ; ! семья́, бельё, колдунья, счастье
JEndC = %^F:0 V: й: %>:0 ;                           ! мураве́й
JEnd = [ JEndV | JEndC ] ;
jEndV = \%^F:0 V: й:й [ я́:0 | ё:0 | я:0 | е:0 ] %>:0 ; ! вайя
jEndC = \%^F:0 V: й:й й:0 %>:0 ;                       ! Гавайи (lexical form = Гавайй)
jEnd = [ jEndV | jEndC ] ;
1ConjV = V %<:0 ;                            ! читать, умолять, белеть, воевать, etc.?
1ConjJ = (%^F:0 V:) :ь %<:0 ;                  ! бить, бей, бью
VerbHUSH = :HUSH (%^M:0) %<:0 ;             ! слышу (ю)

Rules

"Remove extra stress marks on stem: the stress mark closest to the end remains"
! сло́во>а́   у́мн>а́   сёстFёра́>ы   се́мFе́йя́>у́    прочи́та́<ем
! слов00а́   умн0а́   сёст00р00ы   сем00ь0>ю́    прочита́0ем
Vx:Vy <=> \[ %^F:0 | %>:0 | %<:0 ] _ [ :StressedV | [ \[ %>:0 ] $[ :StressedV ] ] ] ;
where Vx in ( а́ э́ о́ у́ ы́ я́ е́ ё ю́ и́ )
      Vy in ( а э о у ы я е е ю и )
    matched ;

"е́>е in comparative adjective ending after velar mutation"
! ли́х>е́е   
! ли́ш0е0   
е́:е <=> [ г: | к: | х: ] %>:0 _ [ е: | й: ] .#. ; ! After velar mutation with comparative adjectives

"V>0: Delete lexical vowel endings (o/о́ and е are covered in other rules), and delete most fleeting vowels with syllabic endings"
Vx:0 <=> .#. [ Letter: | %^F:0 ]+ _ %>:0 ;
            ! Тама́ра>а   мо́ре>о
            ! Тама́р00а   мо́р00е
         %^F:0 _ [ Letter ]+ (End) %>:0 :V ; ! This does not delete люб(о)вью, since the ь is 1st
            ! о́кFоно>о́   зе́мFе́ля́>е́
            ! ок00н00о́   зем00л00е́
         %^F:0 _ й: %<:0 :V ;
            ! бFе́й<у́   бFе́й<Iте
            ! б00ь0ю́   б0е́й00те
         %^F:0 _ ( C: ) %<:0 :л :V ( :с :ь ) .#. ;
            ! TODO examples that are subject to this rule?
where Vx in ( а е́ ё и я а́ и́ я́ ) ;

"е>0: 1) Delete lexical vowel endings; 2) Delete fleeting vowels where ending begins with a vowel; 3) Delete е/е́ after comparative adjective stem mutation."
е:0 <=> .#. [ Letter: | %^F:0 ]+ _ %>:0 ;
            ! see examples in parallel rule of "V>0" above
         %^F:0 _ [ Letter ]+ (End) %>:0 :V ;
            ! see examples in parallel rule of "V>0" above
         [ г: | к: | х: ] %>:0 [ е | е́: ] _ .#. ; ! After comparative adjective mutation
            ! ли́х>е́е   
            ! ли́ш0е0   

"о/о́>0 in lexical endings, fleeting vowels (including verbal prefixes), and zero GenPl"
[ о:0 | о́:0 ] <=> .#. [ Letter: | %^F:0 ]+ _  %>:0 ; ! lexical ending
                    ! see examples in parallel rule of "V>0" above
                  %^F:0 _ [ Letter ]+ (End) %>:0 :V ; ! fleeting vowel
                    ! see examples in parallel rule of "V>0" above
                    ! любFо́вь>ы́   любFо́вь>ью   This does not delete люб(о)вью, since the ь is 1st
                    ! люб00в00и́   люб0о́в00ью   
                  [ VELEndV | HUSHEndV | цEndV | HCEndV | FVn0 | JEndV | jEnd ] _ в:0 .#. ; ! zero GenPl
                    ! кни́га>ов   чудо́вище>ов   пти́ца>ов   пи́ла́>о́в   ба́шFеня>ов   се́мFе́йя́>ов   ва́йя>ов
                    ! кни́г0000   чудо́вищ0000   пти́ц0000   пи́л0000   ба́ш0ен0000   сем0е́й0000   ва́й0000

"pFо/pFо́>0 Delete verbal prefix fleeting vowels"
[ %^pFо:0 | %^pFо́:0 ] <=> [ в | н а д | н а́: д | о б | о́: б | о т | о́: т | п о д | п о́: д | п р е д | п р е́: д | с | в з: | в о з: | в о́: з: | и з: | и́: з: | н и з: | н и́: з: | р а з: | р а́: з: ] _ C (%^F:0) :V ; 
                    ! Fleeting vowels in prefixes, like сожгла сжег
                    ! сFого́н<ю́ 
                    ! с00гон0ю́
                    ! TODO FIX об^pFо́гна́<ть = обо́гна́ть (two stress marks)

!"pFо>о Realize unstressed verbal prefix fleeting vowel"
!%^pFо:о <=> [ в | н а д | н а́: д | о б | о́: б | о т | о́: т | п о д | п о́: д | п р е д | п р е́: д | с | в з: | в о з: | в о́: з: | и з: | и́: з: | н и з: | н и́: з: | р а з: | р а́: з: ] _ [ ?* - [ C (%^F:0) :V ?* ] ] .#. ; 
                    ! сFгна́<ть
                    ! согна́0ть

!"pFо́>о́ Realize stressed verbal prefix fleeting vowel, if there is no stress to the right"
!%^pFо́:о́ <=> [ в | н а д | н а́: д | о б | о́: б | о т | о́: т | п о д | п о́: д | п р е д | п р е́: д | с | в з: | в о з: | в о́: з: | и з: | и́: з: | н и з: | н и́: з: | р а з: | р а́: з: ] _ [ [ ?* - :StressedV ] - [ C (%^F:0) :V [ ?* - :StressedV ] ] ] .#. ;

"pFо́>о Destress verbal prefix fleeting vowel, if there is stress to the right"
%^pFо́:о <=> [ в | н а д | н а́: д | о б | о́: б | о т | о́: т | п о д | п о́: д | п р е д | п р е́: д | с | в з: | в о з: | в о́: з: | и з: | и́: з: | н и з: | н и́: з: | р а з: | р а́: з: ] _ [ [ ?* :StressedV ?* ] - [ C (%^F:0) :V ?* ] ] .#. ;

"з>с in some verbal prefixes with dropped fleeting vowels, when the following consonant is unvoiced"
з:с <=> [ в | в о | в о́: | и | и́: | н и | н и́: | р а | р а́: ] _ [ %^pFо:0 | %^pFо́:0 ] :UnvoicedC ;
            ! разFошFе́й<у́   разFоши́л
            ! раз0ош00ь0ю́   рас00ши́л

"ь>0 in lexical endings, and before fleeting vowels"
ь:0 <=> .#. [ Letter: | %^F:0 ]+ _  %>:0 ;
            ! ко́сть>ь   ми́лость>ы
            ! ко́ст00ь   ми́лост00и
        .#. Letter+ _ %^F:0 V ;
            ! во́сьFемь>ь
            ! во́с00ем00ь

"й>0 in lexical endings, and before fleeting vowels, and in comparative adjectives with velar mutation"
й:0 <=> .#. Letter+ V _  %>:0 ; ! in lexical ending
            ! музе́й>а   
            ! музе́00я   
        .#. Letter+ V й:й _  %>:0 ; ! in lexical ending
            ! Гава́йй>ы
            ! Гава́й00и
        .#. Letter+ _ %^F:0 V ; ! before fleeting vowel
            ! копе́йFека>ов
            ! копе́00ек0000
        [ г: | к: | х: ] %>:0 [ е | е́: ] _ .#. ; ! comparative adjectives with velar mutation
            ! ли́х>е́й
            ! ли́ш0е0

"й>ь (1) when after a fleeting vowel and before a vowel ( муравей > муравья; семей > семья ) (2) in verbs with й-stems, such as бить"
й:ь <=> C: %^F:0 V: _ ([ я́: | ё: | е́: | я: | е: ]) %>:0 :V ;
            ! муравFе́й>а́   белFе́йё>о́
            ! мурав00ь0я́   бел00ь00ё
        C: %^F:0 V: _ %<:0 :V ;
            ! бFе́й<у́
            ! б00ь0ю́

"Z>0"
%^Z:0 <=> [ VELEnd | HUSHEnd | цEnd | HCEnd | JEnd | FVn0 ] _ .#. ; ! Fem 2*a in н, e.g. столетний~столетен
            ! но́в>Z   столе́тFень>Z
            ! но́в00   столе́т0ен000

"Z>ь"
%^Z:ь <=>         :PCNoN ь:0 %>:0 _ .#. ;
            ! 
          \[%^F:0] V: н: ь:0 %>:0 _ .#. ;
            ! си́нь>Z
            ! си́нь00
                   C: н: ь:0 %>:0 _ .#. ;

"Z>й"
%^Z:й <=> VЕnd _ .#. ; ! BROKEN? TODO perhaps this should include SCEnd (or some subset). Not all endings are represented for Z.
            ! длинноше́й>Z
            ! длинноше́00й

"о>е"
о:е <=> [ HUSHEnd | цEnd | VEnd | SCEnd | JEnd | jEnd ] _ [ ?* - [ в:0 ?* ] ] .#. ; ! в:0 occurs in zero GenPl and PastAct participles(see rule below)
            ! хоро́ш>ому   па́льFец>ом   музе́й>ом   си́нь>ой   сча́стьFий>ом   ва́йя>ой   Гава́йй>ов
            ! хоро́ш0ему   па́ль00ц0ем   музе́00ем   си́н00ей   сча́ст000ь>ем   ва́й00ей   Гава́й00ев
            
"ы>и (stressed or unstressed)"
Vx:Vy <=> [ VELEnd | HUSHEnd | VEnd | SCEnd | JEnd | jEnd ] _ ;
            ! кни́га>ы   хоро́ш>ый   музе́й>ы
            ! кни́г00и   хоро́ш0ий   музе́00и
where Vx in ( ы ы́ )
      Vy in ( и и́ )
    matched;

"ю>у and я>а (stressed or unstressed) after a velar, husher, or ц"
Vx:Vy <=> [ VELEnd | HUSHEnd | цEnd | VerbHUSH ] _ ;
            ! слы́ш<ю   и́скM<я́   This rule does not seem to ever apply for nominals
            ! слы́ш0у   и0щ00а́
where Vx in ( ю ю́ я я́ )
      Vy in ( у у́ а а́ )
    matched;

"[а а́ о́] > [я я́ ё] after soft-consonant stems (including й: муравей, семья, бельё, etc) and vowel stems (музей, критерий, понятие, Мария, etc) including 1st conj verbs like читать, болеть, воевать, etc."
Vx:Vy <=> [ SCEnd | VEnd | JEnd | jEnd ] _ [ ?* - [ [ в:й | в:0 ] ?* ] ] .#. ;
          1ConjV _ ; 
            
where Vx in ( а а́ о́ )
      Vy in ( я я́ ё )
    matched;

"у>ю (1) after soft-consonant stems (including й: муравей, семья, бельё, etc) and vowel stems (музей, критерий, понятие, Мария, etc) including 1st conj verbs like читать, болеть, воевать, etc. (2) in 1st conjugation after л or ь"
Vx:Vy <=> [ SCEnd | VEnd | JEnd | jEnd ] _ [ ?* - [ [ в:й | в:0 ] ?* ] ] .#. ;
          [ 1ConjV | 1ConjJ ] _ ;
          :л %<:0 _ ; ! Verbs in ~роть have ю in the lexc output
where Vx in ( у у́ )
      Vy in ( ю ю́ )
    matched;

"е>и after и-stems (e.g. о Марии)"
е:и <=> [ и | и́ ] [ я́:0 | ё:0 | е́:0 | я:0 | е:0 | й:0 ]  %>:0 _ .#. ;

"в>й: GenPl ей"
в:й <=> [ HUSHEndC | SCEndC ] [ о: | о́: ] _ .#. ;
        [ HUSHEndV | SCEndV ] о́: _ .#. ;
"о́>е́: GenPl stressed е́й"
о́:е́ <=> [ HUSHEndC | HUSHEndV | SCEndC | SCEndV ] _ в:й .#. ;

"в>0: (1) GenPl zero and (2) Masc past tense endings or past deverbals, after consonant stems"
в:0 <=> [ VELEndV | цEndV | HCEndV | VEndV | JEndV | jEndV ] [ о: | о́: ] _ .#. ;
        HUSHEndV о: _ .#. ;
        SCEndV о: _ .#. ;
        !C: %<:0 _ ( с я ) .#. ; ! Past verbal adverb (handled by lexc)
        !C: %<:0 _ (ш и ( с :ь ) ) .#. ; ! Past verbal adverb reflexive (handled by lexc)
        C: %<:0 _ ш %>:0 V: [ C (V) | V ] ( с я ) .#. ; ! Past active ppl TODO BROKEN?
"о>ь: GenPl zero after soft C"
о:ь <=> SCEndVNoN _ в:0 .#. ; ! However, Fem 2*a in ~ня go to 0, not ь (characterized by F V н я)
        \[%^F:0] V: н: я:0 %>:0 _ в:0 .#. ; ! However, Fem 2*a in н, e.g. башня~башен
        C: н: [ е́:0 | е:0 | ё:0 | я́:0 ] %>:0 _ в:0 .#. ;
"о/о́>й: GenPl zero after V"
[ о:й | о́:й ] <=> VEndV _ в:0 .#. ;

"г>ж (1) with comparative(-ее)/superlative(-ейший) adjectives (2) with ^M symbol (verbs) (3) in pres/fut before е/ё"
г:ж <=> _ %>:0 [ е | е́: ] [ е: | й: ] ( ш: V: Letter: (V:) ) .#. ;
          _ %^M: ;
          _ %<:0 [ е: | ё: ] ;

"х>ш (1) with comparative(-ее)/superlative(-ейший) adjectives (2) with ^M symbol (verbs)"
х:ш <=> _ %>:0 [ е | е́: ] [ е: | й: ] ( ш: V: Letter: (V:) ) .#. ;
          _ %^M: ;

"к>ч Velar mutation (1) with comparative(-ее)/superlative(-ейший) adjectives (2) with ^M symbol (verbs) (as opposed to ск>щ) (3) in pres/fut before е/ё"
к:ч <=> _ %>:0 [ е | е́: ] [ е: | й: ] ( ш: V: Letter: (V:) ) .#. ;
        \с:0 _ %^M: ;
        _ %<:0 [ е: | ё: ] ;

"т>ч (1) Pres/Fut mutation of т (as opposed to ст>щ); (2) гть/кть > 0чь"
т:ч <=> \с: _ %^M: ;
        [ г: | к: ] %<:0 _ ь ( с я ) .#. ;

"г/к>0 before infinitive ending: гть/кть > чь"
Cx:0 <=> _ %<:0 т: ь ( с я ) .#. ;
where Cx in ( г к ) ;

"ё>е́ in some infinitives (чь and [бдт...]сть)"
ё:е́ <=> _ [ г: | к: ] %<:0 т: ь ( с я ) .#. ;
        _ [ б: | д: | с | (с) т:с ] %<:0 т ь ( с я ) .#. ;

"д/т>0 before past tense"
Cx:0 <=> _ %<:0 л ( [ V ( с :ь ) | с [ :я | :я́ ] ] ) .#.  ;
where Cx in ( д т ) ;

"[бдт]>с in before infinitive endings"
Cx:с <=> _ %<:0 т ( [ и́ ( с :ь ) | ь ( с я ) ] ) .#.  ;
         $[ :в :ы́ ] _ %<:0 т ( и ( с :ь ) ) .#.  ;
where Cx in ( б д т ) ;

"с>0 before т>щ and к>щ stem mutations"
с:0 <=> _ [ к: | т: ] %^M: ;

"с[ к т ]>щ Mutation of ск and ст"
Cx:щ <=> с:0 _ %^M: ;
where Cx in ( к т ) ;

"[ з с д ] > [ ж ш ж ] Verb stem mutations with ^M symbol (г к х covered in another rule)"
Cx:Cy <=> _ %^M: ;
where Cx in ( д з с )
      Cy in ( ж ж ш )
      matched ;

"^M>л Addition of л after labials in 'mutation' with ^M symbol (verbs)"
%^M:л <=> [ б: | п: | в: | ф: | м: ] _ ;

"л>0 in Masc past tense after consonant stems"
л:0 <=> CNoDT: %<:0 _ ( с я ) .#. ;

"Is>0 in imperatives with й stems (e.g. бить, вить, лить, пить, шить)"
%^Is:0 <=> й %<:0 _ (т е) (с [ :я | :ь ]) .#. ;

"Is>и́ in imperatives with consonant stems"
%^Is:и́ <=> [ :C - :й ] (%^M:) %<:0 _ (т е) (с [ :я | :ь ]) .#. ;

"Is/Iu>й in Imperatives with vowel stems"
Ex:й <=> :V %<:0 _ (т е) (с [ :я | :ь ]) .#. ;
where Ex in ( %^Iu %^Is ) ;

"Iu>и in Imperatives with double-consonant stems, and щ stems"
%^Iu:и <=> :C (:ь) [ :C - :й ] (%^M:0) %<:0 _ (т е) (с [ :я | :ь ]) .#. ;
           :щ (%^M:0) %<:0 _ (т е) (с [ :я | :ь ]) .#. ; !Is this true? Examples?
!           .#. ( :п :о ) :в :ы́ [ :Letter | %^F:0 ]*-:V :C (%^M:0) %<:0 _ (т е) (с [ :я | :ь ]) .#. ; ! BROKEN handled in lexc
                ! по́мн<I   обры́скM<I  мо́рщ<I
                ! по́мн0и   обры́0щ00и  мо́рщ0и

"Iu>ь in Imperatives with consonant stem (exceptions in previous rule)"
%^Iu:ь <=> :V [ :C - [ :щ | :г | :й ] ] (%^M:0) %<:0 _ (т е) (с [ :я | :ь ]) .#. ;
                ! отве́т<I
                ! отве́т0ь

"Iu>0 in Imperatives with velar stem, e.g. лечь"
%^Iu:0 <=> :я́ :г (%^M:0) %<:0 _ (т е) (с [ я | ь ]) .#. ;
           й %<:0 _ (т е) (с [ :я | :ь ]) .#. ;

"я>ь (i.e. ся>сь) in reflexive imperatives"
я:ь <=> %<:0 [ :и | :и́ ] с: _ .#. ;
        %<:0 [ :и | :и́ | :ь | :й ] т е с _ .#. ;

!If one wants to test specific two-level rules one can add test pairs to the twolc files. Support for this type of testing has only recently been added, and works only for the Xerox tools (because the pair-testing facilities in Hfst use a different format, and converting from one to the other is non-trivial without knowledge of the alphabet).
!The test data looks like the following: (€ for positive, $ for negative)

!€ рабо́та>у
!€ рабо́т00у
!$ рабо́та>у
!$ рабо́та00
