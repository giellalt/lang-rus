! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

! ================= !
! The Russian clock !
! ================= !


! 5:00 пять часов
! 6:20 два́дцать минут седьмого
! 9:30 половина десятого
! 9:30 полдесятого
! 4:45 без четверти пять
! 4:45 без пятна́дцати пять

! 6:20 два́дцать минут седьмого



Multichar_Symbols

@U.TIME.1@
@U.TIME.2@
@U.TIME.3@
@U.TIME.4@
@U.TIME.5@
@U.TIME.6@
@U.TIME.7@
@U.TIME.8@
@U.TIME.9@
@U.TIME.10@
@U.TIME.11@
@U.TIME.12@

+Use/NG
+Use/NA


LEXICON Root

BEFORE ; ! :31-59
!HALF ;
AFTER ; ! :1-29

LEXICON BEFORE
:без%  BEFOREHOURS ;
 
LEXICON BEFOREHOURS
     %0: beforesmallhours ;
!+Use/NA: beforesmallhours ;
         beforesmallhours ;
         beforelargehours ;
 
LEXICON beforesmallhours
@U.TIME.2@1:@U.TIME.2@     BEFpunkt ;    !1:31 - 01:59
@U.TIME.3@2:@U.TIME.3@     BEFpunkt ;    !2:31 - 02:59
@U.TIME.4@3:@U.TIME.4@     BEFpunkt ;    !3:31 - 03:59
@U.TIME.5@4:@U.TIME.5@     BEFpunkt ;    !4:31 - 04:59
@U.TIME.6@5:@U.TIME.6@     BEFpunkt ;    !5:31 - 05:59
@U.TIME.7@6:@U.TIME.7@     BEFpunkt ;    !6:31 - 06:59
@U.TIME.8@7:@U.TIME.8@     BEFpunkt ;    !7:31 - 07:59
@U.TIME.9@8:@U.TIME.9@     BEFpunkt ;    !8:31 - 08:59
@U.TIME.10@9:@U.TIME.10@   BEFpunkt ;    !9:31 - 09:59
 
LEXICON beforelargehours
@U.TIME.11@1%0:@U.TIME.11@ BEFpunkt ; !10:31 - 10:59
@U.TIME.12@11:@U.TIME.12@ BEFpunkt ; !11:31 - 11:59
@U.TIME.1@12:@U.TIME.1@ BEFpunkt ; !12:31 - 12:59
@U.TIME.2@13:@U.TIME.2@ BEFpunkt ; !13:31 - 13:59
@U.TIME.3@14:@U.TIME.3@ BEFpunkt ; !14:31 - 14:59
@U.TIME.4@15:@U.TIME.4@ BEFpunkt ; !15:31 - 15:59
@U.TIME.5@16:@U.TIME.5@ BEFpunkt ; !16:31 - 16:59
@U.TIME.6@17:@U.TIME.6@ BEFpunkt ; !17:31 - 17:59
@U.TIME.7@18:@U.TIME.7@ BEFpunkt ; !18:31 - 18:59
@U.TIME.8@19:@U.TIME.8@ BEFpunkt ; !19:31 - 19:59
@U.TIME.9@2%0:@U.TIME.9@ BEFpunkt ; !20:31 - 20:59
@U.TIME.10@21:@U.TIME.10@ BEFpunkt ; !21:31 - 21:59
@U.TIME.11@22:@U.TIME.11@ BEFpunkt ; !22:31 - 22:59
@U.TIME.12@23:@U.TIME.12@ BEFpunkt ; !23:31 - 23:59
@U.TIME.1@24:@U.TIME.1@ BEFpunkt ; !00:31 - 00:59
 
 
LEXICON AFTER
     %0: aftersmallhours ;
         aftersmallhours ;
         afterlargehours ;
 
LEXICON aftersmallhours
@U.TIME.1@1:@U.TIME.1@ AFTpunkt ; !01:00 - 01:29
@U.TIME.2@2:@U.TIME.2@ AFTpunkt ; !02:00 - 02:29
@U.TIME.3@3:@U.TIME.3@ AFTpunkt ; !03:00 - 03:29
@U.TIME.4@4:@U.TIME.4@ AFTpunkt ; !04:00 - 04:29
@U.TIME.5@5:@U.TIME.5@ AFTpunkt ; !05:00 - 05:29
@U.TIME.6@6:@U.TIME.6@ AFTpunkt ; !06:00 - 06:29
@U.TIME.7@7:@U.TIME.7@ AFTpunkt ; !07:00 - 07:29
@U.TIME.8@8:@U.TIME.8@ AFTpunkt ; !08:00 - 08:29
@U.TIME.9@9:@U.TIME.9@ AFTpunkt ; !09:00 - 09:29

LEXICON afterlargehours
@U.TIME.10@1%0:@U.TIME.10@ AFTpunkt ; !10:00 - 10:29
@U.TIME.11@11:@U.TIME.11@ AFTpunkt ; !11:00 - 11:59
@U.TIME.12@12:@U.TIME.12@ AFTpunkt ; !12:00 - 12:29
@U.TIME.1@13:@U.TIME.1@ AFTpunkt ; !13:00 - 13:29
@U.TIME.2@14:@U.TIME.2@ AFTpunkt ; !14:00 - 114:29
@U.TIME.3@15:@U.TIME.3@ AFTpunkt ; !15:00 - 15:29
@U.TIME.4@16:@U.TIME.4@ AFTpunkt ; !16:00 - 16:29
@U.TIME.5@17:@U.TIME.5@ AFTpunkt ; !17:00 - 17:29
@U.TIME.6@18:@U.TIME.6@ AFTpunkt ; !18:00 - 18:29
@U.TIME.7@19:@U.TIME.7@ AFTpunkt ; !19:00 - 19:29
@U.TIME.8@2%0:@U.TIME.8@ AFTpunkt ; !20:00 - 20:29
@U.TIME.9@21:@U.TIME.9@ AFTpunkt ; !21:00 - 21:29
@U.TIME.10@22:@U.TIME.10@ AFTpunkt ; !22:00 - 22:29
@U.TIME.11@23:@U.TIME.11@ AFTpunkt ; !23:00 - 23:29
@U.TIME.12@%0%0:@U.TIME.12@ AFTpunkt ; !00:00 - 00:29
24%:%0%0:два́дцать% четы́ре% часа́ # ; !24:00
24.%0%0+Use/NG:два́дцать% четы́ре% часа́ # ; !24:00
24%:%0%0+Use/NG:по́лночь # ; !24:00
24.%0%0+Use/NG:по́лночь # ; !24:00

LEXICON BEFpunkt
!%:+Use/NA: BEF ;
!%.+Use/NA: BEF ;
%:: BEF ;
%.+Use/NG: BEF ;
NOMHOUR ;
  
LEXICON AFTpunkt
!%:+Use/NA: AFT ; ! 2.00 = göökte
!%.+Use/NA: AFT ; ! 2.00 = göökte
!+Use/NA: HOUR ; ! 2 = göökte
%:: AFT ; 
%.+Use/NG: AFT ; 
 
LEXICON BEF
31:двадцати́% девяти́%  TO ;
32:двадцати́% восьми́%  TO ;
33:двадцати́% семи́%  TO ;
34:двадцати́% шести́%  TO ;
35:двадцати́% пяти́%  TO ;
36:двадцати́% четырёх%  TO ;
37:двадцати́% трёх%  TO ;
38:двадцати́% двух%  TO ; 
39:двадцати́% одно́й%  TO ;
4%0:двадцати́%  TO ;
41:девятна́дцати%  TO ;
42:восемна́дцати%  TO ;
43:семна́дцати%  TO ;
44:шестна́дцати%  TO ;
45:че́тверти%  TO ;
46:четы́рнадцати%  TO ;
47:трина́дцати%  TO ;
48:двена́дцати%   TO ;
49:оди́ннадцати%   TO ;
5%0:десяти́%   TO ;
51:девяти́%   TO ;
52:восьми́%  TO ;
53:семи́%  TO ;
54:шести́%  TO ;
55:пяти́%  TO ; 
56:четырёх%  TO ; 
57:трёх%  TO ;
58:двух%  TO ; 
59:одно́й% мину́ты%  NOMHOUR ;
59+Use/NG:мину́ты%  NOMHOUR ;
 
LEXICON AFT  
%0%0: NOMHOUR ; 
%01:одна́%  1OVER ;
%01+Use/NG:мину́та%  HOUR ;
%02:две%  2to4OVER ;
%03:три%  2to4OVER ;
%04:четы́ре%  2to4OVER ;
%05:пять%  OVER ;
%06:шесть%  OVER ;
%07:семь%  OVER ;
%08:во́семь%  OVER ;
%09:де́вять%  OVER ;
1%0:де́сять%  OVER ;
11:оди́ннадцать%  OVER ;
12:двена́дцать%  OVER ;
13:трина́дцать%  OVER ;
14:четы́рнадцать%  OVER ;
15:че́тверть%  15OVER ;
16:шестна́дцать%  OVER ;
17:семна́дцать%  OVER ;
18:восемна́дцать%  OVER ;
19:девятна́дцать%  OVER ;
2%0:два́дцать%  OVER ;
21:два́дцать% одна́%  1OVER ;
22:два́дцать% две%  2to4OVER ;
23:два́дцать% три%  2to4OVER ;
24:два́дцать% четы́ре%  2to4OVER ;
25:два́дцать% пять%  OVER ; 
26:два́дцать% шесть%  OVER ; 
27:два́дцать% семь%  OVER ;
28:два́дцать% во́семь%  OVER ; 
29:два́дцать% де́вять%  OVER ;
3%0:полови́на%  HOUR ;
 

LEXICON TO ! 
:мину́т%  NOMHOUR ;
+Use/NG: NOMHOUR ;

LEXICON NOMHOUR !^N^
@U.TIME.1@:@U.TIME.1@час # ; !^N^
@U.TIME.2@:@U.TIME.2@два # ; !^N^
@U.TIME.3@:@U.TIME.3@три # ; !^N^
@U.TIME.4@:@U.TIME.4@четы́ре # ; !^N^
@U.TIME.5@:@U.TIME.5@пять # ; !^N^
@U.TIME.6@:@U.TIME.6@шесть # ; !^N^
@U.TIME.7@:@U.TIME.7@семь # ; !^N^
@U.TIME.8@:@U.TIME.8@во́семь # ; !^N^
@U.TIME.9@:@U.TIME.9@де́вять # ; !^N^
@U.TIME.10@:@U.TIME.10@де́сять # ; !^N^
@U.TIME.11@:@U.TIME.11@оди́ннадцать # ; !^N^
@U.TIME.12@:@U.TIME.12@двена́дцать # ; !^N^
 

LEXICON 1OVER
:мину́та%  HOUR ;
 
LEXICON 2to4OVER
:мину́ты%  HOUR ;
 
LEXICON OVER
:мину́т%  HOUR ;
 
LEXICON 15OVER
HOUR ;
 
LEXICON HOUR
GENHOUR ; !^N^
 
LEXICON GENHOUR !^N^
@U.TIME.1@:@U.TIME.1@второ́го # ; !^N^
@U.TIME.2@:@U.TIME.2@тре́тьего # ; !^N^
@U.TIME.3@:@U.TIME.3@четвёртого # ; !^N^
@U.TIME.4@:@U.TIME.4@пя́того # ; !^N^
@U.TIME.5@:@U.TIME.5@шесто́го # ; !^N^
@U.TIME.6@:@U.TIME.6@седьмо́го # ; !^N^
@U.TIME.7@:@U.TIME.7@восьмо́го # ; !^N^
@U.TIME.8@:@U.TIME.8@девя́того # ; !^N^
@U.TIME.9@:@U.TIME.9@деся́того # ; !^N^
@U.TIME.10@:@U.TIME.10@оди́ннадцатого # ; !^N^
@U.TIME.11@:@U.TIME.11@двена́дцатого # ; !^N^
@U.TIME.12@:@U.TIME.12@пе́рвого # ; !^N^
 
 

