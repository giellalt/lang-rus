! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ================= !
! The Russian clock !
! ================= !


! 5:00 пять часов
! 6:20 два́дцать минут седьмого
! 9:30 половина десятого
! 9:30 полдесятого
! 4:45 без четверти пять
! 4:45 без пятна́дцати пять

! 6:20 два́дцать минут седьмого



Multichar_Symbols

@U.TIME.1@
@U.TIME.2@
@U.TIME.3@
@U.TIME.4@
@U.TIME.5@
@U.TIME.6@
@U.TIME.7@
@U.TIME.8@
@U.TIME.9@
@U.TIME.10@
@U.TIME.11@
@U.TIME.12@

+Use/NG
+Use/NA


LEXICON Root

BEFORE ; ! 31-59
!HALF ;
REST ; ! 1-30

LEXICON BEFORE
без% : BEFOREHOURS ;
 
LEXICON BEFOREHOURS
:%0 beforesmallhours ;
:+Use/NA beforesmallhours ;
beforelargehours ;
 
LEXICON beforesmallhours
@U.TIME.2@:1@U.TIME.2@ BEFpunkt ; !01:31 - 01:59
@U.TIME.3@:2@U.TIME.3@ BEFpunkt ; !02:31 - 02:59
@U.TIME.4@:3@U.TIME.4@ BEFpunkt ; !03:31 - 03:59
@U.TIME.5@:4@U.TIME.5@ BEFpunkt ; !04:31 - 04:59
@U.TIME.6@:5@U.TIME.6@ BEFpunkt ; !05:31 - 05:59
@U.TIME.7@:6@U.TIME.7@ BEFpunkt ; !06:31 - 06:59
@U.TIME.8@:7@U.TIME.8@ BEFpunkt ; !07:31 - 07:59
@U.TIME.9@:8@U.TIME.9@ BEFpunkt ; !08:31 - 08:59
@U.TIME.10@:9@U.TIME.10@ BEFpunkt ; !09:31 - 09:59
 
LEXICON beforelargehours
@U.TIME.11@:1%0@U.TIME.11@ BEFpunkt ; !10:31 - 10:59
@U.TIME.12@:11@U.TIME.12@ BEFpunkt ; !11:29 - 11:59
@U.TIME.1@:12@U.TIME.1@ BEFpunkt ; !12:29 - 12:59
@U.TIME.2@:13@U.TIME.2@ BEFpunkt ; !13:29 - 13:59
@U.TIME.3@:14@U.TIME.3@ BEFpunkt ; !14:29 - 14:59
@U.TIME.4@:15@U.TIME.4@ BEFpunkt ; !15:29 - 15:59
@U.TIME.5@:16@U.TIME.5@ BEFpunkt ; !16:29 - 16:59
@U.TIME.6@:17@U.TIME.6@ BEFpunkt ; !17:29 - 17:59
@U.TIME.7@:18@U.TIME.7@ BEFpunkt ; !18:29 - 18:59
@U.TIME.8@:19@U.TIME.8@ BEFpunkt ; !19:31 - 19:59
@U.TIME.9@:2%0@U.TIME.9@ BEFpunkt ; !20:31 - 20:59
@U.TIME.10@:21@U.TIME.10@ BEFpunkt ; !21:31 - 21:59
@U.TIME.11@:22@U.TIME.11@ BEFpunkt ; !22:29 - 22:59
@U.TIME.12@:23@U.TIME.12@ BEFpunkt ; !23:29 - 23:59
@U.TIME.1@:%0%0@U.TIME.1@ BEFpunkt ; !00:29 - 00:59
 
LEXICON REST
@U.TIME.1@:%01@U.TIME.1@ AFTpunkt ; !01:00 - 01:29
@U.TIME.2@:%02@U.TIME.2@ AFTpunkt ; !02:00 - 02:29
@U.TIME.3@:%03@U.TIME.3@ AFTpunkt ; !03:00 - 03:29
@U.TIME.4@:%04@U.TIME.4@ AFTpunkt ; !04:00 - 04:29
@U.TIME.5@:%05@U.TIME.5@ AFTpunkt ; !05:00 - 05:29
@U.TIME.6@:%06@U.TIME.6@ AFTpunkt ; !06:00 - 06:29
@U.TIME.7@:%07@U.TIME.7@ AFTpunkt ; !07:00 - 07:29
@U.TIME.8@:%08@U.TIME.8@ AFTpunkt ; !08:00 - 08:29
@U.TIME.9@:%09@U.TIME.9@ AFTpunkt ; !09:00 - 09:29
@U.TIME.10@:1%0@U.TIME.10@ AFTpunkt ; !10:00 - 10:29
@U.TIME.11@:11@U.TIME.11@ AFTpunkt ; !11:00 - 11:59
@U.TIME.12@:12@U.TIME.12@ AFTpunkt ; !12:00 - 12:29
@U.TIME.1@:13@U.TIME.1@ AFTpunkt ; !13:00 - 13:29
@U.TIME.2@:14@U.TIME.2@ AFTpunkt ; !14:00 - 114:29
@U.TIME.3@:15@U.TIME.3@ AFTpunkt ; !15:00 - 15:29
@U.TIME.4@:16@U.TIME.4@ AFTpunkt ; !16:00 - 16:29
@U.TIME.5@:17@U.TIME.5@ AFTpunkt ; !17:00 - 17:29
@U.TIME.6@:18@U.TIME.6@ AFTpunkt ; !18:00 - 18:29
@U.TIME.7@:19@U.TIME.7@ AFTpunkt ; !19:00 - 19:29
@U.TIME.8@:2%0@U.TIME.8@ AFTpunkt ; !20:00 - 20:29
@U.TIME.9@:21@U.TIME.9@ AFTpunkt ; !21:00 - 21:29
@U.TIME.10@:22@U.TIME.10@ AFTpunkt ; !22:00 - 22:29
@U.TIME.11@:23@U.TIME.11@ AFTpunkt ; !23:00 - 23:29
@U.TIME.12@:%0%0@U.TIME.12@ AFTpunkt ; !00:00 - 00:29
:24%:%0%0 # ; !24:00
 
LEXICON BEFpunkt
:%:+Use/NA BEF ;
:%.+Use/NA BEF ;
 
LEXICON AFTpunkt
:%:+Use/NA AFT ; ! 2.00 = göökte
:%.+Use/NA AFT ; ! 2.00 = göökte
:+Use/NA HOUR ; ! 2 = göökte
 
LEXICON BEF
два́дцати% девя́ти% :31 TO ;
два́дцати% восьми́% :32 TO ;
два́дцати% семи́% :33 TO ;
два́дцати% шести́% :34 TO ;
два́дцати% пяти́% :35 TO ;
два́дцати% четырёх% :36 TO ;
два́дцати% трёх% :37 TO ;
два́дцати% двух% :38 TO ; 
два́дцати% одно́й% :39 TO ;
два́дцати% :4%0 TO ;
девятна́дцати% :41 TO ;
восемна́дцати% :42 TO ;
семна́дцати% :43 TO ;
шестна́дцати% :44 TO ;
четверти% :45 TO ;
четырна́дцати% :46 TO ;
трина́дцати% :47 TO ;
двена́дцати% :48 TO ;
одинна́дцати% :49 TO ;
деся́ти% :5%0 TO ;
девя́ти% :51 TO ;
восьми́% :52 TO ;
семи́% :53 TO ;
шести́% :54 TO ;
пяти́% :55 TO ; 
четырёх% :56 TO ; 
трёх% :57 TO ;
двух% :58 TO ; 
одно́й% :59 TO ;
мину́ты% :59+Use/NG TO ;
 
LEXICON AFT
:%0%0 HOUR ;
одна% :%01 1OVER ;
минут% :%01+Use/NG OVER ;
две% :%02 2to4OVER ;
три% :%03 2to4OVER ;
четыре% :%04 2to4OVER ;
пять% :%05 OVER ;
шесть% :%06 OVER ;
семь% :%07 OVER ;
восемь% :%08 OVER ;
девять% :%09 OVER ;
десять% :1%0 OVER ;
однана́дцать% :11 OVER ;
двена́дцать% :12 OVER ;
трина́дцать% :13 OVER ;
четырна́дцать% :14 OVER ;
четве́рть% :15 15OVER ;
шестна́дцать% :16 OVER ;
семна́дцать% :17 OVER ;
восемна́дцать% :18 OVER ;
девятна́дцать% :19 OVER ;
два́дцать% :2%0 OVER ;
два́дцать% % :21 1OVER ;
два́дцать% две% :22 2to4OVER ;
два́дцать% три% :23 2to4OVER ;
два́дцать% четыре% :24 2to4OVER ;
два́дцать% пять% :25 OVER ; 
два́дцать% шесть% :26 OVER ; 
два́дцать% семь% :27 OVER ;
два́дцать% восемь% :28 OVER ; 
два́дцать% девять% :29 OVER ;
полови́на% :3%0 HOUR ;
 

LEXICON TO ! BEFORE
час@U.TIME.1@:@U.TIME.1@ # ; !^N^
два@U.TIME.2@:@U.TIME.2@ # ; !^N^
три@U.TIME.3@:@U.TIME.3@ # ; !^N^
четыре@U.TIME.4@:@U.TIME.4@ # ; !^N^
пять@U.TIME.5@:@U.TIME.5@ # ; !^N^
шесть@U.TIME.6@:@U.TIME.6@ # ; !^N^
семь@U.TIME.7@:@U.TIME.7@ # ; !^N^
восемь@U.TIME.8@:@U.TIME.8@ # ; !^N^
девять@U.TIME.9@:@U.TIME.9@ # ; !^N^
десять@U.TIME.10@:@U.TIME.10@ # ; !^N^
одинна́дцать@U.TIME.11@:@U.TIME.11@ # ; !^N^
двена́дцать@U.TIME.12@:@U.TIME.12@ # ; !^N^
 
 
LEXICON 1OVER
минута% : HOUR ;
 
LEXICON 2to4OVER
мину́ты% : HOUR ;
 
LEXICON OVER
минут% : HOUR ;
 
LEXICON 15OVER
HOUR ;
 
LEXICON HOUR
NOMHOUR ; !^N^
 
LEXICON NOMHOUR !^N^
второго@U.TIME.1@:@U.TIME.1@ # ; !^N^
третьего@U.TIME.2@:@U.TIME.2@ # ; !^N^
четвёртого@U.TIME.3@:@U.TIME.3@ # ; !^N^
пятого@U.TIME.4@:@U.TIME.4@ # ; !^N^
шестого@U.TIME.5@:@U.TIME.5@ # ; !^N^
седьмого@U.TIME.6@:@U.TIME.6@ # ; !^N^
восьмого@U.TIME.7@:@U.TIME.7@ # ; !^N^
девятого@U.TIME.8@:@U.TIME.8@ # ; !^N^
десятого@U.TIME.9@:@U.TIME.9@ # ; !^N^
одинна́дцатого@U.TIME.10@:@U.TIME.10@ # ; !^N^
двена́дцатого@U.TIME.11@:@U.TIME.11@ # ; !^N^
первого@U.TIME.12@:@U.TIME.12@ # ; !^N^
 
 

